:GetButtonFlanks
// Inputs:
// 0@: Type of press - FLAG_PAD_PRESS / FLAG_KEY_PRESS
// 1@: button id
// 2@: button press state - ST_PRESSED / ST_NOT_PRESSED
const
    ST_NOT_PRESSED = 0
    ST_PRESSED = 1
    
    Gbf_TypeOfPress = 0@
    Gbf_ButtonId = 1@
    Gbf_BtnPressState = 2@

    Gbf_MoveAxisX = 3@
    Gbf_MoveAxisY = 4@
    Gbf_SpecialAxisX = 5@
    Gbf_SpecialAxisY = 6@

    Gbf_FlankUp = 7@
    Gbf_FlankDown = 8@
end

if Gbf_TypeOfPress == FLAG_PAD_PRESS
then
    if Gbf_BtnPressState == ST_NOT_PRESSED
    then
        if Gbf_ButtonId > 1
        then
            if 00E1:   player 0 pressed_key Gbf_ButtonId
            then 0AB2: ret 4 ST_PRESSED 1 0 0
            end
        else
            0494: get_joystick 0 data_to Gbf_MoveAxisX Gbf_MoveAxisY Gbf_SpecialAxisX Gbf_SpecialAxisY
            if Gbf_ButtonId == 0 // Left or Right
            then
                if or
                -100 > Gbf_MoveAxisX
                Gbf_MoveAxisX > 100
                then 0AB2: ret 4 ST_PRESSED 1 0 Gbf_MoveAxisX
                end
            end
            if Gbf_ButtonId == 1 // Up or Down
            then
                if or
                -100 > Gbf_MoveAxisY
                Gbf_MoveAxisY > 100
                then 0AB2: ret 4 ST_PRESSED 1 0 Gbf_MoveAxisY
                end
            end
        end
    end
    if Gbf_BtnPressState == ST_PRESSED
    then
        if Gbf_ButtonId > 1
        then
            if 80E1:   not player 0 pressed_key Gbf_ButtonId
            then 0AB2: ret 4 ST_NOT_PRESSED 0 1 0
            end
        else
            0494: get_joystick 0 data_to Gbf_MoveAxisX Gbf_MoveAxisY Gbf_SpecialAxisX Gbf_SpecialAxisY
            if Gbf_ButtonId == 0 // Left or Right
            then
                if Gbf_MoveAxisX == 0
                then 0AB2: ret 4 ST_NOT_PRESSED 0 1 Gbf_MoveAxisX
                end
            end
            if Gbf_ButtonId == 1 // Up or Down
            then
                if Gbf_MoveAxisY == 0
                then 0AB2: ret 4 ST_NOT_PRESSED 0 1 Gbf_MoveAxisY
                end
            end
        end
    end
else // 0@ == FLAG_KEY_PRESS
    if Gbf_BtnPressState == ST_NOT_PRESSED
    then
        if 0AB0:   key_pressed Gbf_ButtonId
        then 0AB2: ret 4 ST_PRESSED 1 0 0
        end
    end
    if Gbf_BtnPressState == ST_PRESSED
    then
        if 8AB0:   not key_pressed Gbf_ButtonId
        then 0AB2: ret 4 ST_NOT_PRESSED 0 1 0
        end
    end
end
0AB2: ret 4 Gbf_BtnPressState 0 0 0

