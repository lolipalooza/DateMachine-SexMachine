{$CLEO .s}
thread 'SexMachine'

{gosub @CmSexStart
if wasted_or_busted 
then gosub @CmSexEnd
end
gosub @CmSexCleanUp
end_thread}

gosub @CmSexStart
gosub @CmSexCleanUp
0A93: end_custom_thread

{======================================================================
Sexual positions, actors and cameras
====================================
Fucking in table  sex_fem1_xxxx / sex_male1_xxxx
----------------
Female: xyz 2372.198 -1132.888 1049.775 angle -90.0
Male: xyz 2372.195 -1132.905 1049.825 angle -90.0

Anal in table  anal_fem1_xxxx / anal_male1_xxxx
-------------
Female: xyz 2372.688 -1132.955 1049.905 angle -90.0
Male: xyz 2372.681 -1133.057 1049.975 angle -90.0

Handjob in sofa  hj_fem1_xxxx / hj_male1_xxxx
---------------
Female: xyz 2372.3 -1121.857 1049.415 angle 0.0
Male: xyz 2372.317 -1121.818 1049.475 angle 0.0

Cunnilingus in sofa   cunnilingus_f1 / cunnilingus_m1
-------------------
Female: xyz 2374.48 -1123.599 1049.475 angle -90.0
Male: xyz 2374.523 -1123.617 1049.535 angle -90.0

Blowjob sit down on bed   bj_femX / bj_maleX
-----------------------
Female: xyz 2359.824 -1132.73 1049.635 angle 90.0
Male: xyz 2359.758 -1132.749 1049.665 angle 90.0



Newer poses
========

Anal on Bed
-----------
Female: xyz 2359.059326 -1132.759766 1048.975464 angle 90.0
Male: xyz 2358.939209 -1132.706665 1048.875488 angle 90.0

Anal on Foot
------------
Female: xyz 2370.270752 -1128.774536 1048.875439 angle 270.0
Male: xyz 2370.299561 -1128.835815 1048.875439 angle 270.0

Anal on Knees
------------
Female: xyz 2362.56 -1122.103271 1049.575317 angle 90.0
Male: xyz 2362.493896 -1122.103271 1049.575317 angle 90.0

Anal on Sofa
------------
Female: xyz 2372.65 -1121.592871 1048.975464 angle 0.000000
Male: xyz 2372.709961 -1121.692871 1048.975464 angle 0.000000

Blowjob Kitchen
------------
Female: xyz 2373.919434 -1131.388306 1049.325317 angle -90.000000
Male: xyz 2374.219727 -1131.390015 1049.895264 angle -90.000000

Blowjob on Bed
------------
Female: xyz 2362.801514 -1122.472900 1049.895142 angle 90.000000
Male: xyz 2362.246582 -1122.476685 1049.875244 angle 90.000000

Blowjob on Knees
------------
Female: xyz 2374.494141 -1123.936768 1049.775269 angle 0.000000
Male: xyz 2374.494141 -1123.224121 1049.975220 angle 0.000000

Blowjob Sofa
------------
Female: xyz 2374.570801 -1123.796509 1049.625366 angle 270.000000
Male: xyz 2374.519531 -1123.158203 1049.475342 angle -90.000000

Cunnilingus Floor
------------
Female: xyz 2372.276367 -1126.481812 1049.075439 angle 90.000000
Male: xyz 2372.923828 -1126.484497 1049.435425 angle 90.000000

Footjob on Sofa
------------
Female: xyz 2372.270508 -1121.804321 1049.475342 angle 0.000000
Male: xyz 2372.970459 -1121.804321 1049.475342 angle 0.000000

Fucking Inverted
------------
Female: xyz 2372.755371 -1125.993896 1049.475342 angle -90.000000
Male: xyz 2372.905518 -1126.023926 1049.475342 angle -90.000000

Fucking on Floor
------------
Female: xyz 2373.205322 -1126.033936 1049.555420 angle -90.000000
Male: xyz 2372.905518 -1126.023926 1049.475342 angle -90.000000

Fucking on Foot
------------
Female: xyz 2369.647461 -1129.161133 1049.875122 angle -90.000000
Male: xyz 2369.757568 -1129.161133 1049.875122 angle -90.000000

Handjob on Bed
------------
Female: xyz 2362.458740 -1122.499878 1049.775269 angle 90.000000
Male: xyz 2361.908447 -1122.499878 1049.775269 angle 90.000000

Handjob on Foot
------------
Female: xyz 2372.667725 -1121.716553 1049.425415 angle 0.000000
Male: xyz 2372.695557 -1122.412842 1049.875244 angle 0.000000

Oral69 Sofa
------------
Female: xyz 2370.628906 -1123.881958 1049.745239 angle 90.000000
Male: xyz 2370.578369 -1123.440063 1049.475342 angle 90.000000

Sex Bed
------------
Female: xyz 2358.946289 -1132.789917 1049.855347 angle 90.000000
Male: xyz 2358.735352 -1132.803345 1049.675293 angle 90.000000

Sex Kitchen
------------
Female: xyz 2374.268066 -1131.571045 1049.975220 angle 90.000000
Male: xyz 2374.015137 -1131.575562 1049.925293 angle 90.000000

Sex Sofa
------------
Female: xyz 2374.267822 -1123.554688 1049.805420 angle 270.000000
Male: xyz 2374.386230 -1123.556152 1049.675293 angle -90.000000

Titjob on Bed 1
------------
Female:
Male:

Titjob on Bed 2
------------
Female: xyz 2363.973145 -1122.475464 1049.605347 angle 90.000000
Male: xyz 2363.566162 -1122.478760 1049.775269 angle 90.000000


======================================================================}

{======================================================================
======================================================================}
const
    SexMenuState = 0@
    ShowSexualStats = 1@
    OnSexActivity = 2@
    PornActress = 3@
    PornActor = 4@
    SexState = 5@
    SexStat_Climax = 6@   // (Float) Min: 1.0 - Max 100.0
    SexStat_Speed = 7@    // (Int) Min: 1 - Max 100
    SexStat_Pleasure = 8@ // (Int) From 1 to unlimited
    ClimaxState = 9@
    PleasureState = 10@
    SpeedState = 11@
    Dummy1 = 12@
    Dummy2 = 13@
    Dummy3 = 14@
    Dummy4 = 15@
    DummyString = 12@s
    Clmx_LstEvntTime = 16@
    Clmx_CurrEvntTime = 17@
    Plsr_LstEvntTime = 18@
    Plsr_CurrEvntTime = 19@
    Plsr_Gain = 20@
    SexPoseInt = 21@
    SexPlace = 22@
    PulseButtonState = 23@
    FemX = 24@
    FemY = 25@
    FemZ = 26@
    FemA = 27@
    MaleX = 28@
    MaleY = 29@
    MaleZ = 30@
    MaleA = 31@
    
    {===================================
    ====================================}
    FLAG_INCREASE = 1
    FLAG_DECREASE = 0
    
    FLAG_PAD_PRESS = 0
    FLAG_KEY_PRESS = 1

    FLAG_NORMAL_INT = 0
    FLAG_NORMAL_FLOAT = 1
    FLAG_PERCENTAGE_INT = 2
    FLAG_PERCENTAGE_FLOAT = 3
    
    FLAG_SX_MENU_LEFT = 0
    FLAG_SX_MENU_RIGHT = 1
    
    SEX_FUCKING = 0
    SEX_BJ = 1
    SEX_ANAL = 2
    SEX_HJ = 3
    SEX_CUNNI = 4
    SEX_69 = 5
    SEX_TJ = 6
    SEX_FJ = 7
    
    PL_BED = 0
    PL_SOFA = 1
    PL_TABLE = 2
    PL_CHAIR = 3
    PL_FLOOR = 4
    PL_BATH = 5
    PL_KITCHEN = 6
    PL_WALL = 7
    PL_FOOT = 8

    SPD_IDLE   = 0
    SPD_SLOW   = 1
    SPD_NORMAL = 2
    SPD_FAST   = 3
    
    SPD_THRESHOLD1 = 13 // Threshold to stop
    SPD_THRESHOLD2 = 52 // Threshold to pass from slow to normal
    SPD_THRESHOLD3 = 88 // Threshold to pass from normal to fast
    
    //IFP_FILE = "sex_event"
    //IFP_DEST_FILE = "modloader/2hugta/sex/sex_event.ifp"
    LOG_FILE = "modloader/2hugta/cleo/SexMachine.log"
                   
    // Sex Menu States
    SX_MENU_LEFT_FIRST_ELEM = 1
    SX_MENU_LEFT_LAST_ELEM = 3
    SX_MENU_OPT1_PLACES_FIRST_ELEM = 20
    SX_MENU_OPT1_PLACES_LAST_ELEM = 28
    SX_MENU_OPT2_POSITIONS_FIRST_ELEM = 40
    SX_MENU_OPT2_POSITIONS_LAST_ELEM = 47
    SX_MENU_OPT3_CAMERAS_FIRST_ELEM = 60
    SX_MENU_OPT3_CAMERAS_LAST_ELEM = 69

    // Pulsed button states
    NOT_PULSED = 0
    PULSED_UP = 10
    PULSED_DOWN = 20
    PULSED_ENTER_VEH = 30
    PULSED_SPRINT = 40
    PULSED_UP_F = 19
    PULSED_DOWN_F = 29
    PULSED_ENTER_VEH_F = 39
    PULSED_SPRINT_F = 49

    {===================================
    ====================================}
    PAD_CHANGE_CAMERA_VIEW = 13
    PAD_ANSWER_PHONE = 4
    PAD_PREV_WEP_ZOOM_IN = 5
    PAD_NEXT_WEP_ZOOM_OUT = 7
    PAD_ANSWER_PHONE = 4
    PAD_ENTER_VEHICLE = 15
    PAD_SPRINT = 16
    
    KEY_F6 = 117
    KEY_F3 = 114
    
    {===================================
    ====================================}
    KEY_TOGGLE_SEX_MACHINE = KEY_F6
end

{======================================================================
======================================================================}
{$I CmSexMachine/CamerasAndCoordinates.txt}
{$I CmSexMachine/ActorsAndPoses.txt}
{$I CmSexMachine/SexualMenuAndStats.txt}
{$I CmSexMachine/GenerateSexFiles.txt}

{======================================================================
======================================================================}
:CmSexStart
$ONMISSION = 1
OnSexActivity = false
gosub @ToggleSexMachine

while true
wait 0
    gosub @SexMachine
    if SexMenuState > 0
    then
        gosub @SexualMenuMachine
        gosub @ShowSexualMenu
    else
        if PulseButtonState == PULSED_ENTER_VEH
        then break // End Sex Activity
        end
    end
    if ShowSexualStats==true
    then 0AB1: @ShowSexualStats 3 SexStat_Climax SexStat_Speed SexStat_Pleasure
    end
end

:CmSexEnd
OnSexActivity = true
gosub @ToggleSexMachine
return

:CmSexCleanUp
$ONMISSION = 0
return

{======================================================================
======================================================================}
:SetDrawText
const
    DwTx_Font = 0@
    DwTx_Linewidth = 1@
    DwTx_SizeX = 2@
    DwTx_SizeY = 3@
    DwTx_PosX = 4@
    DwTx_PosY = 5@
    DwTx_gxt1 = 6@
    DwTx_gxt2 = 7@
    DwTx_Gxt = 6@s
end
0349: set_text_draw_font DwTx_Font
0343: set_text_draw_linewidth DwTx_Linewidth
033F: set_text_draw_letter_size DwTx_SizeX DwTx_SizeY
033E: set_draw_text_position DwTx_PosX DwTx_PosY GXT DwTx_Gxt
0AB2: ret 0

:SetDrawTextRGB
0340: set_text_draw_RGBA 8@ 9@ 10@ 255
0349: set_text_draw_font DwTx_Font
0343: set_text_draw_linewidth DwTx_Linewidth
033F: set_text_draw_letter_size DwTx_SizeX DwTx_SizeY
033E: set_draw_text_position DwTx_PosX DwTx_PosY GXT DwTx_Gxt
0AB2: ret 0

{======================================================================
======================================================================}
:IncOrDecIntVariable
const
    IncOrDec_Variable = 0@
    IncOrDec_MinValue = 1@
    IncOrDec_MaxValue = 2@
    IncOrDec_Flag = 3@
end
if 0039:   IncOrDec_Flag == FLAG_INCREASE
then
    if 002D:   IncOrDec_Variable >= IncOrDec_MaxValue  // (int)
    then 0085: IncOrDec_Variable = IncOrDec_MinValue // (int)
    else IncOrDec_Variable++
    end
else // FLAG_DECREASE
    if 002D:   IncOrDec_MinValue >= IncOrDec_Variable  // (int)
    then 0085: IncOrDec_Variable = IncOrDec_MaxValue // (int)
    else IncOrDec_Variable--
    end
end
0AB2: ret 1 IncOrDec_Variable

{======================================================================
======================================================================}
:ToggleSexMachine
if OnSexActivity == false
then
    fade 0 time 800
    wait 800 ms
    01B4: set_player $PLAYER_CHAR can_move 0
    0581: enable_radar 0
    0826: enable_hud 0
    03F0: enable_text_draw 1
    0AB1: @StorePlayerData 0
    077E: get_active_interior_to Dummy1
    if Dummy1 == 0
    then
        04BB: select_interior 8
        0860: link_actor $PLAYER_ACTOR to_interior 8
        00A1: put_actor $PLAYER_ACTOR at 2364.344 -1136.4468 1051.0762
        0619: enable_actor $PLAYER_ACTOR collision_detection 0
        04E4: unknown_refresh_game_renderer_at 2364.344 -1136.4468
        03CB: set_rendering_origin_at 2364.344 -1136.4468 1051.0762
        //04F9: set_interior 8 color_to 0
    end
    SexPoseInt = SEX_FUCKING
    SexPlace = PL_TABLE
    //0209: SexPoseInt = random_int_in_ranges ? ?
    0AB1: call_scm_func @GetActorsCoordinates 2 SexPoseInt SexPlace ret FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA
    0AB1: call_scm_func @GenerateActors 8 FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA ret PornActress PornActor
    0AB1: call_scm_func @GenerateSexFiles 2 SexPoseInt SexPlace
    0AB1: call_scm_func @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
    0AB1: call_scm_func @ToggleSexualCamera 2 SexPoseInt SexPlace
    OnSexActivity = true
    SexState = 0
    ShowSexualStats = true
    SexStat_Climax = 0.0
    SexStat_Speed = 40
    SexStat_Pleasure = 1
    ClimaxState = 0
    PleasureState = 0
    PulseButtonState = NOT_PULSED
    wait 500
    fade 1 time 800
    wait 500
    {
    03E5: show_text_box 'I_CTRL1'  // Puedes cambiar la vista con ~b~~k~~CAMERA_CHANGE_VIEW_ALL_SITUATIONS~~w~. Ingresa al menu pulsando ~b~~k~~PED_ANSWER_PHONE~~w~ para ver las diferentes opciones.
    wait 7000
    03E5: show_text_box 'I_GNRL1'  // Puedes aumentar el ritmo de los movimientos con ~b~~k~~PED_CYCLE_WEAPON_LEFT~~w~ / ~b~~k~~PED_CYCLE_WEAPON_RIGHT~~w~. Tambien puedes parar totalmente disminuyendo el ritmo a su minimo valor.
    }
    0ACA: show_text_box "~b~~k~~PED_ANSWER_PHONE~: ~s~ingresar al menu. ~b~~k~~VEHICLE_ENTER_EXIT~: ~s~salir."
else
    fade 0 time 800
    wait 800 ms
    0581: enable_radar 1
    0826: enable_hud 1
    03F0: enable_text_draw 0
    0AB1: @RecoverPlayerPosition 0 Dummy1 Dummy2 Dummy3
    0AB1: @RecoverPlayerInterior 0 Dummy4
    04BB: select_interior Dummy4
    0619: enable_actor $PLAYER_ACTOR collision_detection 1
    0860: link_actor $PLAYER_ACTOR to_interior Dummy4
    00A1: put_actor $PLAYER_ACTOR at Dummy1 Dummy2 Dummy3
    0373: set_camera_directly_behind_player
    02EB: restore_camera_with_jumpcut
    009B: destroy_actor PornActress
    009B: destroy_actor PornActor
    01B4: set_player $PLAYER_CHAR can_move 1
    OnSexActivity=false
    SexMenuState=0
    ShowSexualStats=false
    wait 500
    fade 1 time 800
end
return

{======================================================================
======================================================================}
:SexMachine
if SexState == 0
then
    if 00E1:   player 0 pressed_key PAD_CHANGE_CAMERA_VIEW // V
    then SexState=1
        0AB1: call_scm_func @ToggleSexualCamera 2 SexPoseInt SexPlace
    end
    if 00E1:   player 0 pressed_key PAD_ANSWER_PHONE // TAB
    then SexState=2
        gosub @ToggleSexualMenu
    end
end
if SexState == 1
then
    if 80E1:   not player 0 pressed_key PAD_CHANGE_CAMERA_VIEW
    then SexState=0
    end
end
if SexState == 2
then
    if 80E1:   not player 0 pressed_key PAD_ANSWER_PHONE
    then SexState=0
    end
end

// Increase / Decrease Speed
if 00E1:   player 0 pressed_key PAD_PREV_WEP_ZOOM_IN
then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_INCREASE SexStat_Speed
end
if 00E1:   player 0 pressed_key PAD_NEXT_WEP_ZOOM_OUT
then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_DECREASE SexStat_Speed
end

// Increase Pleasure
gosub @PleasureMachine

// Increase Climax
gosub @ClimaxMachine

// Change from stopped to slow or fast
gosub @SpeedMachine

// Buttons UP - DOWN - ACCEPT - CANCEL acknowledge
gosub @PulseButtonMachine
return

{======================================================================
======================================================================}
:PleasureMachine
if PleasureState == 0
then 0209: Plsr_Gain = random_int_in_ranges 1 4
end
if not SexStat_Speed > 13
then return
end
0AB1: call_scm_func @IncreaseTimedVar 7 SexStat_Pleasure Plsr_Gain PleasureState 800 FLAG_NORMAL_INT Plsr_LstEvntTime Plsr_CurrEvntTime SexStat_Pleasure PleasureState Plsr_LstEvntTime Plsr_CurrEvntTime
if PleasureState == 2
then PleasureState=0
end
return

{======================================================================
======================================================================}
:ClimaxMachine
// Speed: min: 13 - max: 100
// steps: 0.3 - min: 1 - max: 250
// steps: 0.4 - min: 1 - max: 500
// Ecuacion de la recta: Time (ms) = -5,7356321839080459770114942528736 * Speed + 574,56321839080459770114942528736
if SexStat_Speed > 13
then
    0093: Dummy1 = integer SexStat_Speed to_float
    Dummy1 *= -5.735
    Dummy1 += 574.563
    0092: Dummy1 = float Dummy1 to_integer
    Dummy2 = 0.4
else
    Dummy1 = 100
    Dummy2 = -0.4
end
0AB1: call_scm_func @IncreaseTimedVar 7 SexStat_Climax Dummy2 ClimaxState Dummy1 FLAG_PERCENTAGE_FLOAT Clmx_LstEvntTime Clmx_CurrEvntTime SexStat_Climax ClimaxState Clmx_LstEvntTime Clmx_CurrEvntTime
if ClimaxState == 2
then ClimaxState=0
end
return

{======================================================================
======================================================================}
:ToggleSexualMenu
if SexMenuState == 0
then SexMenuState = 1 // Show and start from initial state
else SexMenuState = 0 // Hide sexual menu
end
return

{======================================================================
======================================================================}
:IncOrDecPercentageIntStat
const
    PtSt_Stat = 0@
    PtSt_Amount = 1@
    PtSt_IncOrDec = 2@
end
if PtSt_IncOrDec == FLAG_INCREASE
then
    005A: PtSt_Stat += PtSt_Amount  // (int)
    if PtSt_Stat > 100
    then 0AB2: ret 1 100
    end
else // FLAG_DECREASE
    0062: PtSt_Stat -= PtSt_Amount  // (int)
    if 1 > PtSt_Stat
    then 0AB2: ret 1 1
    end
end
0AB2: ret 1 PtSt_Stat

{======================================================================
======================================================================}
:IncOrDecPercentageFloatStat
const
    FtSt_Stat = 0@
    FtSt_Amount = 1@
    FtSt_IncOrDec = 2@
end
if FtSt_IncOrDec == FLAG_INCREASE
then 005B: FtSt_Stat += FtSt_Amount  // (float)
else 0062: FtSt_Stat -= FtSt_Amount  // (int)
end
if FtSt_Stat > 100.0
then 0AB2: ret 1 100.0
end
if 1.0 > FtSt_Stat
then 0AB2: ret 1 1.0
end
0AB2: ret 1 FtSt_Stat

{======================================================================
======================================================================}
:IncreaseTimedVar
const
    IcTm_Var = 0@
    IcTm_Amount = 1@
    IcTm_Stat = 2@
    IcTm_msec = 3@
    IcTm_PercentageFlag = 4@
    IcTm_LastTime = 5@
    IcTm_CurrentTime = 6@
end
if IcTm_Stat == 0
then
    0085: IcTm_LastTime = 32@ // (int)
    005A: IcTm_LastTime += IcTm_msec  // (int)
    IcTm_Stat=1
end
if IcTm_Stat == 1
then
    0085: IcTm_CurrentTime = 32@ // (int)
    if 002D:   IcTm_CurrentTime >= IcTm_LastTime  // (int)
    then
        if IcTm_PercentageFlag == FLAG_NORMAL_INT
        then 005A: IcTm_Var += IcTm_Amount  // (int)
        end
        if IcTm_PercentageFlag == FLAG_NORMAL_FLOAT
        then 005B: IcTm_Var += IcTm_Amount  // (float)
        end
        if IcTm_PercentageFlag == FLAG_PERCENTAGE_INT
        then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 IcTm_Var IcTm_Amount FLAG_INCREASE IcTm_Var
        end
        if IcTm_PercentageFlag == FLAG_PERCENTAGE_FLOAT
        then 0AB1: call_scm_func @IncOrDecPercentageFloatStat 3 IcTm_Var IcTm_Amount FLAG_INCREASE IcTm_Var
        end
        IcTm_Stat=2
    end
end
0AB2: ret 4 IcTm_Var IcTm_Stat IcTm_LastTime IcTm_CurrentTime

{======================================================================
======================================================================}
:SpeedMachine
if SpeedState == 0 // IDLE State
then
    if SexStat_Speed > SPD_THRESHOLD1
    then
        SpeedState=1
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
    end
end
if SpeedState == 1 // SLOW state
then
    if SexStat_Speed < SPD_THRESHOLD1
    then
        SpeedState=0
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_IDLE
    end
    if SexStat_Speed > SPD_THRESHOLD2
    then
        SpeedState=2
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_NORMAL
    end
end
if SpeedState == 2 // NORMAL state
then
    if SexStat_Speed < SPD_THRESHOLD2
    then
        SpeedState=1
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
    end
    if SexStat_Speed > SPD_THRESHOLD3
    then
        SpeedState=3
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_FAST
    end
end
if SpeedState == 3 // FAST state
then
    if SexStat_Speed < SPD_THRESHOLD3
    then
        SpeedState=2
        0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_NORMAL
    end
end
return

{======================================================================
======================================================================}
:SexualMenuMachine
// Is current selector on left menu items ?
if and
0029:   SexMenuState >= SX_MENU_LEFT_FIRST_ELEM
002B:   SX_MENU_LEFT_LAST_ELEM >= SexMenuState  // (int)
then
    if PulseButtonState == PULSED_UP
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState 1 3 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState 1 3 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then SexMenuState *= 20
        PulseButtonState++
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = 0 // Close Sexual Menu
    end
end

// Is current selector on opt 1 right menu items ?
if and
0029:   SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
002B:   SX_MENU_OPT1_PLACES_LAST_ELEM >= SexMenuState  // (int)
then
    if PulseButtonState == PULSED_UP
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState increase_from SX_MENU_OPT1_PLACES_FIRST_ELEM to SX_MENU_OPT1_PLACES_LAST_ELEM FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState increase_from SX_MENU_OPT1_PLACES_FIRST_ELEM to SX_MENU_OPT1_PLACES_LAST_ELEM FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then
        0085: Dummy1 = SexMenuState // (int)
        Dummy1 -= SX_MENU_OPT1_PLACES_FIRST_ELEM
        0AB1: @GetRightSelectedElement 2 SX_MENU_OPT1_PLACES_FIRST_ELEM Dummy1 ret Dummy3 Dummy4
        0AB1: call_scm_func @GetPlaceFromString 2 Dummy3 Dummy4 ret Dummy1
        if 0AB1: @CheckPoseAndPlaceAvailability 2 SexPoseInt Dummy1
        then
            if 803B:   not Dummy1 == SexPlace  // (int)
            then
                0085: SexPlace = Dummy1 // (int)
                fade 0 time 800
                wait 800 ms
                009B: destroy_actor PornActress
                009B: destroy_actor PornActor
                0AB1: call_scm_func @GetActorsCoordinates 2 SexPoseInt SexPlace ret FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA
                0AB1: call_scm_func @GenerateActors 8 FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA ret PornActress PornActor
                0AB1: call_scm_func @GenerateSexFiles 2 SexPoseInt SexPlace
                0AB1: call_scm_func @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
                0AB1: call_scm_func @ToggleSexualCamera 2 SexPoseInt SexPlace
                wait 500
                fade 1 time 800
            end
        end
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

// Is current selector on opt 2 right menu items ?
if and
0029:   SexMenuState >= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
002B:   SX_MENU_OPT2_POSITIONS_LAST_ELEM >= SexMenuState  // (int)
then
    if PulseButtonState == PULSED_UP
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState increase_from SX_MENU_OPT2_POSITIONS_FIRST_ELEM to SX_MENU_OPT2_POSITIONS_LAST_ELEM FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState increase_from SX_MENU_OPT2_POSITIONS_FIRST_ELEM to SX_MENU_OPT2_POSITIONS_LAST_ELEM FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then
        0085: Dummy1 = SexMenuState // (int)
        Dummy1 -= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
        0AB1: @GetRightSelectedElement 2 SX_MENU_OPT2_POSITIONS_FIRST_ELEM Dummy1 ret Dummy3 Dummy4
        0AB1: call_scm_func @GetPoseFromString 2 Dummy3 Dummy4 ret Dummy1
        if 0AB1: @CheckPoseAndPlaceAvailability 2 Dummy1 SexPlace
        then
            if 803B:   not Dummy1 == SexPoseInt  // (int)
            then
                0085: SexPoseInt = Dummy1 // (int)
                fade 0 time 800
                wait 800 ms
                009B: destroy_actor PornActress
                009B: destroy_actor PornActor
                0AB1: call_scm_func @GetActorsCoordinates 2 SexPoseInt SexPlace ret FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA
                0AB1: call_scm_func @GenerateActors 8 FemX FemY FemZ FemA MaleX MaleY MaleZ MaleA ret PornActress PornActor
                0AB1: call_scm_func @GenerateSexFiles 2 SexPoseInt SexPlace
                0AB1: call_scm_func @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
                0AB1: call_scm_func @ToggleSexualCamera 2 SexPoseInt SexPlace
                wait 500
                fade 1 time 800
            end
        end
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

// Is current selector on opt 3 right menu items ?
if and
0029:   SexMenuState >= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
002B:   SX_MENU_OPT3_CAMERAS_LAST_ELEM >= SexMenuState  // (int)
then
    if PulseButtonState == PULSED_UP
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState 60 69 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: @IncOrDecIntVariable 4 SexMenuState 60 69 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then // TODO
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

if or
PulseButtonState == PULSED_UP
PulseButtonState == PULSED_DOWN
PulseButtonState == PULSED_SPRINT
PulseButtonState == PULSED_ENTER_VEH
then PulseButtonState++
end
return

{======================================================================
======================================================================}
:PulseButtonMachine
const
    MoveAxisX = Dummy1
    MoveAxisY = Dummy2
    SpAxisX = Dummy3
    SpAxisY = Dummy4
end

0494: get_joystick 0 data_to MoveAxisX MoveAxisY SpAxisX SpAxisY

if PulseButtonState == NOT_PULSED
then
    if 0018:   MoveAxisY > 100 // UP
    then PulseButtonState = PULSED_UP
    end
    if 001A:   -100 > MoveAxisY // DOWN
    then PulseButtonState = PULSED_DOWN
    end
    if 00E1:   player 0 pressed_key PAD_SPRINT
    then PulseButtonState = PULSED_SPRINT
    end
    if 00E1:   player 0 pressed_key PAD_ENTER_VEHICLE
    then PulseButtonState = PULSED_ENTER_VEH
    end
end

if and
PULSED_UP_F >= PulseButtonState
PulseButtonState > PULSED_UP
then
    if 8018:   not MoveAxisY > 100 // UP
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_DOWN_F >= PulseButtonState
PulseButtonState > PULSED_DOWN
then
    if 801A:   not -100 > MoveAxisY // DOWN
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_SPRINT_F >= PulseButtonState
PulseButtonState > PULSED_SPRINT
then
    if 80E1:   not player 0 pressed_key PAD_SPRINT
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_ENTER_VEH_F >= PulseButtonState
PulseButtonState > PULSED_ENTER_VEH
then
    if 80E1:   not player 0 pressed_key PAD_ENTER_VEHICLE
    then PulseButtonState = NOT_PULSED
    end
end
return

