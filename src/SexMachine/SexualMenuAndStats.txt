{======================================================================
======================================================================}
:ShowSexualMenu
// Pink Box Title
038E: draw_box_position 199.0 106.0 size 347.0 43.0 RGBA 255 147 251 80
038E: draw_box_position 206.0 111.0 size 346.0 43.0 RGBA 255 147 251 40
038E: draw_box_position 201.0 105.0 size 323.0 52.0 RGBA 255 147 251 70
038E: draw_box_position 202.0 115.0 size 355.0 11.0 RGBA 255 147 251 80
DummyString = 'CFG_T7' // Change position
0AB1: @SetDrawText 8 font 0 linewidth 600.0 size 0.6 2.5 position 66.0 92.0 GXT Dummy1 Dummy2

// Black boxes options
038E: draw_box_position 318.0 250.0 size 592.0 262.0 RGBA 0 0 0 80
038E: draw_box_position 318.0 250.0 size 607.0 275.0 RGBA 0 0 0 50
038E: draw_box_position 326.0 250.0 size 593.0 288.0 RGBA 0 0 0 80
038E: draw_box_position 596.0 250.0 size 51.0 276.0 RGBA 0 0 0 100
038E: draw_box_position 300.0 369.0 size 541.0 23.0 RGBA 0 0 0 100
038E: draw_box_position 318.0 350.0 size 607.0 16.0 RGBA 255 255 255 10
038E: draw_box_position 149.0 223.0 size 222.0 179.0 RGBA 0 0 0 100
038E: draw_box_position 438.0 236.0 size 333.0 204.0 RGBA 0 0 0 100

// The "choose" pose button box
038E: draw_box_position 545.0 420.0 size 154.0 30.0 RGBA 255 147 251 60
038E: draw_box_position 538.0 414.0 size 154.0 30.0 RGBA 255 147 251 50
038E: draw_box_position 541.0 416.0 size 139.0 48.0 RGBA 255 147 251 60
038E: draw_box_position 542.0 422.0 size 161.0 6.0 RGBA 255 147 251 100
DummyString = 'S_OPT4' // Change!
0AB1: @SetDrawText 8 font 2 linewidth 640.0 size 0.3 1.5 position 524.0 409.0 GXT Dummy1 Dummy2
DummyString = 'S_OPT5' // Cancel
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT Dummy1 Dummy2

0AB1: @SexualMenu_GetContent 0
0AB1: @SexualMenu_CheckBoxes 2 SexPlace SexPoseInt
return

{======================================================================
======================================================================}
:SexualMenu_GetContent
0AB1: @GetInt 1 @SxMn_Place 0@
// Places (Left Menu)
for 11@ = 1 to 3
    for 10@ = 1 to 3
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 3 ret 20@ // i = x + (y-1)*3 - 1
        20@+=1
        0AD3: 12@v = format "S_PL_%d" 20@
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 56.0 offset 64.0 ret 20@ // xpos = 56 + (x-1)*64
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 152.0 offset 38.0 ret 21@ // ypos = 152 + (y-1)*38
        if 10@ == 1
        then 14@=120.0
        end
        if 10@ == 2
        then 14@=180.0
        end
        if 10@ == 3
        then 14@=260.0
        end
        0AB1: @SetDrawText 8 font 2 linewidth 14@ size 0.2 1.2 position 20@ 21@ GXT 12@ 13@
    end
end
for 11@ = 1 to 5
    for 10@ = 1 to 5
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 5 ret 20@ // i = x + (y-1)*5 - 1
        0AB1: @SxMn_GetGXTPose 2 place 0@ offset 20@ ret 12@ 13@
        if 85AE:   not 12@s == "______" // @s == 'short'
        then
            0AB1: @SxMn_GetCoord 3 index 10@ start_point 276.0 offset 66.0 ret 20@ // xpos = 276 + (x-1)*66
            0AB1: @SxMn_GetCoord 3 index 11@ start_point 152.0 offset 38.0 ret 21@ // ypos = 152 + (y-1)*38
            if 10@ == 1
            then 14@=340.0
            end
            if 10@ == 2
            then 14@=420.0
            end
            if 10@ == 3
            then 14@=480.0
            end
            if 10@ == 4
            then 14@=540.0
            end
            if 10@ == 5
            then 14@=600.0
            end
            0AB1: @SetDrawText 8 font 2 linewidth 14@ size 0.2 1.2 position 20@ 21@ GXT 12@ 13@
        end
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:SxMn_GetGXTPose // 0@: place, 1@: pose
0A9A: 2@ = openfile SEXPLACES_FILE mode "rb"  // IF and SET

// Get interior name
repeat
    0ADA: 3@ = scan_file 2@ format "%s" 4@v //IF and SET
    if 4@v == "SVLAMD"
    then break
    end
until 0AD6:   end_of_file 2@ reached // To do: handle cases when string is not present in file

for 4@ = 0 to 2
    0ADA: 3@ = scan_file 2@ format "%s %s %s" 10@v 14@v 18@v //IF and SET
    for 5@ = 0 to 4
        for 6@ = 0 to 2
            0ADA: 3@ = scan_file 2@ format "%s %s %s %s %s" 10@s 12@s 14@s 16@s 18@s //IF and SET
            0B14: 22@ = 0@ MOD 3
            0085: 23@ = 0@ // (int)
            0016: 23@ /= 3
            if and
            003B:   22@ == 6@  // (int)              // place % 3    === 6@ (block column)
            003B:   23@ == 4@  // (int)              // place / 3    === 4@ (block)
            then
                for 7@ = 0 to 4
                    0B14: 24@ = 1@ MOD 5
                    0085: 25@ = 1@ // (int)
                    0016: 25@ /= 5
                    if and
                    003B:   24@ == 7@  // (int)      // pose % 5    === 7@ (line column)
                    003B:   25@ == 5@  // (int)      // pose / 5    === 7@ (line)
                    then
                        05AA: 30@s = 10@(7@,5s) // @s = 'short'
                        0A9B: closefile 2@
                        0AB2: ret 2 gxt_entry 30@ 31@
                    end
                end
            end
        end
    end
end
0A9B: closefile 2@
0AB2: ret 2 gxt_entry 10@ 11@

{======================================================================
======================================================================}
:SxMn_GetIntegerFromArray // 0@: x,     1@: y,      2@: width       // i = x + (y-1)*width - 1
0085: 10@ = 1@ // (int)
000E: 10@ -= 1
006A: 10@ *= 2@  // (int)
005A: 10@ += 0@  // (int)
000E: 10@ -= 1 // i = x + (y-1)*5 - 1
0AB2: ret 1 10@

:SxMn_GetCoord  // 0@: index,   1@: start point,    2@: offset      // xpos = start + (x-1)*offset
0093: 10@ = integer 0@ to_float
000F: 10@ -= 1.0
006B: 10@ *= 2@  // (float)
005B: 10@ += 1@  // (float)
0AB2: ret 1 10@

{======================================================================
======================================================================}
:SexualMenu_CheckBoxes // 0@ place, 1@: pose
0AB1: @GetInt 1 @SxMn_Place 30@
0AB1: @GetInt 1 @SxMn_Pose 31@
for 11@ = 1 to 3
    for 10@ = 1 to 3
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 3 ret 20@ // i = x + (y-1)*3 - 1
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 85.0 offset 65.0 ret 21@ // xpos = 85 + (x-1)*65
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 159.0 offset 39.0 ret 22@ // ypos = 159 + (y-1)*39
        if 003B:   20@ == 30@  // (int)
        then 038E: draw_box_position 21@ 22@ size 62.0 36.0 RGBA 0 0 160 120 // highlight selected
        end
        if 0AB1: @CursorClickCheck 4 box_position 21@ 22@ box_size 62.0 36.0
        then
            0085: 30@ = 20@ // (int)
            0006: 31@ = 0
        end
    end
end
for 11@ = 1 to 5
    for 10@ = 1 to 5
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 5 ret 20@ // i = x + (y-1)*5 - 1
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 306.0 offset 66.0 ret 21@ // xpos = 306 + (x-1)*66
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 159.0 offset 39.0 ret 22@ // ypos = 159 + (y-1)*39
        if 003B:   20@ == 31@  // (int)
        then 038E: draw_box_position 21@ 22@ size 62.0 36.0 RGBA 0 0 160 120 // highlight selected
        end
        0AB1: @SxMn_GetGXTPose 2 place 30@ offset 20@ ret 12@ 13@
        if 85AE:   not 12@s == "______" // @s == 'short'
        then
            if 0AB1: @CursorClickCheck 4 box_position 21@ 22@ box_size 62.0 36.0
            then 0085: 31@ = 20@ // (int)
            end
        end
    end
end
0AB1: @StoreInt 2 @SxMn_Place 30@
0AB1: @StoreInt 2 @SxMn_Pose 31@
0AB2: ret 0

:SxMn_Place
hex
    0
end

:SxMn_Pose
hex
    0
end

{======================================================================
======================================================================}
:GetSpeedStatFromBarClick // 1 speed_stat 0@
if 0AB1: @CursorClickCheck 4 box_position 296.0 436.0 box_size 114.0 12.0
then
    0AB1: @GetClickPosition 0 click_x 10@ click_y 11@

    // percentage clicked = (click_x - xmin)*100/(xmax - xmin)       xmax = 296.0 + 114.0/2      xmin = 296.0 - 114.0/2
    19@ = 114.0
    19@ /= 2.0
    
    20@ = 296.0
    0063: 20@ -= 19@  // (float)  xmin
    
    21@ = 296.0
    005B: 21@ += 19@  // (float)  xmax
    
    0063: 21@ -= 20@  // (float)  xmax - xmin
    
    0087: 30@ = 10@     // (float)      click_x
    0063: 30@ -= 20@    // (float)      click_x - xmin
    0013: 30@ *= 100.0  //              (click_x - xmin)*100
    0073: 30@ /= 21@    // (float)      (click_x - xmin)*100/(xmax - xmin)
    
    0092: 30@ = float 30@ to_integer
    if 30@ < 1
    then 30@ = 1
    end
else 0085: 30@ = 0@ // (int)
end
0AB2: ret 1 speed_stat 30@ // speed_stat: (Int) Min: 1 - Max 100

{======================================================================
    Orgasm-o-meter bar                        Speed-o-meter bar
    min -> posX 170.5 sizeX 1.0               min -> posX 243.5 sizeX 1.0
    min+1 -> posX 171.0 sizeX 2.0             min+1 -> posX 244.0 sizeX 2.0
    min+2 -> posX 171.5 sizeX 3.0             min+2 -> posX 244.5 sizeX 3.0
    min+3 -> posX 172.0 sizeX 4.0             min+3 -> posX 245.0 sizeX 4.0
    100% -> posX 260.0 sizeX 180.0            100% -> posX 296.0 sizeX 106.0
======================================================================}
:ShowSexualStats
const
    Flt_Climax = 0@
    ShSt_Speed = 1@
    ShSt_Pleasure = 2@
    Flt_Speed = 4@
    Climax_PosBar = 5@
    Climax_SizeBar = 6@
    Speed_PosBar = 7@
    Speed_SizeBar = 8@
end
0093: Flt_Speed = integer ShSt_Speed to_float

//Climax_SizeBar = 1.8 * Flt_Climax
//Climax_PosBar = Climax_SizeBar/2 + 170
0087: Climax_SizeBar = Flt_Climax // (float)
0013: Climax_SizeBar *= 1.8
0087: Climax_PosBar = Climax_SizeBar // (float)
0013: Climax_PosBar *= 0.5
000B: Climax_PosBar += 170.0

//Speed_SizeBar = 1.06 * Flt_Speed
//Speed_PosBar = Speed_SizeBar/2 + 243
0087: Speed_SizeBar = Flt_Speed // (float)
0013: Speed_SizeBar *= 1.06
0087: Speed_PosBar = Speed_SizeBar // (float)
0013: Speed_PosBar *= 0.5
000B: Speed_PosBar += 243.0

//Black styled climax bar
{038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 261.0 419.0 size 186.0 13.0 RGBA 0 0 0 80
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 0 0 0 250}

//Pink styled climax bar
038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 238 71 181 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 419.0 size 186.0 13.0 RGBA 0 0 0 150
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 238 71 181 60 // the orgasm-o-meter bar
038E: draw_box_position Climax_PosBar 411.0 size Climax_SizeBar 3.0 RGBA 255 255 255 50 // the orgasm-o-meter bar 2

// Speedometer
038E: draw_box_position 257.0 436.0 size 1.0 15.0 RGBA 153 153 153 180 // Medium Gray
038E: draw_box_position 298.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 337.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 296.0 436.0 size 114.0 12.0 RGBA 238 71 181 80 // Pink
038E: draw_box_position 296.0 436.0 size 110.0 8.0 RGBA 0 0 0 140 // Black
038E: draw_box_position Speed_PosBar 436.0 size Speed_SizeBar 4.0 RGBA 255 0 0 250 // Red // the speed-o-meter bar

// Shadow on text "pleasure"
038E: draw_box_position 203.0 436.0 size 72.0 12.0 RGBA 0 0 0 120
    
0349: set_text_draw_font 2
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
033E: set_draw_text_position 179.0 409.0 GXT 'S_HUD1' // Climax

0349: set_text_draw_font 1
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
045A: draw_text_1number 171.0 430.0 GXT 'S_HUD2' number ShSt_Pleasure  // Pleasure: ~1~%
0AB2: ret 0

{======================================================================
======================================================================}
:IncDecActorOffset // 0@: gender flag, 1@: increase or decrease, 2@: position offset
if 0@ == 0 // female
then
    0AB1: @GetData 2 @FemaleOffset 2@ out 30@
    005B: 30@ += 1@  // (float)
    0AB1: @StoreData 3 @FemaleOffset 30@ 2@
end
if 0@ == 1 // male
then
    0AB1: @GetData 2 @MaleOffset 2@ out 30@
    005B: 30@ += 1@  // (float)
    0AB1: @StoreData 3 @MaleOffset 30@ 2@
end
if 0@ == 2 // both
then
    0AB1: @GetData 2 @MaleOffset 2@ out 30@
    005B: 30@ += 1@  // (float)
    0AB1: @StoreData 3 @MaleOffset 30@ 2@
    0AB1: @GetData 2 @FemaleOffset 2@ out 30@
    005B: 30@ += 1@  // (float)
    0AB1: @StoreData 3 @FemaleOffset 30@ 2@
end
0AB2: ret 1 true

{======================================================================
======================================================================}
:MaleOffset
hex
00 00 00 00
00 00 00 00
00 00 00 00
00 00 00 00
end
:FemaleOffset
hex
00 00 00 00
00 00 00 00
00 00 00 00
00 00 00 00
end
:ActorsOffsetsMenu_Action
hex
0
end
:ActorsOffsetsMenu_OffsetToModify
hex
0
end
:ActorsOffsetsMenu_AngleOfView
hex
00 00 00 00
end

{======================================================================
======================================================================}
:ActorsOffsets_SetFixedCamera // 0@: actor or object
//CamPosX = Xactor - Xoffset*sin(ActorAngle)
//CamPosY = Yactor + Yoffset*cos(ActorAngle)

if 056D:   actor 0@ defined
then 00A0: store_actor 0@ position_to 10@ 11@ 12@
end
if 03CA:   object 0@ exists
then 01BB: store_object 0@ position_to 10@ 11@ 12@
end
0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 13@

0087: 20@ = 13@ // (float)
02F6: 20@ = sine 20@ // (float)
20@*=-2.0
005B: 20@ += 10@  // (float)

0087: 21@ = 13@ // (float)
02F7: 21@ = cosine 21@ // (float)
21@*=2.0
005B: 21@ += 11@  // (float)

0087: 22@ = 12@ // (float)
22@+=1.0

015F: set_camera_position 20@ 21@ 22@ rotation 0.0 0.0 0.0
0160: set_camera_point_at 10@ 11@ 12@ switchstyle 2

0AB2: ret 0

{======================================================================
======================================================================}
:ActorsOffsetsMenu // 0@: SexMenuState, 1@: Porn Actress, 2@: Porn Actor, 3@: pose, 4@: place, 5@: speed state
const
    OFF_INC_STEP = 0.01
    OFF_DEC_STEP = -0.01
end

0AB1: @GetInt 1 @ActorsOffsetsMenu_OffsetToModify 20@
0AB1: @GetInt 1 @ActorsOffsetsMenu_Action 30@

// Set fixed camera
0AB1: @ActorsOffsets_SetFixedCamera 1 1@

// Change angle of view
10@s = 'CFG_D7' // <
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 170.0 389.0 GXT 10@ 11@
10@s = 'CFG_C3' // Change Angle of View
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 183.0 390.0 GXT 10@ 11@
10@s = 'CFG_D8' // >
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 286.0 389.0 GXT 10@ 11@
if 0AB1: @CursorClickCheck 4 box_position 175.0 395.0 box_size 16.0 10.0 // <
then
    0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
    12@ += 90.0
    0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
end
if 0AB1: @CursorClickCheck 4 box_position 289.0 395.0 box_size 16.0 10.0 // >
then
    0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
    12@ -= 90.0
    0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
end

10@s = 'CFSV_1' // Save
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 414.0 GXT 10@ 11@
10@s = 'CFSV_2' // Load
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 48.0 414.0 GXT 10@ 11@
10@s = 'CFSV_3' // Delete
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 83.0 414.0 GXT 10@ 11@
if not 0@ == 4
then
end

if 0@ == 4
then 10@s = 'CFSV_7' // Back
else 10@s = 'S_OPT6' // Close
end
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT 10@ 11@

if 0AB1: @CursorClickCheck 4 box_position 23.0 421.0 box_size 35.0 12.0 // Save
then 0AB1: @SetNumbers 2 sex_menu_state 4 action_to_execute_for_next_submenu 0 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
end
if 0AB1: @CursorClickCheck 4 box_position 60.0 421.0 box_size 35.0 12.0 // Load
then 0AB1: @SetNumbers 2 sex_menu_state 4 action_to_execute_for_next_submenu 1 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
end
if 0AB1: @CursorClickCheck 4 box_position 100.0 421.0 box_size 41.0 12.0 // Delete
then 0AB1: @SetNumbers 2 sex_menu_state 4 action_to_execute_for_next_submenu 2 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
end
if 0AB1: @CursorClickCheck 4 box_position 52.0 436.0 box_size 93.0 12.0 // Close or Back
then
    if 0@ == 4
    then 0@ = 3 // Back to previous
    else
        0@ = 0 // Close (Reset SexMenuState)
        0AB1: @RelocateActor_Alt 5 actor 1@ gender 0 pose 3@ place 4@ move_player 1
        0AB1: @RelocateActor_Alt 5 actor 2@ gender 1 pose 3@ place 4@ move_player 1
        0AB1: @LoadSexualPose 5 fem 1@ male 2@ pose 3@ place 4@ speed 5@
        0373: set_camera_directly_behind_player
        02EB: restore_camera_with_jumpcut
    end
end

if 0@ == 4
then
    16@ = 0
    for 13@ = 332.0 to 388.0 step 28.0
        for 12@ = 11.0 to 131.0 step 40.0
            16@++
            0AB1: @ReadLineFromIntStorage 1 slot_to_read 16@ returned_values interior 6@ 7@ 8@ 9@ pose 10@ place 10@ center_xyz 10@ 10@ 10@ fem_xyza 10@ 10@ 10@ 10@ male_xyza 10@ 10@ 10@ 10@ slot 10@ in_use 10@
            
            10@s = 'CFSV_4' // Slot ~1~
            0AB1: @SetDrawTextRGB_1Number 12 font 1 linewidth 600.0 size 0.2 1.2 position 12@ 13@ GXT 10@ 11@ rgb 255 255 255 integer 16@
            
            if not 6@v == ""
            then
                0087: 15@ = 13@ // (float)
                000B: 15@ += 8.0
    			10@s = 'CFSV_5' // Data!
                0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 12@ 15@ GXT 10@ 11@
            end
            
            0087: 14@ = 12@ // (float)
            000B: 14@ += 14.0
            0087: 15@ = 13@ // (float)
            000B: 15@ += 10.0
            if 0AB1: @CursorClickCheck 4 box_position 14@ 15@ box_size 38.0 26.0
            then
                if 30@ == 0 // Save
                then 0AB1: @StoreInformationInInternalStorage 3 slot 16@ pose 3@ place 4@
                end
                if 30@ == 1 // Load
                then 0AB1: @RetrieveInformationFromInternalStorage 6 slot 16@ fem 1@ male 2@ pose 3@ place 4@ speed 5@
                end
                if 30@ == 2 // Delete
                then
                    0AB1: @ClearStorageLine 1 line 16@
                    0AB1: @ClearTempStorage 0
                    0ACE: show_formatted_text_box "Deleted!"
                end
            end
        end
    end
    0AB1: @StoreInt 2 @ActorsOffsetsMenu_Action 30@
    0AB2: ret 1 sex_menu_state 0@
else
    10@s = 'CFG_O6' // Reset
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 128.0 414.0 GXT 10@ 11@
    if 0AB1: @CursorClickCheck 4 box_position 142.0 421.0 box_size 39.0 12.0 // Reset
    then
        0AB1: @StoreData 3 @MaleOffset 0.0 0
        0AB1: @StoreData 3 @MaleOffset 0.0 4
        0AB1: @StoreData 3 @MaleOffset 0.0 8
        0AB1: @StoreData 3 @MaleOffset 0.0 12
        0AB1: @StoreData 3 @FemaleOffset 0.0 0
        0AB1: @StoreData 3 @FemaleOffset 0.0 4
        0AB1: @StoreData 3 @FemaleOffset 0.0 8
        0AB1: @StoreData 3 @FemaleOffset 0.0 12
        21@ = 1
    end
    
    if 20@ == 0
    then 10@s = 'CFG_O2' // Female offset
    end
    if 20@ == 1
    then 10@s = 'CFG_O1' // Male offset
    end
    if 20@ == 2
    then 10@s = 'CFG_O9' // Both offset
    end
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 45.0 399.0 GXT 10@ 11@
    
    10@s = 'CFG_D7' // <
    0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.4 1.2 position 20.0 400.0 GXT 10@ 11@
    10@s = 'CFG_D8' // >
    0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.4 1.2 position 127.0 400.0 GXT 10@ 11@
    
    if 0AB1: @CursorClickCheck 4 box_position 25.0 406.0 box_size 19.0 12.0 // <
    then 0AB1: @IncOrDecIntVariable 4 variable 20@ min_value 0 max_value 2 flag FLAG_DECREASE return_in 20@
    end
    if 0AB1: @CursorClickCheck 4 box_position 132.0 406.0 box_size 19.0 12.0 // >
    then 0AB1: @IncOrDecIntVariable 4 variable 20@ min_value 0 max_value 2 flag FLAG_INCREASE return_in 20@
    end
end

0AB1: @GetIncreaseSteps 0 returned_values w_s_steps 12@ 13@ a_d_steps 14@ 15@ offsets_for_x_and_y 16@ 17@

if 0AB0:   key_pressed KEY_W // front +
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step 12@ pos_offset 16@ ret 21@
end
if 0AB0:   key_pressed KEY_S // front -
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step 13@ pos_offset 16@ ret 21@
end
if 0AB0:   key_pressed KEY_D // sides ->
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step 14@ pos_offset 17@ ret 21@
end
if 0AB0:   key_pressed KEY_A // sides <-
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step 15@ pos_offset 17@ ret 21@
end
if 0AB0:   key_pressed KEY_E // up
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step OFF_INC_STEP pos_offset 8 ret 21@
end
if 0AB0:   key_pressed KEY_Q // down
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step OFF_DEC_STEP pos_offset 8 ret 21@
end
if 0AB0:   key_pressed 37 // left
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step -10.0 pos_offset 12 ret 21@
end
if 0AB0:   key_pressed 39 // right
then 0AB1: @IncDecActorOffset 3 gender_flag 20@ inc_step 10.0 pos_offset 12 ret 21@
end

if 21@ == true
then
    0AB1: @RelocateActor_Alt 5 actor 1@ gender 0 pose 3@ place 4@ move_player 0
    0AB1: @RelocateActor_Alt 5 actor 2@ gender 1 pose 3@ place 4@ move_player 0
    0AB1: @LoadSexualPose 5 fem 1@ male 2@ pose 3@ place 4@ speed 5@
end

0AB1: @StoreInt 2 @ActorsOffsetsMenu_OffsetToModify 20@
0AB2: ret 1 sex_menu_state 0@

{======================================================================
======================================================================}
{ 0     (W/S) y = y -/+ step        (A/D) x = x -/+ step        ystep = - step*cos(a) + step*sin(a)
 90     (A/D) y = y -/+ step        (W/S) x = x +/- step        xstep = - step*cos(a) - step*sin(a)
180     (W/S) y = y +/- step        (A/D) x = x +/- step
270     (A/D) y = y +/- step        (W/S) x = x -/+ step                                          }

:GetXSteps // 0@: angle, 1@: step
02F7: 10@ = cosine 0@ // (float)
006B: 10@ *= 1@  // (float)
0013: 10@ *= -1.0
02F6: 11@ = sine 0@ // (float)
006B: 11@ *= 1@  // (float)
0013: 11@ *= -1.0
0087: 30@ = 10@ // (float)
005B: 30@ += 11@  // (float)
0087: 31@ = 30@ // (float)
0013: 31@ *= -1.0
0AB2: ret 2 30@ 31@

:GetYSteps // 0@: angle, 1@: step
02F7: 10@ = cosine 0@ // (float)
006B: 10@ *= 1@  // (float)
0013: 10@ *= -1.0
02F6: 11@ = sine 0@ // (float)
006B: 11@ *= 1@  // (float)
0087: 30@ = 10@ // (float)
005B: 30@ += 11@  // (float)
0087: 31@ = 30@ // (float)
0013: 31@ *= -1.0
0AB2: ret 2 30@ 31@

:GetIncreaseSteps // entries: none
0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 0@
0AB1: @GetXSteps 2 angle 0@ step OFF_INC_STEP store_in 10@ 11@
0AB1: @GetYSteps 2 angle 0@ step OFF_INC_STEP store_in 12@ 13@
if and
8AB0:   not key_pressed KEY_W
8AB0:   not key_pressed KEY_A
8AB0:   not key_pressed KEY_S
8AB0:   not key_pressed KEY_D
8AB0:   not key_pressed KEY_Q
8AB0:   not key_pressed KEY_E
then 32@=0
end
0092: 1@ = float 0@ to_integer
0B14: 1@ = 1@ MOD 180
if 1@ == 0
then 0AB1: @SetNumbers 6 12@ 13@ 10@ 11@ 4 0 ret 20@ 21@ 22@ 23@ 24@ 25@ dummies 9@ 9@ 9@ 9@
else 0AB1: @SetNumbers 6 11@ 10@ 13@ 12@ 0 4 ret 20@ 21@ 22@ 23@ 24@ 25@ dummies 9@ 9@ 9@ 9@
end
0085: 30@ = 32@ // (int)
0016: 30@ /= 1000
000A: 30@ += 1
0093: 30@ = integer 30@ to_float
006B: 20@ *= 30@  // (float)
006B: 21@ *= 30@  // (float)
006B: 22@ *= 30@  // (float)
006B: 23@ *= 30@  // (float)
0AB2: ret 6 w_s_steps 20@ 21@ a_d_steps 22@ 23@ offsets_for_x_and_y 24@ 25@

{======================================================================
======================================================================}
:CamsOffsetsMenu_Action
hex
0
end
:CamsOffsetsMenu_OffsetToModify
hex
00 00
end
:CamsOffsetsMenu_FPVJoystickOffset
hex
00 00 00 00    00 00 00 00
end
:CamsOffsetsMenu_FPVCamMoveable
hex
00
end
:CamsOffsetsMenu_FixedId
hex
0
end

:CamsOffsetsMenu_FreeCamCenterObject
hex
00 00 00 00
end
:CamsOffsetsMenu_FPVCamPosition
hex
00 00 00 00
00 00 00 00
00 00 00 00
end
:CamsOffsetsMenu_FPVCamAngle
hex
00 00 00 00
end
:CamsOffsetsMenu_FPVCamRotation
hex
00 00 00 00
end
:CamsOffsetsMenu_FPVCamTrgtX
hex
00 00 00 00
end
:CamsOffsetsMenu_FixedCam
hex
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00000000 00000000 00000000 00000000       00000000 00000000 00000000 00000000 00000000 00000000
end

{======================================================================
======================================================================}
:CameraOffsetsMenu // 0@: SexMenuState, 1@: Porn Actress, 2@: Porn Actor, 3@: pose, 4@: place, 5@: speed state
0AB1: @GetInt 1 @CamsOffsetsMenu_Action 30@

0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
if 12@ == 0 // Not in edit FPV cam mode
then
    10@s = 'CFSV_1' // Save
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 414.0 GXT 10@ 11@
    10@s = 'CFSV_2' // Load
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 48.0 414.0 GXT 10@ 11@
    10@s = 'CFSV_3' // Delete
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 83.0 414.0 GXT 10@ 11@
    
    if 0@ == 7
    then 10@s = 'CFSV_7' // Back
    else 10@s = 'S_OPT6' // Close
    end
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT 10@ 11@
    
    if 0AB1: @CursorClickCheck 4 box_position 23.0 421.0 box_size 35.0 12.0 // Save
    then 0AB1: @SetNumbers 2 sex_menu_state 7 action_to_execute_for_next_submenu 0 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
    end
    if 0AB1: @CursorClickCheck 4 box_position 60.0 421.0 box_size 35.0 12.0 // Load
    then 0AB1: @SetNumbers 2 sex_menu_state 7 action_to_execute_for_next_submenu 1 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
    end
    if 0AB1: @CursorClickCheck 4 box_position 100.0 421.0 box_size 41.0 12.0 // Delete
    then 0AB1: @SetNumbers 2 sex_menu_state 7 action_to_execute_for_next_submenu 2 ret 0@ 30@ 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@
    end
    if 0AB1: @CursorClickCheck 4 box_position 52.0 436.0 box_size 93.0 12.0 // Close or Back
    then
        if 0@ == 7
        then 0@ = 6 // Back to previous
        else
            0@ = 0 // Close (Reset SexMenuState)
            0AB1: @RemoveFreeCamCenter 0
            0AB1: @StoreInt 2 @CameraMode 0
            0AB1: @RelocateFreeCamCenter 0
            0AB1: @RemoveFirstPersonPoint 0
            0373: set_camera_directly_behind_player
            02EB: restore_camera_with_jumpcut
        end
    end
end
0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@

if 0@ == 7
then
    16@ = 0
    for 13@ = 332.0 to 388.0 step 28.0
        for 12@ = 11.0 to 131.0 step 40.0
            16@++
            0AB1: @ReadLineFromIntStorage 1 slot_to_read 16@ returned_values interior 6@ 7@ 8@ 9@ pose 10@ place 10@ center_xyz 10@ 10@ 10@ fem_xyza 10@ 10@ 10@ 10@ male_xyza 10@ 10@ 10@ 10@ slot 10@ in_use 10@
            
            10@s = 'CFSV_4' // Slot ~1~
            0AB1: @SetDrawTextRGB_1Number 12 font 1 linewidth 600.0 size 0.2 1.2 position 12@ 13@ GXT 10@ 11@ rgb 255 255 255 integer 16@
            
            if not 6@v == ""
            then
                0087: 15@ = 13@ // (float)
                000B: 15@ += 8.0
    			10@s = 'CFSV_5' // Data!
                0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 12@ 15@ GXT 10@ 11@
            end
            
            0087: 14@ = 12@ // (float)
            000B: 14@ += 14.0
            0087: 15@ = 13@ // (float)
            000B: 15@ += 10.0
            if 0AB1: @CursorClickCheck 4 box_position 14@ 15@ box_size 38.0 26.0
            then
                if 30@ == 0 // Save
                then 0AB1: @StoreInformationInInternalStorage 3 slot 16@ pose 3@ place 4@
                end
                if 30@ == 1 // Load
                then
                    0AB1: @RetrieveInformationFromInternalStorage 6 slot 16@ fem 1@ male 2@ pose 3@ place 4@ speed 5@
                    0AB1: @StoreInt 2 label @CamsOffsetsMenu_OffsetToModify value 0
                    0AB1: @RemoveFreeCamCenter 0
                    0AB1: @RemoveFirstPersonPoint 0
                    0AB1: @CreateFreeCamCenter 0
                    0AB1: @StoreInt 2 @CameraMode -1
                end
                if 30@ == 2 // Delete
                then
                    0AB1: @ClearStorageLine 1 line 16@
                    0AB1: @ClearTempStorage 0
                    0ACE: show_formatted_text_box "Deleted!"
                end
            end
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_Action 30@
    0AB2: ret 1 sex_menu_state 0@
else
    // Free Cam Edit
    0AB1: @GetInt 1 @CamsOffsetsMenu_OffsetToModify 20@
    if 20@ == 0
    then
        // Change angle of view
        10@s = 'CFG_D7' // <
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 170.0 389.0 GXT 10@ 11@
        10@s = 'CFG_C3' // Change Angle of View
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 183.0 390.0 GXT 10@ 11@
        10@s = 'CFG_D8' // >
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 286.0 389.0 GXT 10@ 11@
        if 0AB1: @CursorClickCheck 4 box_position 175.0 395.0 box_size 16.0 10.0 // <
        then
            0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
            12@ += 90.0
            0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
            0AB1: @CamsOffsets_SetFixedCamera 0
        end
        if 0AB1: @CursorClickCheck 4 box_position 289.0 395.0 box_size 16.0 10.0 // >
        then
            0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
            12@ -= 90.0
            0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
            0AB1: @CamsOffsets_SetFixedCamera 0
        end
        
        10@s = 'CFG_O6' // Reset
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 128.0 414.0 GXT 10@ 11@
        if 0AB1: @CursorClickCheck 4 box_position 142.0 421.0 box_size 39.0 12.0 // Reset
        then
            0AB1: @StoreData 3 @PlayerPosX 0.0 4
            0AB1: @StoreData 3 @PlayerPosY 0.0 4
            0AB1: @StoreData 3 @PlayerPosZ 0.0 4
            0AB1: @MoveFreeCamCenter 3 step_x 0.0 step_y 0.0 step_z 0.0
        end
    end
    
    
    // FPV Cam Edit
    0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
    0AB1: @FirstPersonCameraControl_Alt 2 flag_edit 1 flag_moveable 12@
    0AB1: @GetInt 1 @CamsOffsetsMenu_OffsetToModify 20@
    if 20@ == 1
    then
        if 12@ == 0
        then 10@s = 'CFG_C5' // Enter FPV edit mode
        else 10@s = 'CFG_C6' // FPV edit mode - Click anywhere to close
        end
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 170.0 389.0 GXT 10@ 11@
        
        if 12@ == 0
        then
            if 0AB1: @CursorClickCheck 4 box_position 260.0 395.0 box_size 186.0 11.0 // Move First Person View camera
            then
                12@ = 1
                0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 0 // Hide cursor
            end
        else
            if 0AB1: @CursorClickCheck 5 box_position 320.0 224.0 box_size 640.0 448.0 disable_draw_box 1 // Full screen box
            then
                12@ = 0
                0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 1 // Show cursor
            end
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@
   
    
    // Fixed Cam Edit
    0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
    0AB1: @GetInt 1 @CamsOffsetsMenu_FixedId 13@
    0085: 14@ = 13@ // (int)
    000A: 14@ += 1
    0AB1: @GetInt 1 @CamsOffsetsMenu_OffsetToModify 20@
    if 20@ == 2
    then
        if 12@ == 0
        then
            10@s = 'CFG_D7' // <
            0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 170.0 389.0 GXT 10@ 11@
            10@s = 'CFG_D8' // >
            0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 344.0 389.0 GXT 10@ 11@
            10@s = 'CFG_C7' // Enter Fixed camera ~1~ edit mode
            0AB1: @SetDrawTextRGB_1Number 12 font 2 linewidth 600.0 size 0.2 1.0 position 188.0 390.0 GXT 10@ 11@ rgb 255 255 255 integer 14@
        else
            10@s = 'CFG_C8' // Fixed cam ~1~ edit mode - click to exit
            0AB1: @SetDrawTextRGB_1Number 12 font 2 linewidth 600.0 size 0.2 1.0 position 170.0 389.0 GXT 10@ 11@ rgb 255 255 255 integer 14@
        end
        if 12@ == 0
        then
            if 0AB1: @CursorClickCheck 4 box_position 175.0 395.0 box_size 16.0 10.0 // <
            then
                0AB1: @RemoveFirstPersonPoint 0
                0AB1: @IncOrDecIntVariable 4 variable 13@ min_value 0 max_value 11 flag FLAG_DECREASE return_in 13@
                0AB1: @GetFixedCam 1 cam_id 13@ store_in cam_pos 21@ 22@ 23@ angle 24@ rotation 25@ trgt_x 26@
                0AB1: @InitFixedCam 6 cam_pos 21@ 22@ 23@ angle 24@ rotation 25@ trgt_x 26@ return_in position 21@ 22@ 23@ angle 24@ trgt_x 26@
                0AB1: @CreateFirstPersonPoint 5 cam_pos 21@ 22@ 23@ angle 24@ trgt_x 26@
            end
            if 0AB1: @CursorClickCheck 4 box_position 345.0 395.0 box_size 16.0 10.0 // >
            then
                0AB1: @RemoveFirstPersonPoint 0
                0AB1: @IncOrDecIntVariable 4 variable 13@ min_value 0 max_value 11 flag FLAG_INCREASE return_in 13@
                0AB1: @GetFixedCam 1 cam_id 13@ store_in cam_pos 21@ 22@ 23@ angle 24@ rotation 25@ trgt_x 26@
                0AB1: @InitFixedCam 6 cam_pos 21@ 22@ 23@ angle 24@ rotation 25@ trgt_x 26@ return_in position 21@ 22@ 23@ angle 24@ trgt_x 26@
                0AB1: @CreateFirstPersonPoint 5 cam_pos 21@ 22@ 23@ angle 24@ trgt_x 26@
            end
            if 0AB1: @CursorClickCheck 4 box_position 260.0 395.0 box_size 150.0 11.0 // Text
            then
                12@ = 1
                0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 0 // Hide cursor
            end
        else
            if 0AB1: @CursorClickCheck 5 box_position 320.0 224.0 box_size 640.0 448.0 disable_draw_box 1 // Full screen box
            then
                12@ = 0
                0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 1 // Show cursor
            end
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FixedId 13@
    
    
    // Change between camera modes
    0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
    if 12@ == 0 // Not in edit FPV cam mode
    then
        if 20@ == 0
        then 10@s = 'CFG_C1' // Free cam offset
        end
        if 20@ == 1
        then 10@s = 'CFG_C2' // First person offset
        end
        if 20@ == 2
        then 10@s = 'CFG_C4' // Fixed Camera ~1~
        end
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 45.0 399.0 GXT 10@ 11@
        
        10@s = 'CFG_D7' // <
        0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.4 1.2 position 20.0 400.0 GXT 10@ 11@
        10@s = 'CFG_D8' // >
        0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.4 1.2 position 127.0 400.0 GXT 10@ 11@
    
        if 0AB1: @CursorClickCheck 4 box_position 25.0 406.0 box_size 19.0 12.0 // <
        then 0AB1: @IncOrDecIntVariable 4 variable 20@ min_value 0 max_value 2 flag FLAG_DECREASE return_in 20@
        end
        if 0AB1: @CursorClickCheck 4 box_position 132.0 406.0 box_size 19.0 12.0 // >
        then 0AB1: @IncOrDecIntVariable 4 variable 20@ min_value 0 max_value 2 flag FLAG_INCREASE return_in 20@
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_OffsetToModify 20@
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@
    
    
    // Manage the camera
    0AB1: @GetInt 1 @CamsOffsetsMenu_OffsetToModify 20@ // Current state
    0AB1: @GetInt 2 @CamsOffsetsMenu_OffsetToModify 1 21@ // Previous state
    if 803B:   not 20@ == 21@  // (int) // State changed?
    then
        if 20@ == 0 // Free camera
        then
            0AB1: @RemoveFirstPersonPoint 0
            0AB1: @CreateFreeCamCenter 0
            0AB1: @StoreInt 2 @CameraMode -1
        end
        if 20@ == 1 // First person camera
        then
            0AB1: @RemoveFreeCamCenter 0
            0AB1: @RemoveFirstPersonPoint 0
            0AB1: @StoreInt 2 @CameraMode 2
            0AB1: @GetFPVCamData 0 ret pos_offset 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@
            0AB1: @InitFirstPersonCameraVars_Alt 6 pos_offset 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@ ret update_values position 10@ 11@ 12@ angle 13@ trgt_x 15@
            0AB1: @CreateFirstPersonPoint 5 cam_pos 10@ 11@ 12@ angle 13@ trgt_x 15@
        end
        if 20@ == 2 // Fixed camera
        then
            0AB1: @RemoveFreeCamCenter 0
            0AB1: @RemoveFirstPersonPoint 0
            0AB1: @StoreInt 2 @CameraMode 2
            0AB1: @StoreInt 2 @CamsOffsetsMenu_FixedId 0
            0AB1: @GetFixedCam 1 cam_id 0 store_in cam_pos 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@
            0AB1: @InitFixedCam 6 cam_pos 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@ return_in position 10@ 11@ 12@ angle 13@ trgt_x 15@
            0AB1: @CreateFirstPersonPoint 5 cam_pos 10@ 11@ 12@ angle 13@ trgt_x 15@
        end
        0AB1: @StoreInt 3 @CamsOffsetsMenu_OffsetToModify 20@ 1 // Store current state in previous state slot
    end
end

0AB1: @GetInt 1 @CamsOffsetsMenu_OffsetToModify 20@

if 20@ == 0 // Free Camera Edit
then
    0AB1: @GetIncreaseSteps_Cam 1 OFF_INC_STEP returned_values steps 12@ 13@ negative_steps 14@ 15@
        
    if 0AB0:   key_pressed KEY_W // front +
    then 0AB1: @MoveFreeCamCenter 3 step_x 12@ step_y 13@ step_z 0.0
    end
    if 0AB0:   key_pressed KEY_S // front -
    then 0AB1: @MoveFreeCamCenter 3 step_x 14@ step_y 15@ step_z 0.0
    end
    if 0AB0:   key_pressed KEY_D // sides ->
    then 0AB1: @MoveFreeCamCenter 3 step_x 13@ step_y 14@ step_z 0.0
    end
    if 0AB0:   key_pressed KEY_A // sides <-
    then 0AB1: @MoveFreeCamCenter 3 step_x 15@ step_y 12@ step_z 0.0
    end
    if 0AB0:   key_pressed KEY_E // up
    then 0AB1: @MoveFreeCamCenter 3 step_x 0.0 step_y 0.0 step_z OFF_INC_STEP
    end
    if 0AB0:   key_pressed KEY_Q // down
    then 0AB1: @MoveFreeCamCenter 3 step_x 0.0 step_y 0.0 step_z OFF_DEC_STEP
    end
end

if 20@ == 1 // First Person Camera Edit
then
    0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
    if 12@ == 1 // In edit FPV cam mode
    then
        14@ = OFF_INC_STEP
        if 0AB0:   key_pressed KEY_SHIFT // speed boost
        then 14@ *= 5.0
        end
        if 0AB0:   key_pressed KEY_W // front +
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides 0 front 1 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_S // front -
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides 0 front -1 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_D // sides ->
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides 1 front 0 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_A // sides <-
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides -1 front 0 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_E // up
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides 0 front 0 height 1 speed 14@
        end
        if 0AB0:   key_pressed KEY_Q // down
        then 0AB1: @MoveFirstPersonPoint 4 normalized_dirs sides 0 front 0 height -1 speed 14@
        end
        
        // Player moved the mouse cursor?
        0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 0 24@
        0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 4 25@
        0A4A: store_joystick_X_offset_to 22@ Y_offset_to 23@
        if or
        8045:   not 22@ == 24@  // (float)
        8045:   not 23@ == 25@  // (float)
        then 0AB1: @MoveFirstPersonPoint 3 normalized_dirs sides 0 front 0 height 0
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@
end

if 20@ == 2 // Fixed Camera Edit
then
    0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable 12@
    0AB1: @GetInt 1 @CamsOffsetsMenu_FixedId 13@
    if 12@ == 1 // In edit FPV cam mode
    then
        14@ = OFF_INC_STEP
        if 0AB0:   key_pressed KEY_SHIFT // speed boost
        then 14@ *= 5.0
        end
        if 0AB0:   key_pressed KEY_W // front +
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides 0 front 1 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_S // front -
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides 0 front -1 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_D // sides ->
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides 1 front 0 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_A // sides <-
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides -1 front 0 height 0 speed 14@
        end
        if 0AB0:   key_pressed KEY_E // up
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides 0 front 0 height 1 speed 14@
        end
        if 0AB0:   key_pressed KEY_Q // down
        then 0AB1: @MoveFixedCamPoint 5 cam_id 13@ normalized_dirs sides 0 front 0 height -1 speed 14@
        end
        
        // Player moved the mouse cursor?
        0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 0 24@
        0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 4 25@
        0A4A: store_joystick_X_offset_to 22@ Y_offset_to 23@
        if or
        8045:   not 22@ == 24@  // (float)
        8045:   not 23@ == 25@  // (float)
        then 0AB1: @MoveFixedCamPoint 4 cam_id 13@ normalized_dirs sides 0 front 0 height 0
        end
    end
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FPVCamMoveable 12@
    0AB1: @StoreInt 2 @CamsOffsetsMenu_FixedId 13@
end
0AB2: ret 1 sex_menu_state 0@

{======================================================================
======================================================================}
:CreateFreeCamCenter // entries: none
0AB1: @GetData 2 @CamsOffsetsMenu_FreeCamCenterObject 0 30@
if 83CA:   not object 30@ exists
then
    0@ = #info // #info, #diamond_3
    0247: load_model 0@
    038B: load_requested_models
    repeat
    wait 0
    until 0248:   model 0@ available
    0AB1: @GetData 2 @PlayerPosX 0 10@
    0AB1: @GetData 2 @PlayerPosY 0 11@
    0AB1: @GetData 2 @PlayerPosZ 0 12@
    0AB1: @GetData 2 @PlayerPosX 4 20@
    0AB1: @GetData 2 @PlayerPosY 4 21@
    0AB1: @GetData 2 @PlayerPosZ 4 22@
    005B: 10@ += 20@  // (float)
    005B: 11@ += 21@  // (float)
    005B: 12@ += 22@  // (float)
    029B: 30@ = init_object 0@ at 10@ 11@ 12@
    //0107: 30@ = create_object 0@ at 10@ 11@ 12@
    0249: release_model 0@
end
0AB1: @StoreData 2 @CamsOffsetsMenu_FreeCamCenterObject 30@
0AB1: @ActorsOffsets_SetFixedCamera 1 30@
0AB2: ret 0

{======================================================================
======================================================================}
:RemoveFreeCamCenter // entries: none
0AB1: @GetData 2 @CamsOffsetsMenu_FreeCamCenterObject 0 30@
if 03CA:   object 30@ exists
then 0108: destroy_object 30@
end
0AB1: @StoreData 2 @CamsOffsetsMenu_FreeCamCenterObject 30@
0AB2: ret 0

{======================================================================
======================================================================}
:MoveFreeCamCenter // step_x 0@ step_y 1@ step_z 2@
0AB1: @GetData 2 @CamsOffsetsMenu_FreeCamCenterObject 0 30@

// Position
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@

// Offset
0AB1: @GetData 2 @PlayerPosX 4 20@
0AB1: @GetData 2 @PlayerPosY 4 21@
0AB1: @GetData 2 @PlayerPosZ 4 22@

005B: 20@ += 0@  // (float)
005B: 21@ += 1@  // (float)
005B: 22@ += 2@  // (float)

005B: 10@ += 20@  // (float)
005B: 11@ += 21@  // (float)
005B: 12@ += 22@  // (float)

01BC: put_object 30@ at 10@ 11@ 12@
0AB1: @StoreData 2 @CamsOffsetsMenu_FreeCamCenterObject 30@
0AB1: @ActorsOffsets_SetFixedCamera 1 30@

0AB1: @StoreData 3 @PlayerPosX 20@ 4
0AB1: @StoreData 3 @PlayerPosY 21@ 4
0AB1: @StoreData 3 @PlayerPosZ 22@ 4
0AB2: ret 0

{======================================================================
======================================================================}
:CamsOffsets_SetFixedCamera // entries: none
0AB1: @GetData 2 @CamsOffsetsMenu_FreeCamCenterObject 0 30@
0AB1: @ActorsOffsets_SetFixedCamera 1 30@
0AB2: ret 0

{======================================================================
======================================================================}
:GetIncreaseSteps_Cam // step 0@
068D: get_camera_position_to 10@ 11@ 12@
068E: get_camera_target_point_to 20@ 21@ 22@

0087: 8@ = 20@ // (float)
0063: 8@ -= 10@  // (float)
006B: 8@ *= 0@  // (float)       // xpos += step * sin(a)        // sin (a) = cam_trgt_x - cam_pos_x

0087: 9@ = 21@ // (float)
0063: 9@ -= 11@  // (float)
006B: 9@ *= 0@  // (float)       // ypos += step * cos(a)        // cos (a) = cam_trgt_y - cam_pos_y

0087: 18@ = 8@ // (float)
0013: 18@ *= -1.0
0087: 19@ = 9@ // (float)
0013: 19@ *= -1.0

0AB2: ret 4 step_x 8@ step_y 9@ n_step_x 18@ n_step_y 19@

{======================================================================
======================================================================}
:DEBUG_ShowFPVCamData
0AB1: @GetFPVCamData 0 ret pos_offset 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@
0AD0: show_formatted_text_lowpriority "pos_offset %f %f %f angle %f rotation %f trgt_x %f" time 0 pos_offset 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@
0AB2: ret 0

{======================================================================
======================================================================}
:ShowSettingTexts
// 0@: sex menu state, 1@: speed, 2@: pose, 3@ place, 4@: flag for info text

10@s = 'CFG_T3' // ~b~~k~~VEHICLE_ENTER_EXIT~: ~s~Go back.
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 12.0 GXT 10@ 11@

if 4@ == 0
then
    10@s = 'CFG_T1' // ~b~Mouse wheel~w~: Increase/decrease speed.
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 26.0 GXT 10@ 11@
    10@s = 'CFG_T4' // ~b~~k~~CAMERA_CHANGE_VIEW_ALL_SITUATIONS~~s~: Change Camera mode. ~b~~k~~GO_LEFT~~s~/~b~~k~~GO_RIGHT~~s~: Change cam view (only in fixed mode).
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 40.0 GXT 10@ 11@
else
    if 0@ == 0
    then
        10@s = 'CFG_T9' // ~b~~k~~PED_ANSWER_PHONE~~w~: Go to the desired place and press ~k~~PED_ANSWER_PHONE~ to select pose.
        0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 26.0 GXT 10@ 11@
    end
end

{// Debug
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 29@
if 056D:   actor 29@ defined
then
    0AB1: @GetData 1 @FirstPersonCameraVars_Rotation 30@
    0172: 31@ = actor 29@ Z_angle
    0AD0: show_formatted_text_lowpriority "rotation: %f, angle: %f" time 1 30@ 31@
end}

if 4@ == 0    // Classic Mode For SexMachine
then
    gosub @SettingTexts_CameraMode
    gosub @SettingTexts_Animations
else
    if and
    not 0@ == 3
    not 0@ == 4
    not 0@ == 6
    not 0@ == 7
    then
        gosub @SettingTexts_CameraMode
        gosub @SettingTexts_Animations
    end
end


if 4@ == 0    // Classic Mode For SexMachine
then
    if 0@ == 0
    then 5@ = 1
    end
else          // Custom mode for SexMachine
    if 0@ == 2
    then 5@ = 1
    end
end
if 5@ == 1
then // sex menu closed/hidden
    {10@s = 'CFG_T7' // Change position
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT 10@ 11@
    
    10@s = 'CFG_T8' // Actors offsets
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 410.0 GXT 10@ 11@}
    
    10@s = 'CFG_M1' // Poses
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 129.0 391.0 GXT 10@ 11@
    10@s = 'CFG_M2' // Offsets
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 119.0 404.0 GXT 10@ 11@
    10@s = 'CFG_M3' // Cameras
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 118.0 417.0 GXT 10@ 11@
    10@s = 'CFSV_7' // Back
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 135.0 430.0 GXT 10@ 11@
end
0AB2: ret 0


:SettingTexts_CameraMode
0AB1: @GetInt 1 @CameraMode 8@
if 8@ == 0 // Free Camera
then
    10@s = 'CFG_C4' // Free Camera
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@
end
if 8@ == 1 // Fixed Camera
then
    0AB1: @GetInt 1 @CurrentCamera 9@
    9@+=1
    10@s = 'CFG_C5' // Fixed Camera ~y~~1~
    0AB1: @SetDrawTextRGB_1Number 12 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@ rgb 255 255 255 number 9@
    10@s = 'CFG_T6' // <  >
    0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.2 1.0 position 244.0 390.0 GXT 10@ 11@
end
if 8@ == 2 // First Person Camera
then
    10@s = 'CFG_C6' // First Person
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@
end
return


:SettingTexts_Animations
// 8@: selected animation, 9@: max animations for current pose
10@s = 'INT_1' // ~1~
0AB1: @GetInt 1 @CurrentAnimation 8@
0AB1: @ReadSexPosesInfo 3 1@ 2@ 3@ out 12@ 13@ 9@
for 12@ = 1 to 9@
    if 003B:   12@ == 8@  // (int)
    then 13@=100
    else 13@=255
    end
    14@=18.0
    0093: 16@ = integer 12@ to_float
    006B: 14@ *= 16@  // (float)
    15@=-18.0
    0093: 16@ = integer 9@ to_float
    006B: 15@ *= 16@  // (float)
    005B: 14@ += 15@  // (float)
    14@ += 340.0      // x-pos = 18*index - 18*max-anims + 340
    0AB1: @SetDrawTextRGB_1Number 12 font 1 linewidth 600.0 size 0.4 1.2 position 14@ 389.0 GXT 10@ 11@ rgb 13@ 13@ 13@ number 12@
end
return
