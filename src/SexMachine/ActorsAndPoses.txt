{======================================================================
======================================================================}
:ActressGXT
hex
    00 00 00 00
    00 00 00 00
end

:ActorGXT
hex
    00 00 00 00
    00 00 00 00
end

:GetActorsInfo
0AB1: @GetData 2 @ActressGXT 0 0@
0AB1: @GetData 2 @ActressGXT 4 1@
0AB1: @GetData 2 @ActorGXT 0 2@
0AB1: @GetData 2 @ActorGXT 4 3@
0AB2: ret 4 gxt_fem 0@ 1@ gtx_male 2@ 3@

:StoreActorsInfo // 0@s: female gxt, 2@s: male gxt
0AB1: @StoreData 3 @ActressGXT 0@ 0
0AB1: @StoreData 3 @ActressGXT 1@ 4
0AB1: @StoreData 3 @ActorGXT 2@ 0
0AB1: @StoreData 3 @ActorGXT 3@ 4
0AB2: ret 0

{======================================================================
======================================================================}
:AddOffsetTo3DCoord // 0@ 1@ 2@ 3@: coordinates, 4@: gender flag
if 4@ == 0 // female by default (0)
then
    0AB1: @GetData 2 @FemaleOffset 0 out 10@
    0AB1: @GetData 2 @FemaleOffset 4 out 11@
    0AB1: @GetData 2 @FemaleOffset 8 out 12@
    0AB1: @GetData 2 @FemaleOffset 12 out 13@
else
    0AB1: @GetData 2 @MaleOffset 0 out 10@
    0AB1: @GetData 2 @MaleOffset 4 out 11@
    0AB1: @GetData 2 @MaleOffset 8 out 12@
    0AB1: @GetData 2 @MaleOffset 12 out 13@
end
005B: 0@ += 10@  // (float)
005B: 1@ += 11@  // (float)
005B: 2@ += 12@  // (float)
005B: 3@ += 13@  // (float)
0AB2: ret 4 0@ 1@ 2@ 3@

{======================================================================
======================================================================}
:GenerateActors // 0@: pose, 1@: place
0AB1: @GetActorsCoordinates 2 0@ 1@ ret fem_xyza 10@ 11@ 12@ 13@ male_xyza 20@ 21@ 22@ 23@

0AB1: @AddOffsetTo3DCoord 5 3dcoord 10@ 11@ 12@ 13@ gender 0 store_in 10@ 11@ 12@ 13@
0AB1: @AddOffsetTo3DCoord 5 3dcoord 20@ 21@ 22@ 23@ gender 1 store_in 20@ 21@ 22@ 23@

0AB1: @GetActorsInfo 0 gxt_fem 18@ 19@ gtx_male 28@ 29@
023C: load_special_actor 18@s as 1
023C: load_special_actor 28@s as 2
038B: load_requested_models
repeat
wait 0
until 023D:   special_actor 1 loaded and 023D:   special_actor 2 loaded

// Actress
009A: 30@ = create_actor_pedtype 5 model #SPECIAL01 at 10@ 11@ 12@
0173: set_actor 30@ Z_angle_to 13@
0619: enable_actor 30@ collision_detection 0

// Actor
009A: 31@ = create_actor_pedtype 4 model #SPECIAL02 at 20@ 21@ 22@
0173: set_actor 31@ Z_angle_to 23@
0619: enable_actor 31@ collision_detection 0

0296: unload_special_actor 1
0296: unload_special_actor 2
0AB2: ret 2 actress 30@ actor 31@

{======================================================================
======================================================================}
:RelocateActor // 0@: Actor, 1@: Actor Gender Flag, 2@: Pose, 3@: Place
0AB1: @GetActorsCoordinates 2 2@ 3@ ret fem_xyza 10@ 11@ 12@ 13@ male_xyza 20@ 21@ 22@ 23@
if 1@ == 0 // female by default (0)
then
    0AB1: @AddOffsetTo3DCoord 5 3dcoord 10@ 11@ 12@ 13@ gender 0 store_in 10@ 11@ 12@ 13@
    00A1: put_actor 0@ at 10@ 11@ 12@
    0173: set_actor 0@ Z_angle_to 13@
else
    0AB1: @AddOffsetTo3DCoord 5 3dcoord 20@ 21@ 22@ 23@ gender 1 store_in 20@ 21@ 22@ 23@
    00A1: put_actor 0@ at 20@ 21@ 22@
    0173: set_actor 0@ Z_angle_to 23@
end
0AB2: ret 0

{======================================================================
======================================================================}
:RelocateActor_Alt // 0@: Actor, 1@: Actor Gender Flag, 2@: Pose, 3@: Place, 4@: flag (move player)
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@
13@ = 0.0
12@-=1.0

0AB1: @AddOffsetTo3DCoord 5 3dcoord 10@ 11@ 12@ 13@ gender 1@ store_in 10@ 11@ 12@ 13@
00A1: put_actor 0@ at 10@ 11@ 12@
0173: set_actor 0@ Z_angle_to 13@

if and
1@ == 0 // female
4@ == true
then
    00A1: put_actor $PLAYER_ACTOR at 10@ 11@ 12@
    0605: actor $PLAYER_ACTOR perform_animation "sex_pose" IFP "ped" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
end
0AB2: ret 0

{======================================================================
======================================================================}
:LoadSexualPose // 0@: actress, 1@: actor, 2@: pose, 3@: place, 4@: speed state
const
    An_Actress = 0@
    An_Actor = 1@
    An_SexPoseInt = 2@
    An_SexPlace = 3@
    An_SpeedInt = 4@
    An_Animation = 5@
    An_Str1 = 6@
    An_Str2 = 7@
    An_MaxAnim = 8@
    AnimIfpFem = 10@v
    AnimIfpMale = 14@v
    An_IfpFile = 18@v
end

0AB1: call_scm_func @ReadSexPosesInfo 3 An_SpeedInt An_SexPoseInt An_SexPlace out An_Str1 An_Str2 An_MaxAnim
0AB1: @GetInt 1 @CurrentAnimation An_Animation
if 001D:   An_Animation > An_MaxAnim  // (int)
then An_Animation = 1
end
if 001B:   1 > An_Animation
then 0085: An_Animation = An_MaxAnim // (int)
end
0AB1: @StoreInt 2 @CurrentAnimation An_Animation
0AB1: call_scm_func @GetAnimationNames 3 An_Str1 An_Str2 An_Animation out ifp_fem 10@ 11@ 12@ 13@ ifp_male 14@ 15@ 16@ 17@ ifp_file 18@ 19@ 20@ 21@

04ED: load_animation An_IfpFile
038B: load_requested_models
repeat
wait 0
until 04EE:   animation An_IfpFile loaded
0605: actor An_Actress perform_animation AnimIfpFem IFP An_IfpFile framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
0605: actor An_Actor perform_animation AnimIfpMale IFP An_IfpFile framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
04EF: release_animation An_IfpFile

0AB2: ret 0


{======================================================================
======================================================================}
:CurrentAnimation
hex
    1
end

:GetInt // 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 1 virtual_protect 0
0AB2: ret 1 11@

:StoreInt // 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 1 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:ReadSexPosesInfo // 0@: speed, 1@: pose, 2@: place
0A9A: 30@ = openfile ANIMATIONS_FILE mode "rb"  // IF and SET
for 4@ = 0 to 3
    0ADA: 31@ = scan_file 30@ format "%s %s" 10@v 14@v //IF and SET     // Dummy words
    for 5@ = 0 to 8
        0ADA: 31@ = scan_file 30@ format "%s" 10@v //IF and SET         // Dummy word
        for 6@ = 0 to 1
            0ADA: 31@ = scan_file 30@ format "%s %d %s %d %s %d %s %d" 10@s 20@ 12@s 21@ 14@s 22@ 16@s 23@ //IF and SET
            0B14: 24@ = 1@ MOD 2
            0085: 25@ = 1@ // (int)
            0016: 25@ /= 2
            if and            // pose % 2 === 6@  &&   pose / 2 === 4@
            003B:   24@ == 6@  // (int)
            003B:   25@ == 4@  // (int)
            then
                if 003B:   2@ == 5@  // (int)   // place === 5@
                then
                    for 7@ = 0 to 3
                        if 003B:   0@ == 7@  // (int)   // speed === 7@
                        then
                            05AA: 26@s = 10@(7@,4s) // @s = 'short'
                            0085: 28@ = 20@(7@,4i) // (int)
                            0A9B: closefile 30@
                            0AB2: ret 3 gxt_entry 26@ 27@ max_anim 28@
                        end
                    end
                end
            end
        end
    end
end
0A9B: closefile 30@
0AB2: ret 3 gxt_entry 26@ 27@ max_anim 28@

{======================================================================
======================================================================}
:GetAnimationNames
const
    Gan_Str1 = 0@
    Gan_Str2 = 1@
    Gan_StrSpeed = 0@s
    Gan_Animation = 2@
end

if 05AE:   Gan_StrSpeed == "idle" // @s == 'short'
then
    0AD3: 10@v = format "idle%d_f" Gan_Animation
    0AD3: 14@v = format "idle%d_m" Gan_Animation
    0AD3: 18@v = format "idle%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "slow" // @s == 'short'
then
    0AD3: 10@v = format "slow%d_f" Gan_Animation
    0AD3: 14@v = format "slow%d_m" Gan_Animation
    0AD3: 18@v = format "slow%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "normal" // @s == 'short'
then
    0AD3: 10@v = format "normal%d_f" Gan_Animation
    0AD3: 14@v = format "normal%d_m" Gan_Animation
    0AD3: 18@v = format "normal%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "fast" // @s == 'short'
then
    0AD3: 10@v = format "fast%d_f" Gan_Animation
    0AD3: 14@v = format "fast%d_m" Gan_Animation
    0AD3: 18@v = format "fast%d" Gan_Animation
end
0AB2: ret 12 ifp_fem 10@ 11@ 12@ 13@ ifp_male 14@ 15@ 16@ 17@ ifp_file 18@ 19@ 20@ 21@

{======================================================================
======================================================================}
:CreateActor // 0@: gender flag (female by default(0)), 1@ 2@ 3@: x,y,z offsets
0AB1: @GetActorsInfo 0 gxt_fem 10@ 11@ gtx_male 12@ 13@

18@ = 5 // CivFemale pedtype
19@ = 4 // CivMale pedtype

023C: load_special_actor 10@(0@,2s) as 1
038B: load_requested_models
repeat
wait 0
until 023D:   special_actor 1 loaded

00A0: store_actor $PLAYER_ACTOR position_to 4@ 5@ 6@
005B: 4@ += 1@  // (float)
005B: 5@ += 2@  // (float)
005B: 6@ += 3@  // (float)
009A: 8@ = create_actor_pedtype 18@(0@,2i) model #SPECIAL01 at 4@ 5@ 6@
0173: set_actor 8@ Z_angle_to 0.0
0619: enable_actor 8@ collision_detection 0
0296: unload_special_actor 1

0AB2: ret 5 actor 8@ position 4@ 5@ 6@ angle 0.0

{======================================================================
======================================================================}
:GenerateActors_Alt // none
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@
10@ += 0.0
11@ += 0.0
12@ += -1.0
13@ = 0.0

0AB1: @SetNumbers 4 fem_coords 10@ 11@ 12@ 13@ ret male_coords 20@ 21@ 22@ 23@ dummy_output 31@ 31@ 31@ 31@ 31@ 31@

0AB1: @AddOffsetTo3DCoord 5 3dcoord 10@ 11@ 12@ 13@ gender 0 store_in 10@ 11@ 12@ 13@
0AB1: @AddOffsetTo3DCoord 5 3dcoord 20@ 21@ 22@ 23@ gender 1 store_in 20@ 21@ 22@ 23@

0AB1: @GetActorsInfo 0 gxt_fem 18@ 19@ gtx_male 28@ 29@
023C: load_special_actor 18@s as 1
023C: load_special_actor 28@s as 2
038B: load_requested_models
repeat
wait 0
until 023D:   special_actor 1 loaded and 023D:   special_actor 2 loaded

// Actress
009A: 30@ = create_actor_pedtype 5 model #SPECIAL01 at 10@ 11@ 12@
0173: set_actor 30@ Z_angle_to 13@
0619: enable_actor 30@ collision_detection 0

// Actor
009A: 31@ = create_actor_pedtype 4 model #SPECIAL02 at 20@ 21@ 22@
0173: set_actor 31@ Z_angle_to 23@
0619: enable_actor 31@ collision_detection 0

0296: unload_special_actor 1
0296: unload_special_actor 2
0AB2: ret 2 actress 30@ actor 31@

{======================================================================
======================================================================}
:RelocateFreeCamCenter // entries: none
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@

0AB1: @GetData 2 @PlayerPosX 4 20@
0AB1: @GetData 2 @PlayerPosY 4 21@
0AB1: @GetData 2 @PlayerPosZ 4 22@

005B: 10@ += 20@  // (float)
005B: 11@ += 21@  // (float)
005B: 12@ += 22@  // (float)

12@ += -1.0
00A1: put_actor $PLAYER_ACTOR at 10@ 11@ 12@
0605: actor $PLAYER_ACTOR perform_animation "sex_pose" IFP "ped" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
0AB2: ret 0
