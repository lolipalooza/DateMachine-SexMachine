{======================================================================
======================================================================}
:PlayerData
hex
    00 00 00 // Position x - y - z
    00 // Interior
end

:PlayerPosX
hex
00000000 00000000 // 1: position, 2: offset
end

:PlayerPosY
hex
00000000 00000000
end

:PlayerPosZ
hex
00000000 00000000
end

:PlayerAngle
hex
00000000
end

:StorePlayerData
00A0: store_actor $PLAYER_ACTOR position_to 10@ 11@ 12@
077E: get_active_interior_to 13@
0AC6: 0@ = label @PlayerData offset
0A8C: write_memory 0@ size 4 value 10@ virtual_protect 0
0@ += 4
0A8C: write_memory 0@ size 4 value 11@ virtual_protect 0
0@ += 4
0A8C: write_memory 0@ size 4 value 12@ virtual_protect 0
0@ += 4
0A8C: write_memory 0@ size 4 value 13@ virtual_protect 0
0AB2: ret 0

:RecoverPlayerPosition
0AC6: 0@ = label @PlayerData offset
0A8D: 10@ = read_memory 0@ size 4 virtual_protect 0
0@ += 4
0A8D: 11@ = read_memory 0@ size 4 virtual_protect 0
0@ += 4
0A8D: 12@ = read_memory 0@ size 4 virtual_protect 0
0AB2: ret 3 10@ 11@ 12@

:RecoverPlayerInterior
0AC6: 0@ = label @PlayerData offset
0@ += 12
0A8D: 13@ = read_memory 0@ size 4 virtual_protect 0
0AB2: ret 1 13@

{======================================================================
======================================================================}
:PutPlayerIntoScene // 0@: pose, 1@: place
0619: enable_actor $PLAYER_ACTOR collision_detection 0
0AB1: @GetCenterCoordinates 2 pose 0@ place 1@ ret 3@ 4@ 5@
5@ -= 1.0
00A1: put_actor $PLAYER_ACTOR at 3@ 4@ 5@
0605: actor $PLAYER_ACTOR perform_animation "sex_pose" IFP "ped" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
0AB2: ret 0

{======================================================================
======================================================================}
:ToggleSexualCamera // pose 0@ place 1@
0AB1: @GetInt 1 @CameraMode 11@
if 11@ == 0 // Free mode
then 02EB: restore_camera_with_jumpcut
end
if 11@ == 1 // Fixed mode
then
    0AB1: @GetInt 1 @CurrentCamera 10@
    0AB1: @GetCameraCoordinates 3 10@ 0@ 1@ ret cam_pos 3@ 4@ 5@ cam_target 6@ 7@ 8@
    015F: set_camera_position 3@ 4@ 5@ rotation 0.0 0.0 0.0
    0160: set_camera_point_at 6@ 7@ 8@ switchstyle 2
end
if 11@ == 2 // First person mode
then
    0AB1: @GetFirstPersonCamCoordinates 2 pose 0@ place 1@ ret cam_pos 4@ 5@ 6@ cam_target 7@ 8@ 9@ rotation 14@ angle 15@ min_rotation 16@ max_rotation 17@ min_angle 18@ max_angle 19@
    0AB1: @StoreData 2 @FirstPersonCameraVars_Rotation 14@
    0AB1: @StoreData 2 @FirstPersonCameraVars_MinRotation 16@
    0AB1: @StoreData 2 @FirstPersonCameraVars_MaxRotation 17@
    0AB1: @StoreData 2 @FirstPersonCameraVars_MinAngle 18@
    0AB1: @StoreData 2 @FirstPersonCameraVars_MaxAngle 19@
    0AB1: @CreateFirstPersonPoint 5 cam_pos 4@ 5@ 6@ angle 15@ dummy_angle 7@
else 0AB1: @RemoveFirstPersonPoint 0
end
0AB2: ret 0

{======================================================================
======================================================================}
:CameraMode
hex
    1
end

:CurrentCamera
hex
    0
end

{======================================================================
======================================================================}
:ToggleSexualCamera_Alt // pose 0@ place 1@
0AB1: @GetInt 1 @CameraMode 11@
if 11@ == 0 // Free mode
then
    0AB1: @RemoveFirstPersonPoint 0
    0AB1: @RelocateFreeCamCenter 0
    02EB: restore_camera_with_jumpcut
end
if 11@ == 1 // Fixed mode
then
    0AB1: @RemoveFirstPersonPoint 0
    0AB1: @GetInt 1 @CurrentCamera 10@
    0AB1: @GetFixedCam 1 cam_id 10@ store_in cam_pos 20@ 21@ 22@ angle 23@ rotation 24@ trgt_x 25@
    0AB1: @InitFixedCam 6 cam_pos 20@ 21@ 22@ angle 23@ rotation 24@ trgt_x 25@ return_in position 20@ 21@ 22@ angle 23@ trgt_x 25@
    0AB1: @CreateFirstPersonPoint 5 cam_pos 20@ 21@ 22@ angle 23@ trgt_x 25@
end
if 11@ == 2 // First person mode
then
    0AB1: @RemoveFirstPersonPoint 0
    0AB1: @GetFPVCamData 0 ret pos_offset 20@ 21@ 22@ angle 23@ rotation 24@ trgt_x 25@
    0AB1: @InitFirstPersonCameraVars_Alt 6 pos_offset 20@ 21@ 22@ angle 23@ rotation 24@ trgt_x 25@ ret update_values position 20@ 21@ 22@ angle 23@ trgt_x 25@
    0AB1: @CreateFirstPersonPoint 5 cam_pos 20@ 21@ 22@ angle 23@ trgt_x 25@
end
0AB2: ret 0

{======================================================================
======================================================================}
:FreezeCamera
0AB1: @GetInt 1 @CameraMode 30@
if 30@ == 0 // Free Camera
then
    068D: get_camera_position_to 10@ 11@ 12@
    015F: set_camera_position 10@ 11@ 12@ rotation 0.0 0.0 0.0
    068E: get_camera_target_point_to 10@ 11@ 12@
    0160: set_camera_point_at 10@ 11@ 12@ switchstyle 2
end
if 30@ == 2 // First Person View Camera
then 0AB1: @StoreInt 2 @FPVCamMoveable 0
end
0AB2: ret 0

{======================================================================
======================================================================}
:RestoreCamera // reset_flag 0@ pose 1@ place 2@
0AB1: @GetInt 1 @CameraMode 30@
if 30@ == 0 // Free Camera
then //03C8: set_camera_directly_before_player//015A: restore_camera
    03C8: set_camera_directly_before_player
    02EB: restore_camera_with_jumpcut
end
if 30@ == 1 // Fixed Camera
then 0AB1: @ToggleSexualCamera_Alt 2 pose 1@ place 2@
end
if 30@ == 2 // First Person View Camera
then 
    if 0@ == 1
    then 0AB1: @ToggleSexualCamera_Alt 2 pose 1@ place 2@
    end
    0AB1: @StoreInt 2 @FPVCamMoveable 1
end
0AB2: ret 0

{======================================================================
======================================================================}
:SetFreeCamera // free_flag 0@ edit_flag 1@ pose 2@ place 3@
if or
0@ == 1
1@ == 1
then
    0AB1: @StoreInt 2 @FPVCamMoveable 1
    0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 0
else
    0AB1: @StoreInt 2 @FPVCamMoveable 0
    0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 1
end

0AB1: @GetInt 1 @CameraMode 20@
if and
20@ == 0 // Free Camera
0@ == 0 // not on Free mode
then
    0AB1: @StoreInt 2 @CameraMode 1 // set as Fixed camera
    0AB1: @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
end
if and
20@ == 1 // Fixed Camera
0@ == 1 // Free mode
then
    0AB1: @StoreInt 2 @CameraMode 0 // set as Free camera
    0AB1: @ToggleSexualCamera_Alt 2 pose 2@ place 3@
end
if or
20@ == 1 // Fixed Camera
20@ == 2 // FPV Camera
then
    0AB1: @StoreInt 2 @FPVCamEditMode 1@
    0AB1: @ToggleSexualCamera_Alt 2 pose 2@ place 3@
end
0AB2: ret 0

{======================================================================
======================================================================}
:Set6numbers
0AB2: ret 6 0@ 1@ 2@ 3@ 4@ 5@

{======================================================================
======================================================================}
:Set3numbers
0AB2: ret 3 0@ 1@ 2@

{======================================================================
======================================================================}
:SetNumbers
0AB2: ret 10 0@ 1@ 2@ 3@ 4@ 5@ 6@ 7@ 8@ 9@

{
How to use this?

If you want to only set one number:
0AB1: @SetNumbers 1 300 ret 16@ DummyVar DummyVar DummyVar DummyVar DummyVar DummyVar DummyVar DummyVar DummyVar // There must be 10 output vars total!

If you want to set 3 numbers (like a coordinate...):
0AB1: @SetNumbers 3 0.0 0.0 -1.0 ret 1@ 2@ 3@ 4@ 4@ 4@ 4@ 4@ 4@ 4@ // 4@ repeated 7 times to make a total of 10 output vars, where 4@ is the dummy var

A coordinate plus angle:
0AB1: @SetNumbers 4 34.9 2467.9 17.5 90.0 ret 1@ 2@ 3@ 4@ Dm Dm Dm Dm Dm Dm

Use as much numbers as you please... But remember: max 10 numbers!
}


{======================================================================
======================================================================}
:GetFirstPersonCamCoordinates // 0@: pose, 1@: place
094B: 10@v = get_active_interior_name_from_actor $PLAYER_ACTOR // 16-byte string
//0AB1: @RecoverPlayerInterior 0 9@

0A9A: 2@ = openfile COORDINATES_FILE mode "rb"  // IF and SET

// Get section
repeat
    0ADA: 3@ = scan_file 2@ format "%s" 4@v //IF and SET
    if 4@v == "FirstPersonCam"
    then break
    end
until 0AD6:   end_of_file 2@ reached // To do: handle cases when string is not present in file

// Get interior name
repeat
    0ADA: 3@ = scan_file 2@ format "%s" 4@v //IF and SET
    if 4@v == 10@v
    then break
    end
until 0AD6:   end_of_file 2@ reached // To do: handle cases when string is not present in file

// Get data
for 4@ = 0 to 7
    for 5@ = 0 to 8
        0ADA: 3@ = scan_file 2@ format "%s" 10@v //IF and SET       // Dummy words
    end
    for 5@ = 0 to 8
        0ADA: 3@ = scan_file 2@ format "%s" 10@v //IF and SET       // Dummy word
        0ADA: 3@ = scan_file 2@ format "%f %f %f %f %f %f %f %f %f %f %f %f" 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ //IF and SET
        if 003B:   4@ == 0@  // (int)
        then
            if 003B:   5@ == 1@  // (int)
            then
                0A9B: closefile 2@
                0AB2: ret 12 cam_pos 10@ 11@ 12@ cam_target 13@ 14@ 15@ rotation 16@ angle 17@ min_rotation 18@ max_rotation 19@ min_angle 20@ max_angle 21@
            end
        end
    end
end
0A9B: closefile 2@
0AB2: ret 6 cam 0.0 0.0 0.0 trgt 0.0 0.0 0.0

{======================================================================
======================================================================}
:GetCenterCoordinates // 0@: pose, 1@: place
094B: 10@v = get_active_interior_name_from_actor $PLAYER_ACTOR // 16-byte string
//0AB1: @RecoverPlayerInterior 0 9@

0A9A: 2@ = openfile COORDINATES_FILE mode "rb"  // IF and SET

// Get section
repeat
    0ADA: 3@ = scan_file 2@ format "%s" 4@v //IF and SET
    if 4@v == "CenterCoordinates"
    then break
    end
until 0AD6:   end_of_file 2@ reached // To do: handle cases when string is not present in file

// Get interior name
repeat
    0ADA: 3@ = scan_file 2@ format "%s" 4@v //IF and SET
    if 4@v == 10@v
    then break
    end
until 0AD6:   end_of_file 2@ reached // To do: handle cases when string is not present in file

// Get data
for 4@ = 0 to 3
    0ADA: 3@ = scan_file 2@ format "%s %s" 10@v 14@v //IF and SET           // Dummy words
    for 5@ = 0 to 8
        0ADA: 3@ = scan_file 2@ format "%s" 10@v //IF and SET               // Dummy word
        for 6@ = 0 to 1
            0ADA: 3@ = scan_file 2@ format "%f %f %f" 10@ 11@ 12@ //IF and SET
            0B14: 20@ = 0@ MOD 2
            0085: 21@ = 0@ // (int)
            0016: 21@ /= 2
            if and            // pose % 2 === 6@        and         pose / 2 === 4@
            003B:   20@ == 6@  // (int)
            003B:   21@ == 4@  // (int)
            then
                if 003B:   5@ == 1@  // (int)   // place === 5@
                then
                    0A9B: closefile 2@
                    0AB2: ret 3 10@ 11@ 12@
                end
            end
        end
    end
end
0A9B: closefile 2@
0AB2: ret 3 0.0 0.0 0.0

{======================================================================
======================================================================}
:GetCameraCoordinates // 0@: current camera, 1@: pose, 2@: place
094B: 10@v = get_active_interior_name_from_actor $PLAYER_ACTOR // 16-byte string
//0AB1: @RecoverPlayerInterior 0 9@

0A9A: 30@ = openfile COORDINATES_FILE mode "rb"  // IF and SET

// Get section
repeat
    0ADA: 31@ = scan_file 30@ format "%s" 4@v //IF and SET
    if 4@v == "FixedCameras"
    then break
    end
until 0AD6:   end_of_file 30@ reached // To do: handle cases when string is not present in file

// Get interior name
repeat
    0ADA: 31@ = scan_file 30@ format "%s" 4@v //IF and SET
    if 4@v == 10@v
    then break
    end
until 0AD6:   end_of_file 30@ reached // To do: handle cases when string is not present in file

// Get data
for 4@ = 0 to 7
    0ADA: 31@ = scan_file 30@ format "%s" 10@v //IF and SET                           // Dummy word
    for 5@ = 0 to 8
        0ADA: 31@ = scan_file 30@ format "%s %s %s" 10@v 14@v 18@v //IF and SET       // Dummy words
        for 6@ = 0 to 11
            0ADA: 31@ = scan_file 30@ format "%f %f %f %f %f %f" 10@ 11@ 12@ 13@ 14@ 15@ //IF and SET
            0ADA: 31@ = scan_file 30@ format "%s" 18@v //IF and SET                   // Dummy word
            if and
            003B:   4@ == 1@  // (int)    // Pose
            003B:   5@ == 2@  // (int)    // Place
            003B:   6@ == 0@  // (int)    // Current Camera
            then
                0A9B: closefile 30@
                0AB2: ret 6 cam_pos 10@ 11@ 12@ cam_target 13@ 14@ 15@
            end
        end
    end
end
0A9B: closefile 30@
0AB2: ret 6 0.0 0.0 0.0 0.0 0.0 0.0

{======================================================================
======================================================================}
:GetActorsCoordinates // inputs: 0@: pose, 1@: place
094B: 10@v = get_active_interior_name_from_actor $PLAYER_ACTOR // 16-byte string
//0AB1: @RecoverPlayerInterior 0 9@

0A9A: 30@ = openfile COORDINATES_FILE mode "rb"  // IF and SET

// Get section
repeat
    0ADA: 31@ = scan_file 30@ format "%s" 4@v //IF and SET
    if 4@v == "ActorsCoordinates"
    then break
    end
until 0AD6:   end_of_file 30@ reached // To do: handle cases when string is not present in file

// Get interior name
repeat
    0ADA: 31@ = scan_file 30@ format "%s" 4@v //IF and SET
    if 4@v == 10@v
    then break
    end
until 0AD6:   end_of_file 30@ reached // To do: handle cases when string is not present in file

// Get data
for 4@ = 0 to 7
    0ADA: 31@ = scan_file 30@ format "%s %s %s %s %s" 10@v 14@v 18@v 22@v 26@v //IF and SET     // Dummy words
    for 5@ = 0 to 8
        0ADA: 31@ = scan_file 30@ format "%s" 10@v //IF and SET                                 // Dummy word
        0ADA: 31@ = scan_file 30@ format "%f %f %f %f %f %f %f %f" 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ //IF and SET
        if and
        003B:   4@ == 0@  // (int)    // Pose
        003B:   5@ == 1@  // (int)    // Place
        then
            0A9B: closefile 30@
            0AB2: ret 8 fem_xyz 10@ 11@ 12@ angle 13@ male_xyz 14@ 15@ 16@ angle 17@
        end
    end
end

0A9B: closefile 30@
0AB2: ret 8 fem_xyz 0.0 0.0 0.0 angle 0.0 male_xyz 0.0 0.0 0.0 angle 0.0

{======================================================================
======================================================================}
:GetPlaceFromString
if 0@s == 'S_PL_1' // Cama
then 0AB2: ret 1 PL_BED
end
if 0@s == 'S_PL_2' // Sofa
then 0AB2: ret 1 PL_SOFA
end
if 0@s == 'S_PL_3' // Mesa
then 0AB2: ret 1 PL_TABLE
end
if 0@s == 'S_PL_4' // Silla
then 0AB2: ret 1 PL_CHAIR
end
if 0@s == 'S_PL_5' // Piso
then 0AB2: ret 1 PL_FLOOR
end
if 0@s == 'S_PL_6' // Baño
then 0AB2: ret 1 PL_BATH
end
if 0@s == 'S_PL_7' // Cocina
then 0AB2: ret 1 PL_KITCHEN
end
if 0@s == 'S_PL_8' // Pared
then 0AB2: ret 1 PL_WALL
end
if 0@s == 'S_PL_9' // De pié
then 0AB2: ret 1 PL_FOOT
end
0AB2: ret 1 404

:GetPoseFromString // gxt_entry 0@ 1@
if 0@s == 'S_BJ_1'
then 0AB2: ret 1 SEX_BJ
end
if 0@s == 'S_HJ_1'
then 0AB2: ret 1 SEX_HJ
end
if 0@s == 'S_CN_1'
then 0AB2: ret 1 SEX_CUNNI
end
if 0@s == 'S_FK_1'
then 0AB2: ret 1 SEX_FUCKING
end
if 0@s == 'S_AN_1'
then 0AB2: ret 1 SEX_ANAL
end
if 0@s == 'S_69_1'
then 0AB2: ret 1 SEX_69
end
if 0@s == 'S_TJ_1'
then 0AB2: ret 1 SEX_TJ
end
if 0@s == 'S_FJ_1'
then 0AB2: ret 1 SEX_FJ
end
if 0@s == 'S_BL_1'
then 0AB2: ret 2 pose SEX_BJ body_type BT_LOLI
end
0AB2: ret 2 pose 404 body_type 0

{======================================================================
======================================================================}
:CheckPoseAndPlaceAvailability
// 0@: pose, 1@: place
0AB1: call_scm_func @GetActorsCoordinates 2 0@ 1@ ret fem_xyza 10@ 11@ 12@ 13@ male_xyza 20@ 21@ 22@ 23@
if and
10@ == 0.0
11@ == 0.0
12@ == 0.0
20@ == 0.0
21@ == 0.0
22@ == 0.0
then 059A: return_false
else 0485: return_true
end
0AB2: ret 0

{======================================================================
======================================================================}
:RelocateFreeCamCenter // entries: none
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@

0AB1: @GetData 2 @PlayerPosX 4 20@
0AB1: @GetData 2 @PlayerPosY 4 21@
0AB1: @GetData 2 @PlayerPosZ 4 22@

005B: 10@ += 20@  // (float)
005B: 11@ += 21@  // (float)
005B: 12@ += 22@  // (float)

00A1: put_actor $PLAYER_ACTOR at 10@ 11@ 12@
//0605: actor $PLAYER_ACTOR perform_animation "sex_pose" IFP "ped" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1

0AB2: ret 0

{======================================================================
======================================================================}
:FirstPersonCameraControl
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 0@
if 056D:   actor 0@ defined
then
    0AB1: @GetInt 1 @CameraMode 11@
    if 11@ == 2 // First person mode
    then
        0AB1: @GetData 1 @FirstPersonCameraVars_MinRotation 14@
        0AB1: @GetData 1 @FirstPersonCameraVars_MaxRotation 15@
        0AB1: @GetData 1 @FirstPersonCameraVars_MinAngle 16@
        0AB1: @GetData 1 @FirstPersonCameraVars_MaxAngle 17@

        0AB1: @GetData 1 @FirstPersonCameraVars_Rotation 30@
        
        0A4A: store_joystick_X_offset_to 4@ Y_offset_to 5@
        0172: 9@ = actor 0@ Z_angle
        0063: 9@ -= 4@ // (float) 
        
        5@ /= 200.0 
        005B: 30@ += 5@ // (float) 
        
        if 0025:   30@ > 15@  // (float)   MAX ROTATION
        then 0087: 30@ = 15@ // (float)
        end
        if 0025:   14@ > 30@  // (float)   MIN ROTATION
        then 0087: 30@ = 14@ // (float)
        end
        if 0025:   9@ > 17@  // (float)   MAX ROTATION
        then 0087: 9@ = 17@ // (float)
        end
        if 0025:   16@ > 9@  // (float)   MIN ROTATION
        then 0087: 9@ = 16@ // (float)
        end
        0173: set_actor 0@ Z_angle_to 9@
        067C: put_camera_on_actor 0@ offset 0.0 0.0 -1.0 rotation 0.0 0.5 30@ tilt 0.0 switchstyle 2 
        
        0AB1: @StoreData 2 @FirstPersonCameraVars_Rotation 30@
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:FirstPersonCameraVars_Actor
hex
00 00 00 00
end
:FirstPersonCameraVars_Rotation
hex
00 00 00 00
end
:FirstPersonCameraVars_MinRotation
hex
00 00 00 00
end
:FirstPersonCameraVars_MaxRotation
hex
00 00 00 00
end
:FirstPersonCameraVars_MinAngle
hex
00 00 00 00
end
:FirstPersonCameraVars_MaxAngle
hex
00 00 00 00
end
:FPVCamMoveable
hex
0
end
:FPVCamEditMode
hex
0
end
{======================================================================
======================================================================}
:InitFirstPersonCameraVars_Alt // pos_offset 0@ 1@ 2@ angle 3@ rotation 4@ trgt_x 5@
if and
0@ == 0.0
1@ == 0.0
2@ == 0.0
3@ == 0.0
4@ == 0.0
5@ == 0.0
then 0AB1: @SetNumbers 5 pos_offset 0.15 0.8 0.65 angle 165.0 rot -1.25 store_in 0@ 1@ 2@ 3@ 4@ dummies 30@ 30@ 30@ 30@ 30@ // Initialize
end

// Position    
0AB1: @GetData 2 @PlayerPosX 0 10@
0AB1: @GetData 2 @PlayerPosY 0 11@
0AB1: @GetData 2 @PlayerPosZ 0 12@
005B: 0@ += 10@  // (float)
005B: 1@ += 11@  // (float)
005B: 2@ += 12@  // (float)

0087: 16@ = 3@ // (float)      // Min Angle
16@ -= 45.0
0087: 17@ = 3@ // (float)      // Max Angle
17@ += 45.0
    
0087: 14@ = 4@ // (float)       // Min Rotation
14@ -= 0.4
0087: 15@ = 4@ // (float)       // Min Rotation
15@ += 0.4

0AB1: @StoreData 2 @FirstPersonCameraVars_Rotation 4@
0AB1: @StoreData 2 @FirstPersonCameraVars_MinRotation 14@
0AB1: @StoreData 2 @FirstPersonCameraVars_MaxRotation 15@
0AB1: @StoreData 2 @FirstPersonCameraVars_MinAngle 16@
0AB1: @StoreData 2 @FirstPersonCameraVars_MaxAngle 17@
0AB2: ret 5 position 0@ 1@ 2@ angle 3@ trgt_x 5@

{======================================================================
======================================================================}
:FirstPersonCameraControl_Alt // entries: none
0AB1: @GetInt 1 @FPVCamEditMode 0@
0AB1: @GetInt 1 @FPVCamMoveable 1@

0AB1: @GetInt 1 @CameraMode 11@
if or
11@ == 1 // Fixed Camera
11@ == 2 // First Person Camera
then
    0AB1: @GetData 1 @FirstPersonCameraVars_Actor 10@
    if 056D:   actor 10@ defined
    then
        0AB1: @GetData 1 @FirstPersonCameraVars_MinRotation 14@
        0AB1: @GetData 1 @FirstPersonCameraVars_MaxRotation 15@
        0AB1: @GetData 1 @FirstPersonCameraVars_MinAngle 16@
        0AB1: @GetData 1 @FirstPersonCameraVars_MaxAngle 17@
        
        0AB1: @GetData 1 @FirstPersonCameraVars_Rotation 30@
        
        if 1@ == 1  // flag_moveable
        then
            0A4A: store_joystick_X_offset_to 4@ Y_offset_to 5@
            0172: 9@ = actor 10@ Z_angle
            0063: 9@ -= 4@ // (float)
            
            5@ /= 200.0 
            005B: 30@ += 5@ // (float)
        end
        
        if and
        0@ == 0  // not in Edit Mode
        11@ == 2 // First Person Camera
        then
            if 0025:   30@ > 15@  // (float)   MAX ROTATION
            then 0087: 30@ = 15@ // (float)
            end
            if 0025:   14@ > 30@  // (float)   MIN ROTATION
            then 0087: 30@ = 14@ // (float)
            end
            if 0025:   9@ > 17@  // (float)   MAX ROTATION
            then 0087: 9@ = 17@ // (float)
            end
            if 0025:   16@ > 9@  // (float)   MIN ROTATION
            then 0087: 9@ = 16@ // (float)
            end
        end
        if 1@ == 1  // flag_moveable
        then 0173: set_actor 10@ Z_angle_to 9@
        end
        067C: put_camera_on_actor 10@ offset 0.0 0.0 -1.0 rotation 0.0 0.5 30@ tilt 0.0 switchstyle 2        
        0AB1: @StoreData 2 @FirstPersonCameraVars_Rotation 30@
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:StoreFPVCamData // position 0@ 1@ 2@ angle 3@ rotation 4@ trgt_x 5@
0AB1: @StoreData 3 @CamsOffsetsMenu_FPVCamPosition 0@ 0
0AB1: @StoreData 3 @CamsOffsetsMenu_FPVCamPosition 1@ 4
0AB1: @StoreData 3 @CamsOffsetsMenu_FPVCamPosition 2@ 8
0AB1: @StoreData 2 @CamsOffsetsMenu_FPVCamAngle 3@
0AB1: @StoreData 2 @CamsOffsetsMenu_FPVCamRotation 4@
0AB1: @StoreData 2 @CamsOffsetsMenu_FPVCamTrgtX 5@
0AB2: ret 0

{======================================================================
======================================================================}
:GetFPVCamData // entries: none
0AB1: @GetData 2 @CamsOffsetsMenu_FPVCamPosition 0 10@
0AB1: @GetData 2 @CamsOffsetsMenu_FPVCamPosition 4 11@
0AB1: @GetData 2 @CamsOffsetsMenu_FPVCamPosition 8 12@
0AB1: @GetData 1 @CamsOffsetsMenu_FPVCamAngle 13@
0AB1: @GetData 1 @CamsOffsetsMenu_FPVCamRotation 14@
0AB1: @GetData 1 @CamsOffsetsMenu_FPVCamTrgtX 15@
0AB2: ret 6 pos_offset 10@ 11@ 12@ angle 13@ rotation 14@ trgt_x 15@

{======================================================================
======================================================================}
:CreateFirstPersonPoint // cam_pos 0@ 1@ 2@ angle 3@ dummy_angle 4@
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 10@
if 856D:   not actor 10@ defined
then
    Model.Load(#NULL)
    repeat
    wait 0 
    until Model.Available(#NULL)
    10@ = Actor.Create(Mission1, #NULL, 0@, 1@, 2@)
    Actor.Health(10@) = 2000
    Actor.SetImmunities(10@, 1, 1, 1, 1, 1)
    Actor.Angle(10@) = 3@
    0337: set_actor 10@ visibility 0 
    0619: enable_actor 10@ collision_detection 0 
    Model.Destroy(#NULL)
else
    00A1: put_actor 10@ at 0@ 1@ 2@
    Actor.Angle(10@) = 3@
end
0AB1: @StoreData 2 @FirstPersonCameraVars_Actor 10@
0AB2: ret 0

{======================================================================
======================================================================}
:RemoveFirstPersonPoint // entries: none
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 10@
068D: get_camera_position_to 20@ 21@ 22@
068E: get_camera_target_point_to 25@ 26@ 27@
015F: set_camera_position 20@ 21@ 22@ rotation 0.0 0.0 0.0
0160: set_camera_point_at 25@ 26@ 27@ switchstyle 2
if 056D:   actor 10@ defined
then 009B: destroy_actor 10@
end
0AB1: @StoreData 2 @FirstPersonCameraVars_Actor 10@
0AB2: ret 0

{======================================================================
======================================================================}
:MoveFirstPersonPoint // type_of_cam 0@ cam_id 1@ normalized_dirs sides 2@ front 3@ pos_z 4@ speed 5@
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 10@
if 056D:   actor 10@ defined
then
    008F: 2@ = integer 2@ to_float
    006B: 2@ *= 5@  // (float)
    008F: 3@ = integer 3@ to_float
    006B: 3@ *= 5@  // (float)
    008F: 4@ = integer 4@ to_float
    006B: 4@ *= 5@  // (float)
    000B: 4@ += -1.0

    04C4: store_coords_to 11@ 12@ 13@ from_actor 10@ with_offset 2@ 3@ 4@
    Actor.PutAt(10@, 11@, 12@, 13@)
    0AB1: @StoreData 2 @FirstPersonCameraVars_Actor 10@
    
    068D: get_camera_position_to 20@ 21@ 22@
    068E: get_camera_target_point_to 25@ 26@ 27@
    0172: 23@ = actor 10@ Z_angle
    0AB1: @GetData 1 @FirstPersonCameraVars_Rotation 24@

    0AB1: @GetData 1 @PlayerPosX 20@
    0AB1: @GetData 1 @PlayerPosY 21@
    0AB1: @GetData 1 @PlayerPosZ 22@
    
    0063: 11@ -= 20@  // (float)
    0063: 12@ -= 21@  // (float)
    0063: 13@ -= 22@  // (float)
    
    if 0@ == 1 // Fixed Camera
    then
        0AB1: @GetData 2 @FemaleOffset 0 25@
        0AB1: @GetData 2 @FemaleOffset 4 26@
        0AB1: @GetData 2 @FemaleOffset 8 27@
        
        0063: 11@ -= 25@  // (float)
        0063: 12@ -= 26@  // (float)
        0063: 13@ -= 27@  // (float)
    end

    if 0@ == 1 // Fixed Camera
    then 0AB1: @StoreFixedCam 7 cam_id 1@ cam_pos 11@ 12@ 13@ angle 23@ rotation 24@ trgt_x 25@
    end
    if 0@ == 2 // First Person View Camera
    then 0AB1: @StoreFPVCamData 6 position 11@ 12@ 13@ angle 23@ rotation 24@ trgt_x 25@
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:GetFixedCam // cam_id 0@
0085: 30@ = 0@ // (int)
0012: 30@ *= 6
0012: 30@ *= 4

for 31@ = 0 to 5
    0AB1: @GetData 2 @CamsOffsetsMenu_FixedCam 30@ 10@(31@,6f)
    000A: 30@ += 4
end
0AB2: ret 6 cam_pos 10@ 11@ 12@ cam_trgt 13@ 14@ 15@

{======================================================================
======================================================================}
:StoreFixedCam // cam_id 0@ cam_pos 1@ 2@ 3@ cam_trgt 4@ 5@ 6@
0085: 30@ = 0@ // (int)
0012: 30@ *= 6
0012: 30@ *= 4

for 31@ = 0 to 5
    0AB1: @StoreData 3 @CamsOffsetsMenu_FixedCam 1@(31@,6f) 30@
    000A: 30@ += 4
end
0AB2: ret 0

{======================================================================
======================================================================}
:InitFixedCam // cam_pos 0@ 1@ 2@ angle 3@ rotation 4@ trgt_x 5@
if and
0@ == 0.0
1@ == 0.0
2@ == 0.0
3@ == 0.0
4@ == 0.0
5@ == 0.0
then 0AB1: @SetNumbers 5 pos_offset 0.8 -1.22 1.0 angle 30.0 rot -1.32 store_in 0@ 1@ 2@ 3@ 4@ dummies 30@ 30@ 30@ 30@ 30@ // Initialize
end

0AB1: @GetData 1 @PlayerPosX 10@
0AB1: @GetData 1 @PlayerPosY 11@
0AB1: @GetData 1 @PlayerPosZ 12@

0AB1: @GetData 2 @FemaleOffset 0 20@
0AB1: @GetData 2 @FemaleOffset 4 21@
0AB1: @GetData 2 @FemaleOffset 8 22@

005B: 0@ += 10@  // (float)
005B: 1@ += 11@  // (float)
005B: 2@ += 12@  // (float)

005B: 0@ += 20@  // (float)
005B: 1@ += 21@  // (float)
005B: 2@ += 22@  // (float)

0AB1: @StoreData 2 @FirstPersonCameraVars_Rotation 4@
0AB2: ret 5 position 0@ 1@ 2@ angle 3@ trgt_x 5@

{======================================================================
======================================================================}
:MoveFixedCamPoint // cam_id 0@ normalized_dirs sides 1@ front 2@ pos_z 3@ speed 4@
0AB1: @GetData 1 @FirstPersonCameraVars_Actor 10@
if 056D:   actor 10@ defined
then
    008F: 1@ = integer 1@ to_float
    006B: 1@ *= 4@  // (float)
    008F: 2@ = integer 2@ to_float
    006B: 2@ *= 4@  // (float)
    008F: 3@ = integer 3@ to_float
    006B: 3@ *= 4@  // (float)
    000B: 3@ += -1.0
    
    04C4: store_coords_to 11@ 12@ 13@ from_actor 10@ with_offset 1@ 2@ 3@
    Actor.PutAt(10@, 11@, 12@, 13@)
    0AB1: @StoreData 2 @FirstPersonCameraVars_Actor 10@
    
    068D: get_camera_position_to 20@ 21@ 22@
    068E: get_camera_target_point_to 25@ 26@ 27@
    0172: 23@ = actor 10@ Z_angle
    0AB1: @GetData 1 @FirstPersonCameraVars_Rotation 24@
    
    0AB1: @GetData 1 @PlayerPosX 20@
    0AB1: @GetData 1 @PlayerPosY 21@
    0AB1: @GetData 1 @PlayerPosZ 22@
    
    0AB1: @GetData 2 @FemaleOffset 0 25@
    0AB1: @GetData 2 @FemaleOffset 4 26@
    0AB1: @GetData 2 @FemaleOffset 8 27@
    
    0063: 11@ -= 20@  // (float)
    0063: 12@ -= 21@  // (float)
    0063: 13@ -= 22@  // (float)
    
    0063: 11@ -= 25@  // (float)
    0063: 12@ -= 26@  // (float)
    0063: 13@ -= 27@  // (float)
    
    0AB1: @StoreFixedCam 7 cam_id 0@ cam_pos 11@ 12@ 13@ angle 23@ rotation 24@ trgt_x 25@
end
0AB2: ret 0

{======================================================================
======================================================================}
:FirstPersonCameraMoveable_KeysControl // camera 0@ cam_id 1@
14@ = 0.01
if 0AB0:   key_pressed KEY_SHIFT // speed boost
then 14@ *= 5.0
end
if 0AB0:   key_pressed KEY_W // front +
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides 0 front 1 height 0 speed 14@
end
if 0AB0:   key_pressed KEY_S // front -
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides 0 front -1 height 0 speed 14@
end
if 0AB0:   key_pressed KEY_D // sides ->
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides 1 front 0 height 0 speed 14@
end
if 0AB0:   key_pressed KEY_A // sides <-
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides -1 front 0 height 0 speed 14@
end
if 0AB0:   key_pressed KEY_E // up
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides 0 front 0 height 1 speed 14@
end
if 0AB0:   key_pressed KEY_Q // down
then 0AB1: @MoveFirstPersonPoint 6 type_of_cam 0@ cam_id 1@ normalized_dirs sides 0 front 0 height -1 speed 14@
end
        
// Player moved the mouse cursor?
0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 0 24@
0AB1: @GetData 2 @CamsOffsetsMenu_FPVJoystickOffset 4 25@
0A4A: store_joystick_X_offset_to 22@ Y_offset_to 23@
if or
8045:   not 22@ == 24@  // (float)
8045:   not 23@ == 25@  // (float)
then 0AB1: @MoveFirstPersonPoint 5 type_of_cam 0@ cam_id 1@ normalized_dirs sides 0 front 0 height 0
end
0AB2: ret 0

{======================================================================
======================================================================}
:FreeCamEditMode

// Change angle of view
10@s = 'CFG_D7' // <
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 170.0 389.0 GXT 10@ 11@
10@s = 'CFG_E3' // Change Angle of View
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 183.0 390.0 GXT 10@ 11@
10@s = 'CFG_D8' // >
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 286.0 389.0 GXT 10@ 11@
if 0AB1: @CursorClickCheck 4 box_position 175.0 395.0 box_size 16.0 10.0 // <
then
    0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
    12@ += 90.0
    0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
    0AB1: @CamsOffsets_SetFixedCamera 0
end
if 0AB1: @CursorClickCheck 4 box_position 289.0 395.0 box_size 16.0 10.0 // >
then
    0AB1: @GetData 2 @ActorsOffsetsMenu_AngleOfView 0 12@
    12@ -= 90.0
    0AB1: @StoreData 3 @ActorsOffsetsMenu_AngleOfView 12@ 0
    0AB1: @CamsOffsets_SetFixedCamera 0
end

// Keys control
0AB1: @GetIncreaseSteps_Cam 1 0.01 returned_values steps 12@ 13@ negative_steps 14@ 15@
0AB1: @SetNumbers 2 0.01 -0.01 ret 16@ 17@ dummies 31@ 31@ 31@ 31@ 31@ 31@ 31@ 31@    
if 0AB0:   key_pressed KEY_SHIFT // speed boost
then
    12@ *= 5.0
    13@ *= 5.0
    14@ *= 5.0
    15@ *= 5.0
    16@ *= 5.0
    17@ *= 5.0
end
if 0AB0:   key_pressed KEY_W // front +
then 0AB1: @MoveFreeCamCenter 3 step_x 12@ step_y 13@ step_z 0.0
end
if 0AB0:   key_pressed KEY_S // front -
then 0AB1: @MoveFreeCamCenter 3 step_x 14@ step_y 15@ step_z 0.0
end
if 0AB0:   key_pressed KEY_D // sides ->
then 0AB1: @MoveFreeCamCenter 3 step_x 13@ step_y 14@ step_z 0.0
end
if 0AB0:   key_pressed KEY_A // sides <-
then 0AB1: @MoveFreeCamCenter 3 step_x 15@ step_y 12@ step_z 0.0
end
if 0AB0:   key_pressed KEY_E // up
then 0AB1: @MoveFreeCamCenter 3 step_x 0.0 step_y 0.0 step_z 16@
end
if 0AB0:   key_pressed KEY_Q // down
then 0AB1: @MoveFreeCamCenter 3 step_x 0.0 step_y 0.0 step_z 17@
end

10@s = 'CFSV_7' // Back
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 330.0 390.0 GXT 10@ 11@
if 0AB1: @CursorClickCheck 4 box_position 340.0 395.0 box_size 26.0 11.0 // Close or Back
then
    0485:  return_true
    0AB2: ret 0
end
059A:  return_false
0AB2: ret 0

{======================================================================
======================================================================}
:DEBUG_ShowFixedCamStorage
0A9A: 30@ = openfile LOG_FILE mode "wb+"  // IF and SET
for 10@ = 0 to 11
    0AB1: @GetFixedCam 1 cam_id 10@ ret 20@ 21@ 22@ 23@ 24@ 25@
    0AD9: write_formatted_text "pos %f %f %f angle %f rot %f x_trgt %f%c%c" in_file 30@ vars 20@ 21@ 22@ 23@ 24@ 25@ 0xd 0xa
end
0A9B: closefile 30@
0AB2: ret 0

{======================================================================
======================================================================}
:GetData // 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 4 virtual_protect 0
0AB2: ret 1 11@

:StoreData // 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 4 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:ARC_SIN // 0@
if 0@ == 1.0
then 31@ = 90.0
end
if 0@ == -1.0
then 31@ = -90.0
end
if and
0@ > -1.0
1.0 > 0@
then
    1@ = 51.0 // The greater this number, the better (i guess)
    31@ = 0.0
    for 10@ = 1.0 to 1@ step 2.0
        2@ = 1.0
        for 3@ = 1.0 to 10@ step 2.0
            006B: 2@ *= 3@  // (float)
        end
        
        4@ = 1.0
        0087: 5@ = 10@ // (float)
        5@ += 1.0
        for 3@ = 2.0 to 5@ step 2.0
            006B: 4@ *= 3@  // (float)
        end
        
        0087: 30@ = 0@ // (float)
        0AEE: 30@ = 30@ exp 10@ //all floats
        0073: 30@ /= 10@ // (float)
        006B: 30@ *= 2@  // (float)
        0073: 30@ /= 4@ // (float)
        
        005B: 31@ += 30@  // (float)
    end
    const
        NUM_PI = 3.14159265359
    end
    31@ *= 180.0
    31@ /= NUM_PI
else 31@ = 0.0 // ??
end
0AB2: ret 1 31@

{======================================================================
======================================================================}
:ARC_TAN // value 0@
1@ = 10 // This number is supposed to be as big as possible
31@ = 0.0
for 2@ = 0 to 1@
    0093: 3@ = integer 1@ to_float
    
    4@ = -1.0
    0AEE: 4@ = 4@ exp 3@ //all floats
    
    0087: 5@ = 3@ // (float)
    5@ *= 2.0
    5@ += 1.0
    
    0AEE: 30@ = 0@ exp 5@ //all floats
    006B: 30@ *= 4@  // (float)
    0073: 30@ /= 5@ // (float)
    
    005B: 31@ += 30@  // (float)
end
0AB2: ret 1 result 31@

