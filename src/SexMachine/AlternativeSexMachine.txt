{======================================================================
======================================================================}
:CmSexStart2
$ONMISSION = 1

// Entry parameters
0AB1: @StoreActorsInfo 4 gxt_fem 3@ 4@ gtx_male 5@ 6@

// Initialize Machine
    0581: enable_radar 0
    0826: enable_hud 0
    03F0: enable_text_draw 1
    03E0: draw_text_behind_textures 1
    08E7: disable_entrance_markers 1

    GenderFlag = 1 // Gender of the player
    BodyType = BT_NORMAL
    0AB1: @StoreInt 2 @SxMn_Slot 0
    0AB1: @StoreInt 2 @SxMn_Pose SexPoseInt
    0AB1: @StoreInt 2 @SxMn_Place SexPlace
    0AB1: @LoadData 2 SexPoseInt SexPlace
    0AB1: @GenerateSexFiles 3 SexPoseInt SexPlace BodyType

    0AB1: @StoreInt 2 @CameraMode 0 // Free Camera
0AB1: @InitCursorControl 0
ShowSexualStats = true
SexState = 0
SexMenuState = 0
SexStat_Speed = 0
07CC: set_player $PLAYER_CHAR button_15 0

// Initialize offset to 0.0
0AB1: @StoreData 3 @PlayerPosX 0.0 4
0AB1: @StoreData 3 @PlayerPosY 0.0 4
0AB1: @StoreData 3 @PlayerPosZ 0.0 4

0AB1: @ChangeGenderSkin 1 GenderFlag

while true
wait 0
    // Buttons UP - DOWN - ACCEPT - CANCEL acknowledge
    gosub @PulseButtonMachine

    if SexMenuState > 0             // 0 is the only state when player can move
    then 0AB1: @CursorControl 0     // so is best to activate cursor when player can't!
    end
    gosub @SexMachine_Alt
    if SexMenuState == 0
    then
        if PulseButtonState == PULSED_ENTER_VEH
        then break // End Sex Activity
        end
        if 00E1:   player 0 pressed_key PAD_ANSWER_PHONE // TAB
        then
            SexMenuState = 1
            01B4: set_player $PLAYER_CHAR can_move 0
        end
    end
    if SexMenuState == 1
    then
        0AB1: @ShowSexualMenu 3 SexPoseInt SexPlace generate_sex_files 1
        if 0AB1: @CursorClickCheck 4 box_position 542.0 416.0 box_size 162.0 48.0 // Change Button Click
        then
            0AB1: @GetInt 1 @SxMn_Pose Dummy1
            0AB1: @GetInt 1 @SxMn_Place Dummy2
            0AB1: @SxMn_GetGXTPose 2 place Dummy2 offset Dummy1 ret gxt_entry Dummy3 Dummy4
            0AB1: @GetPoseFromString 2 gxt_entry Dummy3 Dummy4 ret pose Dummy1 body_type Dummy5
            0085: SexPoseInt = Dummy1 // (int)
            0085: SexPlace = Dummy2 // (int)
            0085: BodyType = Dummy5 // (int)
            gosub @ChangePoseAndPlace_Alt
            SexMenuState = 2
        end
        if 0AB1: @CursorClickCheck 4 box_position 76.0 436.0 box_size 143.0 13.0 // Cancel Button
        then
            gosub @WaitUntilReleaseClickButton
            SexMenuState = 0
            01B4: set_player $PLAYER_CHAR can_move 1
        end
        if PulseButtonState == PULSED_ENTER_VEH
        then 01B4: set_player $PLAYER_CHAR can_move 1
        end
    end
    if SexMenuState == 5
    then
        0AB1: @ShowSexualMenu 3 SexPoseInt SexPlace generate_sex_files 0
        if 0AB1: @CursorClickCheck 4 box_position 542.0 416.0 box_size 162.0 48.0 // Change Button Click
        then
            0AB1: @GetInt 1 @SxMn_Pose Dummy1
            0AB1: @GetInt 1 @SxMn_Place Dummy2
            0AB1: @SxMn_GetGXTPose 2 place Dummy2 offset Dummy1 ret gxt_entry Dummy3 Dummy4
            0AB1: @GetPoseFromString 2 gxt_entry Dummy3 Dummy4 ret pose Dummy1 body_type Dummy5
            if or
            803B:   not Dummy1 == SexPoseInt  // (int)
            803B:   not Dummy2 == SexPlace  // (int)
            803B:   not Dummy5 == BodyType  // (int)
            then
                fade 0 750
                wait 750
                0085: SexPoseInt = Dummy1 // (int)
                0085: SexPlace = Dummy2 // (int)
                0085: BodyType = Dummy5 // (int)
                gosub @RemovePosesAndActors
                0AB1: @GenerateSexFiles 3 SexPoseInt SexPlace BodyType
                gosub @ChangePoseAndPlace_Alt
                SexMenuState = 2
                wait 250
                fade 1 750
            else
                if and
                003B:   Dummy1 == SexPoseInt  // (int)
                003B:   Dummy2 == SexPlace  // (int)
                then
                    0AB1: @GetInt 1 @SxMn_Slot Dummy3
                    if not Dummy3 == 0
                    then
                        SexMenuState = 2
                        0AB1: @RetrieveInformationFromInternalStorage 6 slot Dummy3 PornActress PornActor SexPoseInt SexPlace SpeedState
                        0AB1: @RestoreCamera 3 1 SexPoseInt SexPlace
                    end
                end
            end
        end
        if 0AB1: @CursorClickCheck 4 box_position 76.0 436.0 box_size 143.0 13.0 // Cancel Button
        then
            gosub @WaitUntilReleaseClickButton
            SexMenuState = 2
            0AB1: @RestoreCamera 0
        end
        if PulseButtonState == PULSED_ENTER_VEH
        then
            SexMenuState = 2
            PulseButtonState++
            0AB1: @RestoreCamera 0
        end
    end
    if SexMenuState == 2
    then
        if 0AB1: @CursorClickCheck 4 box_position 130.0 398.0 box_size 64.0 12.0 // Poses button
        then
            0AB1: @FreezeCamera 0
            SexMenuState=5
        end
        if 0AB1: @CursorClickCheck 4 box_position 130.0 411.0 box_size 64.0 12.0 // Offsets button
        then
            0AB1: @LoadData 2 SexPoseInt SexPlace
            0373: set_camera_directly_behind_player
            02EB: restore_camera_with_jumpcut
            0605: actor $PLAYER_ACTOR perform_animation "IDLE_stance" IFP "ped" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
            //0AB1: @ChangeGenderSkin 1 GenderFlag
            SexMenuState = 3
        end
        if 0AB1: @CursorClickCheck 4 box_position 130.0 424.0 box_size 64.0 12.0 // Save/Load button
        then
            0AB1: @LoadData 2 SexPoseInt SexPlace
            SexMenuState = 4
        end
        if 0AB1: @CursorClickCheck 4 box_position 130.0 437.0 box_size 64.0 12.0 // Back button
        then
            0AB1: @StoreInt 2 @CameraMode 0 // Free Camera
            gosub @RemovePosesAndActors
            0AB1: @ChangeGenderSkin 1 GenderFlag
            SexMenuState=0
        end
        if PulseButtonState == PULSED_ENTER_VEH
        then
            0AB1: @StoreInt 2 @CameraMode 0 // Free Camera
            gosub @RemovePosesAndActors
            0AB1: @ChangeGenderSkin 1 GenderFlag
            SexMenuState=0
        end
    end
    if SexMenuState == 3
    then
        0AB1: @ActorsOffsetsMenu 6 SexMenuState PornActress PornActor SexPoseInt SexPlace SpeedState returned SexMenuState
        if PulseButtonState == PULSED_ENTER_VEH
        then
            SexMenuState = 2
            PulseButtonState++
            0AB1: @RelocateFreeCamCenter 0
            0687: clear_actor $PLAYER_ACTOR task
        end
        if SexMenuState == 0
        then SexMenuState = 2
        end
    end
    if SexMenuState == 4
    then
        0AB1: @SaveLoadMenu 6 SexMenuState PornActress PornActor SexPoseInt SexPlace SpeedState returned SexMenuState
        if PulseButtonState == PULSED_ENTER_VEH
        then
            PulseButtonState++
            SexMenuState = 2
        end
    end
    if ShowSexualStats==true
    then
        0AB1: @ShowSexualStats 3 SexStat_Climax SexStat_Speed SexStat_Pleasure
        0AB1: @ShowSettingTexts 5 SexMenuState SpeedState SexPoseInt SexPlace true
    end
    if not SexMenuState == 3
    then gosub @SexualMenuMachine
    end
end

0581: enable_radar 1
0826: enable_hud 1
03F0: enable_text_draw 0
03E0: draw_text_behind_textures 0
08E7: disable_entrance_markers 0

0AB1: @ChangeGenderSkin 1 GenderFlag

repeat // Avoid the false "enter car" effect when you end the SexMachine
wait 0
until 80E1:   not player 0 pressed_key 15
07CC: set_player $PLAYER_CHAR button_15 1
return

{======================================================================
======================================================================}
:SexMachine_Alt
if SexState == 0
then
    if SexMenuState == 2
    then
        if 0AB1: @CursorClickCheck 4 box_position 203.0 395.0 box_size 72.0 11.0 // Camera mode Button
        then
            0AB1: @GetInt 1 @CameraMode Dummy1
            0AB1: @IncOrDecIntVariable 4 var_to_increase Dummy1 min_value 1 max_value 2 flag FLAG_INCREASE store_into Dummy1
            0AB1: @StoreInt 2 @CameraMode Dummy1
            0AB1: @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
        end
        if 0AB1: @CursorClickCheck 4 box_position 310.0 395.0 box_size 26.0 11.0 // 'Free' Button
        then
            SexMenuState = 8
            0AB1: @SetFreeCamera 4 free_flag 1 edit_flag 0 SexPoseInt SexPlace
        end
        if 0AB1: @CursorClickCheck 4 box_position 340.0 395.0 box_size 26.0 11.0 // 'Edit' Button
        then
            SexMenuState = 8
            0AB1: @SetFreeCamera 4 free_flag 0 edit_flag 1 SexPoseInt SexPlace
        end
        0AB1: @ManageAnimationsMenu 5 SpeedState SexPoseInt SexPlace PornActress PornActor
    end
    if SexMenuState == 8
    then
        if 0AB1: @CursorClickCheck 5 box_position 320.0 224.0 box_size 640.0 448.0 disable_draw_box 1 // Full screen box
        then
            SexMenuState = 2
            0AB1: @SetFreeCamera 4 free_flag 0 edit_flag 0 SexPoseInt SexPlace
        end
        if PulseButtonState == PULSED_ENTER_VEH
        then
            SexMenuState = 2
            0AB1: @SetFreeCamera 4 free_flag 0 edit_flag 0 SexPoseInt SexPlace
            PulseButtonState++
        end
        0AB1: @GetInt 1 @CameraMode Dummy1
        if Dummy1 == 0 // Free Camera
        then
            if 00E1:   player 0 pressed_key PAD_ANSWER_PHONE
            then
                SexMenuState = 9
                0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 1
                0AB1: @RemoveFirstPersonPoint 0
                0AB1: @CreateFreeCamCenter 0
            end
        end
    end
    if SexMenuState == 9
    then
        if 0AB1: @FreeCamEditMode 0
        then
            SexMenuState = 8
            0AB1: @RemoveFreeCamCenter 0
            0AB1: @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
            0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 0
        end
        if PulseButtonState == PULSED_ENTER_VEH
        then
            SexMenuState = 8
            0AB1: @RemoveFreeCamCenter 0
            0AB1: @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
            0AB1: @StoreInt 2 @CursorControl_FlagShowCursor 0
        end
    end

    0AB1: @GetInt 1 @CameraMode Dummy1
    if Dummy1 == 1 // Fixed Camera
    then
        0494: get_joystick 0 data_to MoveAxisX MoveAxisY SpAxisX SpAxisY
        if 0018:   SpAxisX > 100 // RIGHT
        then SexState = 4
            0AB1: @GetInt 1 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @IncOrDecIntVariable 4 MoveAxisY 0 11 FLAG_INCREASE MoveAxisY
            0AB1: @StoreInt 2 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
        end
        if 001A:   -100 > SpAxisX // LEFT
        then SexState = 5
            0AB1: @GetInt 1 @CurrentCamera MoveAxisY
            0AB1: @IncOrDecIntVariable 4 var_to_increase MoveAxisY min_value 0 max_value 11 flag FLAG_DECREASE store_into MoveAxisY
            0AB1: @StoreInt 2 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
        end
        if 0AB1: @CursorClickCheck 4 box_position 247.0 395.0 box_size 13.0 12.0
        then
            0AB1: @GetInt 1 @CurrentCamera MoveAxisY
            0AB1: @IncOrDecIntVariable 4 var_to_increase MoveAxisY min_value 0 max_value 11 flag FLAG_DECREASE store_into MoveAxisY
            0AB1: @StoreInt 2 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
        end
        if 0AB1: @CursorClickCheck 4 box_position 263.0 395.0 box_size 13.0 12.0
        then
            0AB1: @GetInt 1 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @IncOrDecIntVariable 4 MoveAxisY 0 11 FLAG_INCREASE MoveAxisY
            0AB1: @StoreInt 2 @CurrentCamera MoveAxisY
            0AB1: call_scm_func @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
        end
    end
end
if SexState == 1
then
    if 80E1:   not player 0 pressed_key PAD_CHANGE_CAMERA_VIEW
    then SexState=0
    end
end
if SexState == 2
then
    if 80E1:   not player 0 pressed_key PAD_ANSWER_PHONE
    then SexState=0
    end
end
if SexState == 4
then
    if 8018:   not SpAxisX > 100
    then SexState = 0
    end
end
if SexState == 5
then
    if 801A:   not -100 > SpAxisX
    then SexState = 0
    end
end

// Increase / Decrease Speed
Dummy5=0
0AB1: @GetInt 1 @CameraMode Dummy1
0AB1: @GetInt 1 @FPVCamEditMode Dummy2
if SexMenuState == 2
then Dummy5 = 1
end
if and
SexMenuState == 8 // state when the camera is free or when in edit mode
Dummy2 == 0 // not in Edit Mode
then Dummy5 = 1
end
if Dummy5 == 1
then
    if 00E1:   player 0 pressed_key PAD_PREV_WEP_ZOOM_IN
    then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_INCREASE SexStat_Speed
    end
    if 00E1:   player 0 pressed_key PAD_NEXT_WEP_ZOOM_OUT
    then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_DECREASE SexStat_Speed
    end
end

// Increase / Decrease Speed (Clickable bar)
0AB1: @GetInt 1 @CamsOffsetsMenu_FPVCamMoveable Dummy1
if Dummy1 == 0 // not in Edit Mode (FPV/Fixed)
then
    if or
    SexMenuState == 2 // Having sex
    SexMenuState == 3 // Changing actors offsets
    SexMenuState == 6 // Changing cameras offsets
    then 0AB1: @GetSpeedStatFromBarClick 1 SexStat_Speed returned_value SexStat_Speed
    end
end

// Increase Pleasure
0AB1: @PleasureMachine 3 SexStat_Pleasure PleasureState SexStat_Speed ret SexStat_Pleasure PleasureState

// Increase Climax
0AB1: @ClimaxMachine 3 SexStat_Climax ClimaxState SexStat_Speed ret SexStat_Climax ClimaxState

// Change from stopped to slow or fast
if and
056D:   actor PornActress defined
056D:   actor PornActor defined
then gosub @SpeedMachine
end

// First Person Camera control
if {and
not SexMenuState == 6
not SexMenuState == 7}
not SexMenuState == 3
then 0AB1: @FirstPersonCameraControl_Alt 0
end

// Keys Control for Edit Mode
0AB1: @GetInt 1 @FPVCamEditMode Dummy1
0AB1: @GetInt 1 @CameraMode Dummy2
0AB1: @GetInt 1 @CurrentCamera Dummy3
if and
Dummy1 == 1
{not SexMenuState == 6
not SexMenuState == 7}
not SexMenuState == 3
then 0AB1: @FirstPersonCameraMoveable_KeysControl 2 camera Dummy2 cam_id Dummy3
end
return

{======================================================================
======================================================================}
:ChangePoseAndPlace_Alt
SexStat_Speed = 40

0AB1: @LoadData 2 SexPoseInt SexPlace

00A0: store_actor $PLAYER_ACTOR position_to Dummy1 Dummy2 Dummy3
Dummy3 += -1.0
0AB1: @StoreData 3 @PlayerPosX Dummy1 0
0AB1: @StoreData 3 @PlayerPosY Dummy2 0
0AB1: @StoreData 3 @PlayerPosZ Dummy3 0
0172: Dummy4 = actor $PLAYER_ACTOR Z_angle
0AB1: @StoreData 3 @PlayerAngle Dummy4 0            

repeat // Give time to GenerateSexFiles to work
wait 0
until 0029:   32@ >= SEXFILES_MINIMUM_WAIT_TIME

//0AB1: @CreateActor 4 gender_flag 0 xyz_offsets 0.0 0.0 -1.0 ret_values actor_handle PornActress
//0AB1: @CreateActor 4 gender_flag 1 xyz_offsets 0.0 0.0 -1.0 ret_values actor_handle PornActor
0AB1: @GenerateActors_Alt 0 ret PornActress PornActor
0AB1: @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SpeedState
0619: enable_actor $PLAYER_ACTOR collision_detection 0

0AB1: @GetInt 1 @SxMn_Slot Dummy1
if not Dummy1 == 0 // A slot was selected
then 0AB1: @RetrieveInformationFromInternalStorage 6 slot Dummy1 PornActress PornActor SexPoseInt SexPlace SpeedState
end

0AB1: @RelocateFreeCamCenter 0
01B4: set_player $PLAYER_CHAR can_move 1

0AB1: @StoreInt 2 @CameraMode 1 // Fixed Camera
wait 100
0AB1: @ToggleSexualCamera_Alt 2 SexPoseInt SexPlace
0337: set_actor PornActress visibility 1
0337: set_actor PornActor visibility 1
0AB1: @PlayerDissapear 0
return

{======================================================================
======================================================================}
:RemovePosesAndActors
gosub @WaitUntilReleaseClickButton
009B: destroy_actor PornActress
009B: destroy_actor PornActor
0619: enable_actor $PLAYER_ACTOR collision_detection 1

0AB1: @GetData 1 @PlayerPosX Dummy1
0AB1: @GetData 1 @PlayerPosY Dummy2
0AB1: @GetData 1 @PlayerPosZ Dummy3
0AB1: @GetData 1 @PlayerAngle Dummy4
00A1: put_actor $PLAYER_ACTOR at Dummy1 Dummy2 Dummy3
0173: set_actor $PLAYER_ACTOR Z_angle_to Dummy4
0373: set_camera_directly_behind_player
02EB: restore_camera_with_jumpcut

01B4: set_player $PLAYER_CHAR can_move 1
SexStat_Speed = 0
return

