{======================================================================
======================================================================}
:AddOffsetTo3DCoord // 0@ 1@ 2@: coordinates, 3@: gender flag
if 3@ == 0 // female by default (0)
then
    0AB1: @GetData 2 @FemaleOffset 0 out 10@
    0AB1: @GetData 2 @FemaleOffset 4 out 11@
    0AB1: @GetData 2 @FemaleOffset 8 out 12@
else
    0AB1: @GetData 2 @MaleOffset 0 out 10@
    0AB1: @GetData 2 @MaleOffset 4 out 11@
    0AB1: @GetData 2 @MaleOffset 8 out 12@
end
005B: 0@ += 10@  // (float)
005B: 1@ += 11@  // (float)
005B: 2@ += 12@  // (float)
0AB2: ret 3 0@ 1@ 2@

{======================================================================
======================================================================}
:GenerateActors
// 0@: pose, 1@: place
0AB1: @GetActorsCoordinates 2 0@ 1@ ret fem_xyza 10@ 11@ 12@ 13@ male_xyza 20@ 21@ 22@ 23@

0AB1: @AddOffsetTo3DCoord 4 3dcoord 10@ 11@ 12@ gender 0 store_in 10@ 11@ 12@
0AB1: @AddOffsetTo3DCoord 4 3dcoord 20@ 21@ 22@ gender 1 store_in 20@ 21@ 22@

023C: load_special_actor 'bdoll' as 1
023C: load_special_actor 'tumi' as 2
038B: load_requested_models
repeat
wait 0
until 023D:   special_actor 1 loaded and 023D:   special_actor 2 loaded

// Actress
009A: 30@ = create_actor_pedtype 5 model #SPECIAL01 at 10@ 11@ 12@
0173: set_actor 30@ Z_angle_to 13@
0619: enable_actor 30@ collision_detection 0

// Actor
009A: 31@ = create_actor_pedtype 4 model #SPECIAL02 at 20@ 21@ 22@
0173: set_actor 31@ Z_angle_to 23@
0619: enable_actor 31@ collision_detection 0

0296: unload_special_actor 1
0296: unload_special_actor 2
0AB2: ret 2 actress 30@ actor 31@

{======================================================================
======================================================================}
:RelocateActor // 0@: Actor, 1@: Actor Gender Flag, 2@: Pose, 3@: Place
0AB1: @GetActorsCoordinates 2 2@ 3@ ret fem_xyza 10@ 11@ 12@ 13@ male_xyza 20@ 21@ 22@ 23@
if 1@ == 0 // female by default (0)
then
    0AB1: @AddOffsetTo3DCoord 4 3dcoord 10@ 11@ 12@ gender 0 store_in 10@ 11@ 12@
    00A1: put_actor 0@ at 10@ 11@ 12@
    0173: set_actor 0@ Z_angle_to 13@
else
    0AB1: @AddOffsetTo3DCoord 4 3dcoord 20@ 21@ 22@ gender 1 store_in 20@ 21@ 22@
    00A1: put_actor 0@ at 20@ 21@ 22@
    0173: set_actor 0@ Z_angle_to 23@
end
0AB2: ret 0

{======================================================================
======================================================================}
:LoadSexualPose
const
    An_Actress = 0@
    An_Actor = 1@
    An_SexPoseInt = 2@
    An_SexPlace = 3@
    An_SpeedInt = 4@
    An_Animation = 5@
    An_Str1 = 6@
    An_Str2 = 7@
    An_MaxAnim = 8@
    AnimIfpFem = 10@v
    AnimIfpMale = 14@v
    An_IfpFile = 18@v
end

0AB1: call_scm_func @ReadSexPosesInfo 3 An_SpeedInt An_SexPoseInt An_SexPlace out An_Str1 An_Str2 An_MaxAnim
0AB1: @GetInt 1 @CurrentAnimation An_Animation
if 001D:   An_Animation > An_MaxAnim  // (int)
then An_Animation = 1
end
if 001B:   1 > An_Animation
then 0085: An_Animation = An_MaxAnim // (int)
end
0AB1: @StoreInt 2 @CurrentAnimation An_Animation
0AB1: call_scm_func @GetAnimationNames 3 An_Str1 An_Str2 An_Animation out ifp_fem 10@ 11@ 12@ 13@ ifp_male 14@ 15@ 16@ 17@ ifp_file 18@ 19@ 20@ 21@

04ED: load_animation An_IfpFile
038B: load_requested_models
repeat
wait 0
until 04EE:   animation An_IfpFile loaded
0605: actor An_Actress perform_animation AnimIfpFem IFP An_IfpFile framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
0605: actor An_Actor perform_animation AnimIfpMale IFP An_IfpFile framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
04EF: release_animation An_IfpFile

0AB2: ret 0


{======================================================================
======================================================================}
:CurrentAnimation
hex
    1
end

:GetInt
// 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 1 virtual_protect 0
0AB2: ret 1 11@

:StoreInt
// 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 1 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:ReadSexPosesInfo // 0@: speed, 1@: pose, 2@: place
0A9A: 30@ = openfile ANIMATIONS_FILE mode "rb"  // IF and SET
for 4@ = 0 to 3
    0ADA: 31@ = scan_file 30@ format "%s %s" 10@v 14@v //IF and SET     // Dummy words
    for 5@ = 0 to 8
        0ADA: 31@ = scan_file 30@ format "%s" 10@v //IF and SET         // Dummy word
        for 6@ = 0 to 1
            0ADA: 31@ = scan_file 30@ format "%s %d %s %d %s %d %s %d" 10@s 20@ 12@s 21@ 14@s 22@ 16@s 23@ //IF and SET
            0B14: 24@ = 1@ MOD 2
            0085: 25@ = 1@ // (int)
            0016: 25@ /= 2
            if and            // pose % 2 === 6@  &&   pose / 2 === 4@
            003B:   24@ == 6@  // (int)
            003B:   25@ == 4@  // (int)
            then
                if 003B:   2@ == 5@  // (int)   // place === 5@
                then
                    for 7@ = 0 to 3
                        if 003B:   0@ == 7@  // (int)   // speed === 7@
                        then
                            05AA: 26@s = 10@(7@,4s) // @s = 'short'
                            0085: 28@ = 20@(7@,4i) // (int)
                            0A9B: closefile 30@
                            0AB2: ret 3 gxt_entry 26@ 27@ max_anim 28@
                        end
                    end
                end
            end
        end
    end
end
0A9B: closefile 30@
0AB2: ret 3 gxt_entry 26@ 27@ max_anim 28@

{======================================================================
======================================================================}
:GetAnimationNames
const
    Gan_Str1 = 0@
    Gan_Str2 = 1@
    Gan_StrSpeed = 0@s
    Gan_Animation = 2@
end

if 05AE:   Gan_StrSpeed == "idle" // @s == 'short'
then
    0AD3: 10@v = format "idle%d_f" Gan_Animation
    0AD3: 14@v = format "idle%d_m" Gan_Animation
    0AD3: 18@v = format "idle%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "slow" // @s == 'short'
then
    0AD3: 10@v = format "slow%d_f" Gan_Animation
    0AD3: 14@v = format "slow%d_m" Gan_Animation
    0AD3: 18@v = format "slow%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "normal" // @s == 'short'
then
    0AD3: 10@v = format "normal%d_f" Gan_Animation
    0AD3: 14@v = format "normal%d_m" Gan_Animation
    0AD3: 18@v = format "normal%d" Gan_Animation
end
if 05AE:   Gan_StrSpeed == "fast" // @s == 'short'
then
    0AD3: 10@v = format "fast%d_f" Gan_Animation
    0AD3: 14@v = format "fast%d_m" Gan_Animation
    0AD3: 18@v = format "fast%d" Gan_Animation
end
0AB2: ret 12 ifp_fem 10@ 11@ 12@ 13@ ifp_male 14@ 15@ 16@ 17@ ifp_file 18@ 19@ 20@ 21@
