{$CLEO .cs}
thread 'SexMachine'

{======================================================================
Sexual positions, actors and cameras
====================================
Fucking in table  sex_fem1_xxxx / sex_male1_xxxx
----------------
Female: xyz 2372.198 -1132.888 1049.775 angle -90.0
Male: xyz 2372.195 -1132.905 1049.825 angle -90.0

Anal in table  anal_fem1_xxxx / anal_male1_xxxx
-------------
Female: xyz 2372.688 -1132.955 1049.905 angle -90.0
Male: xyz 2372.681 -1133.057 1049.975 angle -90.0

Handjob in sofa  hj_fem1_xxxx / hj_male1_xxxx
---------------
Female: xyz 2372.3 -1121.857 1049.415 angle 0.0
Male: xyz 2372.317 -1121.818 1049.475 angle 0.0

Cunnilingus in sofa   cunnilingus_f1 / cunnilingus_m1
-------------------
Female: xyz 2374.48 -1123.599 1049.475 angle -90.0
Male: xyz 2374.523 -1123.617 1049.535 angle -90.0

Blowjob sit down on bed   bj_femX / bj_maleX
-----------------------
Female: xyz 2359.824 -1132.73 1049.635 angle 90.0
Male: xyz 2359.758 -1132.749 1049.665 angle 90.0

======================================================================}


{======================================================================
======================================================================}
const
    GeneralState = 0@
    SexMenuState = 1@
    ShowSexualStats = 2@
    OnSexActivity = 3@
    PornActress = 4@
    PornActor = 5@
    PlyX = 6@
    PlyY = 7@
    PlyZ = 8@
    PlayerInterior = 9@
    SexCameraState = 10@
    SexState = 11@
    SexStat_Climax = 12@   // (Float) Min: 1.0 - Max 100.0
    SexStat_Speed = 13@    // (Int) Min: 1 - Max 100
    SexStat_Pleasure = 14@ // (Int) From 1 to unlimited
    ClimaxState = 15@
    PleasureState = 16@
    SpeedState = 17@
    Dummy1 = 18@
    Dummy2 = 19@
    Dummy3 = 20@
    Dummy4 = 21@
    DummyString = 18@s
    Clmx_LstEvntTime = 22@
    Clmx_CurrEvntTime = 23@
    Plsr_LstEvntTime = 24@
    Plsr_CurrEvntTime = 25@
    Plsr_Gain = 26@
    SexPoseInt = 27@
    PulseButtonState = 28@
    
    {===================================
    ====================================}
    FLAG_INCREASE = 1
    FLAG_DECREASE = 0
    
    FLAG_NORMAL_INT = 0
    FLAG_NORMAL_FLOAT = 1
    FLAG_PERCENTAGE_INT = 2
    FLAG_PERCENTAGE_FLOAT = 3
    
    FLAG_SX_MENU_LEFT = 0
    FLAG_SX_MENU_RIGHT = 1
    
    SEX_SLOW = 0
    SEX_FAST = 1
    SEX_IDLE = 2
    ANAL_SLOW = 3
    ANAL_FAST = 4
    ANAL_IDLE = 5
    HJ_SLOW = 6
    HJ_FAST = 7
    HJ_IDLE = 8
    CUNNILINGUS_SLOW = 9
    CUNNILINGUS_FAST = 10
    CUNNILINGUS_IDLE = 11
    
    SEX_FUCKING = 0
    SEX_BJ = 1
    SEX_ANAL = 2
    SEX_HJ = 2
    SEX_CUNNI = 3
    
    SPD_THRESHOLD1 = 13 // Threshold to stop
    SPD_THRESHOLD2 = 52 // ?
    SPD_THRESHOLD3 = 88 // Threshold to pass from slow to fast
    
    // Sex Menu States
    SX_MENU_LEFT_FIRST_ELEM = 1  // 1, 2, 3 for left menu options
    SX_MENU_OPT1_PLACES_FIRST_ELEM = 20 // 20, 21, ... , (20 + offset)
    SX_MENU_OPT2_POSITIONS_FIRST_ELEM = 40
    SX_MENU_OPT3_CAMERAS_FIRST_ELEM = 60

    // Pulsed button states
    NOT_PULSED = 0
    PULSED_UP = 10
    PULSED_DOWN = 20
    PULSED_ENTER_VEH = 30
    PULSED_SPRINT = 40
    PULSED_UP_F = 19
    PULSED_DOWN_F = 29
    PULSED_ENTER_VEH_F = 39
    PULSED_SPRINT_F = 49

    {===================================
    ====================================}
    PAD_CHANGE_CAMERA_VIEW = 13
    PAD_ANSWER_PHONE = 4
    PAD_PREV_WEP_ZOOM_IN = 5
    PAD_NEXT_WEP_ZOOM_OUT = 7
    PAD_ANSWER_PHONE = 4
    PAD_ENTER_VEHICLE = 15
    PAD_SPRINT = 16
    
    KEY_F6 = 117
    KEY_F3 = 114
    
    {===================================
    ====================================}
    KEY_TOGGLE_SEX_MACHINE = KEY_F6
end

{======================================================================
======================================================================}
:Main
while true
wait 0
    if GeneralState == 0
    then
        if 0AB0:   key_pressed KEY_TOGGLE_SEX_MACHINE
        then GeneralState=1
            gosub @ToggleSexMachine
        end
    end
    if GeneralState == 1
    then
        if 8AB0:   not key_pressed KEY_TOGGLE_SEX_MACHINE
        then GeneralState=0
        end
    end
    if OnSexActivity==true
    then gosub @SexMachine
    end
    if SexMenuState > 0
    then
        gosub @SexualMenuMachine
        gosub @ShowSexualMenu
    end
    if ShowSexualStats==true
    then 0AB1: call_scm_func @ShowSexualStats 3 SexStat_Climax SexStat_Speed SexStat_Pleasure
    end
end

0A93: end_custom_thread

{======================================================================
======================================================================}
:GenerateActors
const
    GenAct_f_X = 0@
    GenAct_f_Y = 1@
    GenAct_f_Z = 2@
    GenAct_f_A = 3@
    GenAct_m_X = 4@
    GenAct_m_Y = 5@
    GenAct_m_Z = 6@
    GenAct_m_A = 7@
    GenAct_Actor = 8@
    GenAct_Actress = 9@
end
023C: load_special_actor 'bdoll' as 1
023C: load_special_actor 'tumi' as 2
038B: load_requested_models
repeat
wait 0
until 023D:   special_actor 1 loaded and 023D:   special_actor 2 loaded

// Actress
009A: GenAct_Actress = create_actor_pedtype 5 model #SPECIAL01 at GenAct_f_X GenAct_f_Y GenAct_f_Z
0173: set_actor GenAct_Actress Z_angle_to GenAct_f_A
0619: enable_actor GenAct_Actress collision_detection 0

// Actor
009A: GenAct_Actor = create_actor_pedtype 4 model #SPECIAL02 at GenAct_m_X GenAct_m_Y GenAct_m_Z
0173: set_actor GenAct_Actor Z_angle_to GenAct_m_A
0619: enable_actor GenAct_Actor collision_detection 0

0296: unload_special_actor 1
0296: unload_special_actor 2
0AB2: ret 2 GenAct_Actress GenAct_Actor

{======================================================================
======================================================================}
:LoadSexualPose
const
    An_Actress = 0@
    An_Actor = 1@
    An_SexPoseInt = 2@
    AnimIfpFem = 10@v
    AnimIfpMale = 20@v
    
    GENERIC_ANIM_NAME = "sex_pose"
end

If An_SexPoseInt == SEX_SLOW
then AnimIfpFem = "sex_fem1_slow"
    AnimIfpMale = "sex_male1_slow"
end
If An_SexPoseInt == SEX_FAST
then AnimIfpFem = "sex_fem1_fast"
    AnimIfpMale = "sex_male1_fast"
end
If An_SexPoseInt == SEX_IDLE
then AnimIfpFem = "sex_fem1_idle"
    AnimIfpMale = "sex_male1_idle"
end
If An_SexPoseInt == ANAL_SLOW
then AnimIfpFem = "anal_fem1_slow"
    AnimIfpMale = "anal_male1_slow"
end
If An_SexPoseInt == ANAL_FAST
then AnimIfpFem = "anal_fem1_fast"
    AnimIfpMale = "anal_male1_fast"
end
If An_SexPoseInt == HJ_SLOW
then AnimIfpFem = "hj_fem1_slow"
    AnimIfpMale = "hj_male1_slow"
end
If An_SexPoseInt == HJ_FAST
then AnimIfpFem = "hj_fem1_fast"
    AnimIfpMale = "hj_male1_fast"
end
If An_SexPoseInt == CUNNILINGUS_SLOW
then AnimIfpFem = "cunnilingus_f1"
    AnimIfpMale = "cunnilingus_m1"
end

04ED: load_animation AnimIfpFem
04ED: load_animation AnimIfpMale
038B: load_requested_models
repeat
wait 0
until 04EE:   animation AnimIfpFem loaded and 04EE:   animation AnimIfpMale loaded

0605: actor An_Actress perform_animation GENERIC_ANIM_NAME IFP AnimIfpFem framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
0605: actor An_Actor perform_animation GENERIC_ANIM_NAME IFP AnimIfpMale framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1

04EF: release_animation AnimIfpFem
04EF: release_animation AnimIfpMale
0AB2: ret 0

{======================================================================
======================================================================}
:ShowSexualMenu
// Menu left
038E: draw_box_position 75.0 317.0 size 150.0 263.0 RGBA 0 0 0 100
038E: draw_box_position 78.0 314.0 size 156.0 269.0 RGBA 0 0 0 100
038E: draw_box_position 83.0 223.0 size 167.0 49.0 RGBA 238 71 181 50
038E: draw_box_position 80.0 223.0 size 161.0 62.0 RGBA 238 71 181 50
038E: draw_box_position 83.0 229.0 size 167.0 20.0 RGBA 238 71 181 50
038E: draw_box_position 98.0 351.0 size 91.0 194.0 RGBA 0 0 0 110
038E: draw_box_position 98.0 189.0 size 91.0 6.0 RGBA 0 0 0 110

// Menu right
038E: draw_box_position 560.0 224.0 size 160.0 448.0 RGBA 0 0 0 60
038E: draw_box_position 556.0 224.0 size 168.0 448.0 RGBA 0 0 0 60
038E: draw_box_position 532.0 224.0 size 83.0 448.0 RGBA 0 0 0 80
038E: draw_box_position 556.0 430.0 size 168.0 10.0 RGBA 0 0 0 50
038E: draw_box_position 553.0 390.0 size 174.0 70.0 RGBA 238 71 181 50
038E: draw_box_position 548.0 390.0 size 183.0 60.0 RGBA 238 71 181 60
038E: draw_box_position 550.0 400.0 size 180.0 26.0 RGBA 238 71 181 80
038E: draw_box_position 531.0 139.0 size 136.0 235.0 RGBA 0 0 0 50

0AB1: @SexualMenu_GetContent 1 FLAG_SX_MENU_LEFT

if SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
then    
    // Highlighting right title
    038E: draw_box_position 548.0 390.0 size 173.0 70.0 RGBA 255 255 255 40 // White
    038E: draw_box_position 548.0 410.0 size 173.0 30.0 RGBA 255 255 255 40 // White

    0085: Dummy1 = SexMenuState // (int)
    
    // Is current selector on opt 1 right menu items ?
    Dummy2 = SX_MENU_OPT1_PLACES_FIRST_ELEM
    Dummy2 += 7
    if and
    001D:   Dummy2 > SexMenuState  // (int)
    0029:   SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
    then
        Dummy1 -= SX_MENU_OPT1_PLACES_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 2 FLAG_SX_MENU_RIGHT SX_MENU_OPT1_PLACES_FIRST_ELEM
    end
        
    // Is current selector on opt 2 right menu items ?
    Dummy2 = SX_MENU_OPT2_POSITIONS_FIRST_ELEM
    Dummy2 += 10
    if and
    001D:   Dummy2 > SexMenuState  // (int)
    0029:   SexMenuState >= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
    then
        Dummy1 -= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 2 FLAG_SX_MENU_RIGHT SX_MENU_OPT2_POSITIONS_FIRST_ELEM
    end
        
    // Is current selector on opt 3 right menu items ?
    Dummy2 = SX_MENU_OPT3_CAMERAS_FIRST_ELEM
    Dummy2 += 10
    if and
    001D:   Dummy2 > SexMenuState  // (int)
    0029:   SexMenuState >= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
    then
        Dummy1 -= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 2 FLAG_SX_MENU_RIGHT SX_MENU_OPT3_CAMERAS_FIRST_ELEM
    end
    // Highlighting right selected item
    0093: Dummy1 = integer Dummy1 to_float
    Dummy1 *= 20.0
    Dummy1 += 46.0
    038E: draw_box_position 556.0 Dummy1 size 168.0 24.0 RGBA 0 0 160 120 // Dark blue
else
    // Highlighting left title
    038E: draw_box_position 83.0 223.0 size 166.0 62.0 RGBA 255 255 255 40 // White
    038E: draw_box_position 83.0 240.0 size 166.0 28.0 RGBA 255 255 255 50 // White
    
    // Highlighting left selected item
    0085: Dummy1 = SexMenuState // (int)
    Dummy1 -= 1
    0093: Dummy1 = integer Dummy1 to_float
    Dummy1 *= 20.0
    Dummy1 += 276.0
    038E: draw_box_position 78.0 Dummy1 size 156.0 24.0 RGBA 0 0 160 80 // Dark blue
end
return

{======================================================================
======================================================================}
:SetDrawText
const
    DwTx_Font = 0@
    DwTx_Linewidth = 1@
    DwTx_SizeX = 2@
    DwTx_SizeY = 3@
    DwTx_PosX = 4@
    DwTx_PosY = 5@
    DwTx_gxt1 = 6@
    DwTx_gxt2 = 7@
    DwTx_Gxt = 6@s
end
0349: set_text_draw_font DwTx_Font
0343: set_text_draw_linewidth DwTx_Linewidth
033F: set_text_draw_letter_size DwTx_SizeX DwTx_SizeY
033E: set_draw_text_position DwTx_PosX DwTx_PosY GXT DwTx_Gxt
0AB2: ret 0

{======================================================================
======================================================================}
:SexualMenu_GetContent
const
    SmGc_LftOrRghtFlag = 0@
    SmGc_RightMenuFlag = 1@
    SmGc_AuxStr = 26@s
    SmGc_Aux1 = 26@
    SmGc_Aux2 = 27@
    SmGc_MaxOpts = 28@
    SmGc_Index = 29@
    SmGc_PosY = 30@
    {4@, 5@}
    {6@s - 8@s - .... - 24@s: Array of 10 strings}
end

// Read Content
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then 0AB1: @GetLeftMenuElements 0 SmGc_MaxOpts 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@
else 0AB1: @GetRightMenuElements 1 SmGc_RightMenuFlag SmGc_MaxOpts 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@
end

// Title
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then 0AB1: @SetDrawText 8 0 600.0 size 0.6 2.6 pos 6.0 205.0 GXT 4@ 5@
else 0AB1: @SetDrawText 8 0 640.0 size 0.8 2.7 pos 448.0 378.0 GXT 4@ 5@
end

// Options
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then SmGc_PosY = 270.0
else SmGc_PosY = 40.0
end
for SmGc_Index = 0 to 28@ // 28@ : SmGc_MaxOpts 
    05AA: SmGc_AuxStr = 6@(SmGc_Index,12s) // @s = 'short'
    if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
    then 0AB1: @SetDrawText 8 2 600.0 size 0.2 1.2 pos 10.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2
    else 0AB1: @SetDrawText 8 2 600.0 size 0.2 1.2 pos 492.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2
    end
    SmGc_PosY += 20.0
end
0AB2: ret 0

{======================================================================
======================================================================}
:GetLeftMenuElements
4@s = 'S_MENU' // Sex Options
6@s = 'S_OPT1' // Cambiar lugar
8@s = 'S_OPT2' // Cambiar posicion
10@s = 'S_OPT3' // Cambiar camara
1@ = 2 // 3 gxt sin incluir título - disminuir en 1 para el ciclo "for"
0AB2: ret 23 1@ 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@

{======================================================================
======================================================================}
:GetRightMenuElements
if 0@ == SX_MENU_OPT1_PLACES_FIRST_ELEM
then
    4@s = 'S_MENU1' // Lugares
    6@s = 'S_PL_1' // Cama
    8@s = 'S_PL_2' // Sofa
    10@s = 'S_PL_3' // Mesa
    12@s = 'S_PL_4' // Silla
    14@s = 'S_PL_5' // Piso
    16@s = 'S_PL_6' // Baño
    18@s = 'S_PL_7' // Cocina
    1@ = 6 // 7 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end
if 0@ == SX_MENU_OPT2_POSITIONS_FIRST_ELEM
then
    4@s = 'S_MENU2' // Posiciones
    6@s = 'S_CN_1' // Cunnilingus
    8@s = 'S_69_1' // Oral in 69
    10@s = 'S_BJ_1' // Blowjob
    12@s = 'S_HJ_1' // Handjob
    14@s = 'S_FK_1' // Fucking
    16@s = 'S_AN_1' // Anal
    18@s = 'S_TJ_1' // Titjob
    20@s = 'S_FJ_1' // Footjob
    22@s = 'S_KS_1' // Kissing
    24@s = 'S_TC_1' // Touching
    1@ = 9 // 10 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end
{if 0@ == SX_MENU_OPT3_CAMERAS_FIRST_ELEM
then
    4@s = 'S_MENU3' // Camaras
    6@s = '' //
    8@s = '' //
    10@s = '' //
    12@s = '' //
    14@s = '' //
    16@s = '' //
    18@s = '' //
    20@s = '' //
    22@s = '' //
    24@s = '' //
    1@ = 9 // 10 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end}
0AB2: ret 23 1@ 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@

{======================================================================
    Orgasm-o-meter bar                        Speed-o-meter bar
    min -> posX 170.5 sizeX 1.0               min -> posX 243.5 sizeX 1.0
    min+1 -> posX 171.0 sizeX 2.0             min+1 -> posX 244.0 sizeX 2.0
    min+2 -> posX 171.5 sizeX 3.0             min+2 -> posX 244.5 sizeX 3.0
    min+3 -> posX 172.0 sizeX 4.0             min+3 -> posX 245.0 sizeX 4.0
    100% -> posX 260.0 sizeX 180.0            100% -> posX 296.0 sizeX 106.0
======================================================================}
:ShowSexualStats
const
    Flt_Climax = 0@
    ShSt_Speed = 1@
    ShSt_Pleasure = 2@
    Flt_Speed = 4@
    Climax_PosBar = 5@
    Climax_SizeBar = 6@
    Speed_PosBar = 7@
    Speed_SizeBar = 8@
end
0093: Flt_Speed = integer ShSt_Speed to_float

//Climax_SizeBar = 1.8 * Flt_Climax
//Climax_PosBar = Climax_SizeBar/2 + 170
0087: Climax_SizeBar = Flt_Climax // (float)
0013: Climax_SizeBar *= 1.8
0087: Climax_PosBar = Climax_SizeBar // (float)
0013: Climax_PosBar *= 0.5
000B: Climax_PosBar += 170.0

//Speed_SizeBar = 1.06 * Flt_Speed
//Speed_PosBar = Speed_SizeBar/2 + 243
0087: Speed_SizeBar = Flt_Speed // (float)
0013: Speed_SizeBar *= 1.06
0087: Speed_PosBar = Speed_SizeBar // (float)
0013: Speed_PosBar *= 0.5
000B: Speed_PosBar += 243.0

//Black styled climax bar
{038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 261.0 419.0 size 186.0 13.0 RGBA 0 0 0 80
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 0 0 0 250}

//Pink styled climax bar
038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 238 71 181 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 419.0 size 186.0 13.0 RGBA 0 0 0 150
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 238 71 181 60 // the orgasm-o-meter bar
038E: draw_box_position Climax_PosBar 411.0 size Climax_SizeBar 3.0 RGBA 255 255 255 50 // the orgasm-o-meter bar 2

// Speedometer
038E: draw_box_position 257.0 436.0 size 1.0 15.0 RGBA 153 153 153 180 // Medium Gray
038E: draw_box_position 298.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 337.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 296.0 436.0 size 114.0 12.0 RGBA 238 71 181 80 // Pink
038E: draw_box_position 296.0 436.0 size 110.0 8.0 RGBA 0 0 0 140 // Black
038E: draw_box_position Speed_PosBar 436.0 size Speed_SizeBar 4.0 RGBA 255 0 0 250 // Red // the speed-o-meter bar

// Shadow on text "pleasure"
038E: draw_box_position 203.0 436.0 size 72.0 12.0 RGBA 0 0 0 120
    
0349: set_text_draw_font 2
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
033E: set_draw_text_position 179.0 409.0 GXT 'S_HUD1' // Climax

0349: set_text_draw_font 1
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
045A: draw_text_1number 171.0 430.0 GXT 'S_HUD2' number ShSt_Pleasure  // Pleasure: ~1~%
0AB2: ret 0

{======================================================================
======================================================================}
:ToggleSexualCamera
if 0@ == 0
then 015F: set_camera_position 2373.360839 -1134.396972 1051.675415 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2372.830078 -1133.676879 1051.228271 switchstyle 2
end
if 0@ == 1
then 015F: set_camera_position 2370.840087 -1132.937622 1051.675415 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.658447 -1132.903564 1051.101684 switchstyle 2
end
if 0@ == 2
then 015F: set_camera_position 2371.700439 -1134.403198 1051.175537 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.944091 -1133.470336 1050.910156 switchstyle 2
end
if 0@ == 3
then 015F: set_camera_position 2372.286865 -1133.062622 1052.975097 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2372.226806 -1132.963989 1051.981811 switchstyle 2
end
if 0@ == 4
then 015F: set_camera_position 2372.265136 -1133.077148 1052.075317 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2372.155029 -1132.927856 1051.092651 switchstyle 2
end
if 0@ == 5
then 015F: set_camera_position 2371.559814 -1132.563598 1052.075317 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.843505 -1132.798461 1051.145629 switchstyle 2
end
if 0@ == 6
then 015F: set_camera_position 2372.394042 -1132.386840 1051.575439 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2372.034912 -1132.926391 1050.813964 switchstyle 2
end
if 0@ == 7
then 015F: set_camera_position 2371.326904 -1130.574951 1052.475219 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.521240 -1131.331176 1051.850463 switchstyle 2
end
if 0@ == 8
then 015F: set_camera_position 2370.883544 -1132.128417 1050.875610 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.674316 -1132.715576 1051.048095 switchstyle 2
end
if 0@ == 9
then 015F: set_camera_position 2372.027587 -1132.840576 1051.573439 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.727294 -1132.847900 1050.621382 switchstyle 2
end
if 0@ == 10
then 015F: set_camera_position 2371.823242 -1132.939453 1052.175292 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.935791 -1132.936645 1051.181640 switchstyle 2
end
if 0@ == 11
then 015F: set_camera_position 2371.022216 -1133.324340 1050.775634 rotation 0.0 0.0 0.0
    0160: set_camera_point_at 2371.881347 -1132.916259 1051.084472 switchstyle 2
end
0AB2: ret 0

{======================================================================
======================================================================}
:IncOrDecIntVariable
const
    IncOrDec_Variable = 0@
    IncOrDec_MinValue = 1@
    IncOrDec_MaxValue = 2@
    IncOrDec_Flag = 3@
end
if 0039:   IncOrDec_Flag == FLAG_INCREASE
then
    if 002D:   IncOrDec_Variable >= IncOrDec_MaxValue  // (int)
    then 0085: IncOrDec_Variable = IncOrDec_MinValue // (int)
    else IncOrDec_Variable++
    end
else // FLAG_DECREASE
    if 002D:   IncOrDec_MinValue >= IncOrDec_Variable  // (int)
    then 0085: IncOrDec_Variable = IncOrDec_MaxValue // (int)
    else IncOrDec_Variable--
    end
end
0AB2: ret 1 IncOrDec_Variable

{======================================================================
======================================================================}
:ToggleSexMachine
if OnSexActivity == false
then
    fade 0 time 800
    wait 800 ms
    01B4: set_player $PLAYER_CHAR can_move 0
    0581: enable_radar 0
    0826: enable_hud 0
    03F0: enable_text_draw 1
    00A0: store_actor $PLAYER_ACTOR position_to PlyX PlyY PlyZ
    077E: get_active_interior_to PlayerInterior
    04BB: select_interior 8
    0860: link_actor $PLAYER_ACTOR to_interior 8
    00A1: put_actor $PLAYER_ACTOR at 2372.198 -1132.888 1039.775
    04E4: unknown_refresh_game_renderer_at 2372.198 -1132.888
    03CB: set_rendering_origin_at 2372.198 -1132.888 1049.775
    //04F9: set_interior 8 color_to 0
    0AB1: call_scm_func @GenerateActors 8 Xf 2372.198 Yf -1132.888 Zf 1049.775 angle -90.0 Xm 2372.195 Ym -1132.905 Zm 1049.825 angle -90.0 PornActress PornActor
    0AB1: call_scm_func @LoadSexualPose 3 PornActress PornActor SEX_SLOW
    0AB1: call_scm_func @ToggleSexualCamera 1 SexCameraState
    0AB1: call_scm_func @IncOrDecIntVariable 4 SexCameraState 0 11 FLAG_INCREASE SexCameraState
    OnSexActivity = true
    SexState=0
    ShowSexualStats=true
    SexStat_Climax = 0.0
    SexStat_Speed = 40
    SexStat_Pleasure = 1
    ClimaxState = 0
    PleasureState = 0
    SexPoseInt = SEX_FUCKING
    //0209: SexCameraState = random_int_in_ranges 0 12
    SexCameraState=0
    wait 500
    fade 1 time 800
    {wait 500
    03E5: show_text_box 'I_CTRL1'  // Puedes cambiar la vista con ~b~~k~~CAMERA_CHANGE_VIEW_ALL_SITUATIONS~~w~. Ingresa al menu pulsando ~b~~k~~PED_ANSWER_PHONE~~w~ para ver las diferentes opciones.
    wait 7000
    03E5: show_text_box 'I_GNRL1'}  // Puedes aumentar el ritmo de los movimientos con ~b~~k~~PED_CYCLE_WEAPON_LEFT~~w~ / ~b~~k~~PED_CYCLE_WEAPON_RIGHT~~w~. Tambien puedes parar totalmente disminuyendo el ritmo a su minimo valor.
else
    fade 0 time 800
    wait 800 ms
    0581: enable_radar 1
    0826: enable_hud 1
    03F0: enable_text_draw 0
    04BB: select_interior PlayerInterior
    0860: link_actor $PLAYER_ACTOR to_interior PlayerInterior
    00A1: put_actor $PLAYER_ACTOR at PlyX PlyY PlyZ
    0373: set_camera_directly_behind_player
    02EB: restore_camera_with_jumpcut
    009B: destroy_actor PornActress
    009B: destroy_actor PornActor
    01B4: set_player $PLAYER_CHAR can_move 1
    OnSexActivity=false
    SexMenuState=0
    ShowSexualStats=false
    wait 500
    fade 1 time 800
end
return

{======================================================================
======================================================================}
:SexMachine
if SexState == 0
then
    if 00E1:   player 0 pressed_key PAD_CHANGE_CAMERA_VIEW // V
    then SexState=1
        0AB1: call_scm_func @ToggleSexualCamera 1 SexCameraState
        0AB1: call_scm_func @IncOrDecIntVariable 4 SexCameraState 0 11 FLAG_INCREASE SexCameraState
    end
    if 00E1:   player 0 pressed_key PAD_ANSWER_PHONE // TAB
    then SexState=2
        gosub @ToggleSexualMenu
    end
end
if SexState == 1
then
    if 80E1:   not player 0 pressed_key PAD_CHANGE_CAMERA_VIEW
    then SexState=0
    end
end
if SexState == 2
then
    if 80E1:   not player 0 pressed_key PAD_ANSWER_PHONE
    then SexState=0
    end
end

// Increase / Decrease Speed
if 00E1:   player 0 pressed_key PAD_PREV_WEP_ZOOM_IN
then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_INCREASE SexStat_Speed
end
if 00E1:   player 0 pressed_key PAD_NEXT_WEP_ZOOM_OUT
then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 SexStat_Speed 4 FLAG_DECREASE SexStat_Speed
end

// Increase Pleasure
gosub @PleasureMachine

// Increase Climax
gosub @ClimaxMachine

// Change from stopped to slow or fast
gosub @SpeedMachine

// Buttons UP - DOWN - ACCEPT - CANCEL acknowledge
gosub @PulseButtonMachine
return

{======================================================================
======================================================================}
:PleasureMachine
if PleasureState == 0
then 0209: Plsr_Gain = random_int_in_ranges 1 4
end
if not SexStat_Speed > 13
then return
end
0AB1: call_scm_func @IncreaseTimedVar 7 SexStat_Pleasure Plsr_Gain PleasureState 800 FLAG_NORMAL_INT Plsr_LstEvntTime Plsr_CurrEvntTime SexStat_Pleasure PleasureState Plsr_LstEvntTime Plsr_CurrEvntTime
if PleasureState == 2
then PleasureState=0
end
return

{======================================================================
======================================================================}
:ClimaxMachine
// Speed: min: 13 - max: 100
// steps: 0.3 - min: 1 - max: 250
// steps: 0.4 - min: 1 - max: 500
// Ecuacion de la recta: Time (ms) = -5,7356321839080459770114942528736 * Speed + 574,56321839080459770114942528736
if SexStat_Speed > 13
then
    0093: Dummy1 = integer SexStat_Speed to_float
    Dummy1 *= -5.735
    Dummy1 += 574.563
    0092: Dummy1 = float Dummy1 to_integer
    Dummy2 = 0.4
else
    Dummy1 = 100
    Dummy2 = -0.4
end
0AB1: call_scm_func @IncreaseTimedVar 7 SexStat_Climax Dummy2 ClimaxState Dummy1 FLAG_PERCENTAGE_FLOAT Clmx_LstEvntTime Clmx_CurrEvntTime SexStat_Climax ClimaxState Clmx_LstEvntTime Clmx_CurrEvntTime
if ClimaxState == 2
then ClimaxState=0
end
return

{======================================================================
======================================================================}
:ToggleSexualMenu
if SexMenuState == 0
then SexMenuState = 1 // Show and start from initial state
else SexMenuState = 0 // Hide sexual menu
end
return

{======================================================================
======================================================================}
:IncOrDecPercentageIntStat
const
    PtSt_Stat = 0@
    PtSt_Amount = 1@
    PtSt_IncOrDec = 2@
end
if PtSt_IncOrDec == FLAG_INCREASE
then
    005A: PtSt_Stat += PtSt_Amount  // (int)
    if PtSt_Stat > 100
    then 0AB2: ret 1 100
    end
else // FLAG_DECREASE
    0062: PtSt_Stat -= PtSt_Amount  // (int)
    if 1 > PtSt_Stat
    then 0AB2: ret 1 1
    end
end
0AB2: ret 1 PtSt_Stat

{======================================================================
======================================================================}
:IncOrDecPercentageFloatStat
const
    FtSt_Stat = 0@
    FtSt_Amount = 1@
    FtSt_IncOrDec = 2@
end
if FtSt_IncOrDec == FLAG_INCREASE
then 005B: FtSt_Stat += FtSt_Amount  // (float)
else 0062: FtSt_Stat -= FtSt_Amount  // (int)
end
if FtSt_Stat > 100.0
then 0AB2: ret 1 100.0
end
if 1.0 > FtSt_Stat
then 0AB2: ret 1 1.0
end
0AB2: ret 1 FtSt_Stat

{======================================================================
======================================================================}
:IncreaseTimedVar
const
    IcTm_Var = 0@
    IcTm_Amount = 1@
    IcTm_Stat = 2@
    IcTm_msec = 3@
    IcTm_PercentageFlag = 4@
    IcTm_LastTime = 5@
    IcTm_CurrentTime = 6@
end
if IcTm_Stat == 0
then
    0085: IcTm_LastTime = 32@ // (int)
    005A: IcTm_LastTime += IcTm_msec  // (int)
    IcTm_Stat=1
end
if IcTm_Stat == 1
then
    0085: IcTm_CurrentTime = 32@ // (int)
    if 002D:   IcTm_CurrentTime >= IcTm_LastTime  // (int)
    then
        if IcTm_PercentageFlag == FLAG_NORMAL_INT
        then 005A: IcTm_Var += IcTm_Amount  // (int)
        end
        if IcTm_PercentageFlag == FLAG_NORMAL_FLOAT
        then 005B: IcTm_Var += IcTm_Amount  // (float)
        end
        if IcTm_PercentageFlag == FLAG_PERCENTAGE_INT
        then 0AB1: call_scm_func @IncOrDecPercentageIntStat 3 IcTm_Var IcTm_Amount FLAG_INCREASE IcTm_Var
        end
        if IcTm_PercentageFlag == FLAG_PERCENTAGE_FLOAT
        then 0AB1: call_scm_func @IncOrDecPercentageFloatStat 3 IcTm_Var IcTm_Amount FLAG_INCREASE IcTm_Var
        end
        IcTm_Stat=2
    end
end
0AB2: ret 4 IcTm_Var IcTm_Stat IcTm_LastTime IcTm_CurrentTime

{======================================================================
======================================================================}
:SpeedMachine
if SpeedState == 0
then
    if SexStat_Speed > SPD_THRESHOLD1
    then SpeedState=1
        0AB1: call_scm_func @ChangeAnimation 4 SexPoseInt PornActress PornActor SpeedState
    end
end
if SpeedState == 1
then
    if SexStat_Speed > SPD_THRESHOLD2
    then SpeedState=2
    end
    if SexStat_Speed < SPD_THRESHOLD1
    then SpeedState=0
        0AB1: call_scm_func @ChangeAnimation 4 SexPoseInt PornActress PornActor SpeedState
    end
end
if SpeedState == 2
then
    if SexStat_Speed > SPD_THRESHOLD3
    then SpeedState=3
        0AB1: call_scm_func @ChangeAnimation 4 SexPoseInt PornActress PornActor SpeedState
    end
    if SexStat_Speed < SPD_THRESHOLD2
    then SpeedState=1
        0AB1: call_scm_func @ChangeAnimation 4 SexPoseInt PornActress PornActor SpeedState
    end
end
if SpeedState == 3
then
    if SexStat_Speed < SPD_THRESHOLD3
    then SpeedState=2
    end
end
return

{======================================================================
======================================================================}
:ChangeAnimation
const
    ChgAn_Pose = 0@
    ChgAn_Actress = 1@
    ChgAn_Actor = 2@
    ChgAn_SpeedStatus = 3@
    ChgAn_SpeedInt = 4@
    
    SPD_IDLE = 0
    SPD_SLOW = 1
    SPD_FAST = 2
end

// Determining speed of pose
if ChgAn_SpeedStatus == 0
then ChgAn_SpeedInt = SPD_IDLE
end
if ChgAn_SpeedStatus == 1
then ChgAn_SpeedInt = SPD_SLOW
end
if ChgAn_SpeedStatus == 2
then 0AB2: ret 0 // I don't want to change the animation in this case
end
if ChgAn_SpeedStatus == 3
then ChgAn_SpeedInt = SPD_FAST
end

// Determining pose
if and
ChgAn_Pose == SEX_FUCKING
ChgAn_SpeedInt == SPD_SLOW
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor SEX_SLOW
end
if and
ChgAn_Pose == SEX_FUCKING
ChgAn_SpeedInt == SPD_FAST
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor SEX_FAST
end
if and
ChgAn_Pose == SEX_FUCKING
ChgAn_SpeedInt == SPD_IDLE
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor SEX_IDLE
end
{if and
ChgAn_Pose == SEX_BJ
then
end}
if and
ChgAn_Pose == SEX_ANAL
ChgAn_SpeedInt == SPD_SLOW
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor ANAL_SLOW
end
if and
ChgAn_Pose == SEX_ANAL
ChgAn_SpeedInt == SPD_FAST
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor ANAL_FAST
end
if and
ChgAn_Pose == SEX_HJ
ChgAn_SpeedInt == SPD_SLOW
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor HJ_SLOW
end
if and
ChgAn_Pose == SEX_HJ
ChgAn_SpeedInt == SPD_FAST
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor HJ_FAST
end
if ChgAn_Pose == SEX_CUNNI
then 0AB1: call_scm_func @LoadSexualPose 3 ChgAn_Actress ChgAn_Actor CUNNILINGUS
end
0AB2: ret 0

{======================================================================
======================================================================}
:SexualMenuMachine
Dummy1 = SX_MENU_LEFT_FIRST_ELEM
Dummy1 += 3

// Is current selector on left menu items ?
if and
001D:   Dummy1 > SexMenuState  // (int)
0029:   SexMenuState >= SX_MENU_LEFT_FIRST_ELEM
then
    if PulseButtonState == PULSED_UP
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 1 3 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 1 3 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then SexMenuState *= 20
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = 0 // Close Sexual Menu
    end
end

Dummy1 = SX_MENU_OPT1_PLACES_FIRST_ELEM
Dummy1 += 7

// Is current selector on opt 1 right menu items ?
if and
001D:   Dummy1 > SexMenuState  // (int)
0029:   SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
then
    if PulseButtonState == PULSED_UP
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 20 26 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 20 26 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then // TODO
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

Dummy1 = SX_MENU_OPT2_POSITIONS_FIRST_ELEM
Dummy1 += 10

// Is current selector on opt 2 right menu items ?
if and
001D:   Dummy1 > SexMenuState  // (int)
0029:   SexMenuState >= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
then
    if PulseButtonState == PULSED_UP
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 40 49 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 40 49 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then // TODO
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

Dummy1 = SX_MENU_OPT3_CAMERAS_FIRST_ELEM
Dummy1 += 10

// Is current selector on opt 3 right menu items ?
if and
001D:   Dummy1 > SexMenuState  // (int)
0029:   SexMenuState >= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
then
    if PulseButtonState == PULSED_UP
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 60 69 FLAG_INCREASE SexMenuState
    end
    if PulseButtonState == PULSED_DOWN
    then 0AB1: call_scm_func @IncOrDecIntVariable 4 SexMenuState 60 69 FLAG_DECREASE SexMenuState
    end
    if PulseButtonState == PULSED_SPRINT
    then // TODO
    end
    if PulseButtonState == PULSED_ENTER_VEH
    then SexMenuState = SX_MENU_LEFT_FIRST_ELEM
    end
end

if or
PulseButtonState == PULSED_UP
PulseButtonState == PULSED_DOWN
PulseButtonState == PULSED_SPRINT
PulseButtonState == PULSED_ENTER_VEH
then PulseButtonState++
end
return

{======================================================================
======================================================================}
:PulseButtonMachine
const
    MoveAxisX = Dummy1
    MoveAxisY = Dummy2
    SpAxisX = Dummy3
    SpAxisY = Dummy4
end

0494: get_joystick 0 data_to MoveAxisX MoveAxisY SpAxisX SpAxisY

if PulseButtonState == NOT_PULSED
then
    if 0018:   MoveAxisY > 100 // UP
    then PulseButtonState = PULSED_UP
    end
    if 001A:   -100 > MoveAxisY // DOWN
    then PulseButtonState = PULSED_DOWN
    end
    if 00E1:   player 0 pressed_key PAD_SPRINT
    then PulseButtonState = PULSED_SPRINT
    end
    if 00E1:   player 0 pressed_key PAD_ENTER_VEHICLE
    then PulseButtonState = PULSED_ENTER_VEH
    end
end

if and
PULSED_UP_F >= PulseButtonState
PulseButtonState > PULSED_UP
then
    if 8018:   not MoveAxisY > 100 // UP
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_DOWN_F >= PulseButtonState
PulseButtonState > PULSED_DOWN
then
    if 801A:   not -100 > MoveAxisY // DOWN
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_SPRINT_F >= PulseButtonState
PulseButtonState > PULSED_SPRINT
then
    if 80E1:   not player 0 pressed_key PAD_SPRINT
    then PulseButtonState = NOT_PULSED
    end
end

if and
PULSED_ENTER_VEH_F >= PulseButtonState
PulseButtonState > PULSED_ENTER_VEH
then
    if 80E1:   not player 0 pressed_key PAD_ENTER_VEHICLE
    then PulseButtonState = NOT_PULSED
    end
end
return

